# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=libldac
pkgver=2.0.2.3
pkgrel=2.2
pkgdesc="LDAC Bluetooth encoder library"
url="https://github.com/EHfive/ldacBT"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(Apache-2.0)
depends=(glibc)
makedepends=(git cmake)
provides=(libldacBT_{abr,enc}.so)
_commit=fbffba45d15d959da6ee04eafe14c0d4721f6030  # tags/v2.0.2.3
source=("git+$url#commit=$_commit"
        "git+https://gitlab.com/eh5/libldac.git"
        big-endian.patch)
sha256sums=('d2a53c44c8c45c81276608e6dad66c781fa3f185c7c42d46088e8e4579f3526e'
            'SKIP'
            '3b5df24599b3ba893e7fbb5f38b573b9013fc7828cb8cdcc5dc7d6fb85b58ba2')

pkgver() {
  cd ldacBT
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd ldacBT

  git submodule init
  git submodule set-url libldac "$srcdir/libldac"
  git -c protocol.file.allow=always submodule update

  patch -Np1 -i ${srcdir}/big-endian.patch
}

build() {
  cmake -S ldacBT -B build \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DLDAC_SOFT_FLOAT=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
