# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Daurnimator <daurnimator@archlinux.org>
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Chris Brannon <chris@the-brannons.com>
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Contributor: Anders Bergh <anders1@gmail.com>

pkgname=luajit
# LuaJIT has a "rolling release" where you should follow git HEAD
_commit=4da850743b197a7e864ab811ec3dce7204040a7b
# The patch version is the timestamp of the above git commit, obtain via `git show -s --format=%ct`
_ct=1698135991
pkgver="2.1.${_ct}"
pkgrel=1
pkgdesc='Just-in-time compiler and drop-in replacement for Lua 5.1'
arch=(x86_64 riscv64)
url='https://luajit.org/'
license=('MIT')
depends=('gcc-libs')
source=("LuaJIT-${_commit}.tar.gz::https://github.com/infiWang/LuaJIT/archive/${_commit}.tar.gz")
md5sums=('e92ce6c7b86c452a1be3f014ac57c685')
sha256sums=('24369db7e0be21ff9f70a43ef3ef4622fdc45883c526cb717f7b7281ea66c065')
b2sums=('31ffed44719ff45b6bab028e1cd26cc7a977feec68585e1bfaeb9fa5cfeca15eaa80576881cb40bd0453b8f344a5e3a2613a2e0a0be9f7247e717ff83a1510ac')

build() {
  cd "LuaJIT-${_commit}"

  # Avoid early stripping
  make amalg PREFIX=/usr BUILDMODE=dynamic TARGET_STRIP=" @:"
}

check() {
  cd "LuaJIT-${_commit}"

  # Make sure that _ct was updated
  test "${_ct}" == "$(cat .relver)"
}

package() {
  cd "LuaJIT-${_commit}"

  make install DESTDIR="$pkgdir" PREFIX=/usr
  install -Dm644 COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/COPYRIGHT"
}
