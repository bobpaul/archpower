# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: bender02 at gmx dot com

pkgname=nettle
pkgver=3.7.2
pkgrel=1
pkgdesc="A low-level cryptographic library"
arch=(x86_64 powerpc64le powerpc)
url="https://www.lysator.liu.se/~nisse/nettle"
license=('GPL2')
depends=('gmp')
provides=('libnettle.so' 'libhogweed.so')
checkdepends=('valgrind')
source=(https://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.gz{,.sig})
sha256sums=('8d2a604ef1cde4cd5fb77e422531ea25ad064679ff0adf956e78b3352e0ef162'
            'SKIP')
validpgpkeys=('343C2FF0FBEE5EC2EDBEF399F3599FF828C67298') # Niels MÃ¶ller <nisse@lysator.liu.se>


build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
    --disable-static
  make
}

check() {
  cd $pkgname-$pkgver
  make -k check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir/" install
}
