# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=sbc
pkgver=1.5
pkgrel=2
pkgdesc="Bluetooth Subband Codec (SBC) library"
url="https://git.kernel.org/pub/scm/bluetooth/sbc.git"
arch=(x86_64 powerpc64le powerpc riscv64)
license=(GPL LGPL)
depends=(glibc)
makedepends=(git)
provides=(libsbc.so)
_commit=b2c20a3ecf5ed00f63c94077a9762b505935ede9  # tags/1.5^0
source=("git+$url#commit=$_commit"
        fix-build-on-non-x86.patch)
sha512sums=('SKIP'
            '0802b4369a6204200235d2c5b0b8c6f70d4efa5579d803139eb27c83d8568a48df2c17914e566f55a95edb541120b5cc8f9082b7510f13070cd6d95324cf00bc')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  patch -Np1 -i ${srcdir}/fix-build-on-non-x86.patch
  autoreconf -fvi
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --disable-static --disable-tester
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
