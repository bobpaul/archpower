# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer:
# Contributor: kevku <kevku@gmx.com>

pkgname=xsd
pkgver=4.0.0
_pkgver=4.0.0+dep
pkgrel=4
pkgdesc="An open-source, cross-platform W3C XML Schema to C++ data binding compiler"
arch=(x86_64 powerpc64le powerpc)
url="https://www.codesynthesis.com/products/xsd"
license=('GPL2')
depends=('xerces-c')
makedepends_powerpc=(gcc6)
source=("https://www.codesynthesis.com/download/${pkgname}/4.0/${pkgname}-${_pkgver}.tar.bz2"
        "xsdcxx.patch"
        "C++Parser_Expat_support.patch"
        "Fix_path_handling_bug.patch"
        "xerces-c3.2.patch"
)
sha256sums=('eca52a9c8f52cdbe2ae4e364e4a909503493a0d51ea388fc6c9734565a859817'
            '93f2e6a9dc942cf1fcab7edbe9c38fc102cbaf86f8b667b70aad4e8e2d1e2fd3'
            'f3e984fdc1cae51e2394170c69f6d0c37e1e82182d8b7b202e458ec0afcf2e1d'
            'be445dd180038df42fa4610737406e4405f4517311fe726e993a9fc8914f5247'
            'bd228182441245d3572ec6f64c77fe622d4fc99857a2a4d1f72bda461ba43475')

prepare() {
  cd ${pkgname}-${_pkgver}
  patch -p1 -i "${srcdir}/xsdcxx.patch"
  patch -p1 -i "${srcdir}/C++Parser_Expat_support.patch"
  patch -p1 -i "${srcdir}/Fix_path_handling_bug.patch"
  patch -p1 -i "${srcdir}/xerces-c3.2.patch"
}

build() {
  cd ${pkgname}-${_pkgver}

  case "${CARCH}" in
    powerpc) export CXX=g++-6 ;;
  esac

  make CXXFLAGS="${CXXFLAGS} -Wno-error -std=c++0x"
}

package() {
  cd ${pkgname}-${_pkgver}
  make install_prefix="${pkgdir}/usr" install

  # Fix conflicts with mono
  mv "${pkgdir}"/usr/bin/xsd{,cxx}
  mv "${pkgdir}"/usr/share/man/man1/xsd{,cxx}.1
}
