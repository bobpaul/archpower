# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Aleksey Filippov <sarum9in@gmail.com>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>
# Contributor: Geoffroy Carrier <geoffroy@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgbase='protobuf'
pkgname=('protobuf' 'python-protobuf')
pkgver=21.3
pkgrel=1
pkgdesc="Protocol Buffers - Google's data interchange format"
arch=(x86_64 powerpc64le powerpc riscv64)
url='https://developers.google.com/protocol-buffers/'
license=('BSD')
depends=('gcc-libs' 'glibc' 'zlib')
makedepends=('python-setuptools' 'cmake' 'gtest')
source=(https://github.com/protocolbuffers/protobuf/archive/v$pkgver/$pkgname-$pkgver.tar.gz
        protobuf-assertion-powerpc.patch)
sha512sums=('0bbce4dca03c554d5ada37055a3f9eb009c8413046a29211b751a87936330be32f2c2d18ae3c3d4cf50c9f29f5adb1142037140fafec116db9ee4941b3f8805d')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  [ "${CARCH}" = 'powerpc' ] && patch -Np1 -i ${srcdir}/protobuf-assertion-powerpc.patch
}

build() {
  case "${CARCH}" in
    powerpc) export LDFLAGS+=' -latomic'
  esac

  cmake -B build -S $pkgbase-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -Dprotobuf_USE_EXTERNAL_GTEST=ON \
    -Dprotobuf_BUILD_SHARED_LIBS=ON
  cmake --build build

  ln -rs build $pkgbase-$pkgver/src/.libs
  cd $pkgbase-$pkgver/python
  PROTOC="$srcdir"/build/protoc \
  python setup.py build --cpp_implementation
}

check() {
  cmake --build build --target check

  cd $pkgbase-$pkgver/python
  python setup.py test
}

package_protobuf() {
  replaces=('protobuf-cpp')
  provides=('libprotoc.so' 'libprotobuf.so' 'libprotobuf-lite.so')

  DESTDIR="$pkgdir" cmake --install build

  cd $pkgbase-$pkgver
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -vDm 644 editors/protobuf-mode.el \
    -t "$pkgdir/usr/share/emacs/site-lisp/"
  install -vDm 644 editors/proto.vim \
    -t "${pkgdir}/usr/share/vim/vimfiles/syntax"
}

package_python-protobuf() {
  pkgdesc='Python 3 bindings for Google Protocol Buffers'
  depends=('python' 'python-six' "protobuf=$pkgver")

  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
  cd "$pkgbase-$pkgver"
  (
    cd python
    PYTHONPATH="$PWD/build/lib.linux-$CARCH-${python_version}:PYTHONPATH" \
    python setup.py install --skip-build \
                            --cpp_implementation \
                            --optimize=1 \
                            --root="$pkgdir"
  )
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
sha512sums=('0bbce4dca03c554d5ada37055a3f9eb009c8413046a29211b751a87936330be32f2c2d18ae3c3d4cf50c9f29f5adb1142037140fafec116db9ee4941b3f8805d'
            'e8f6dea62a4b5d000838cc0502f72ceadcbb956d6fd648d61ac3056eb1709da7ac28ffe46752980918584f3b4844cc2a0a91df9771a9acce6c302f542935134d')
