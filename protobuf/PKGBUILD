# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Aleksey Filippov <sarum9in@gmail.com>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>
# Contributor: Geoffroy Carrier <geoffroy@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgbase='protobuf'
pkgname=('protobuf' 'python-protobuf')
pkgver=3.15.6
pkgrel=1
pkgdesc="Protocol Buffers - Google's data interchange format"
arch=(x86_64 powerpc64le powerpc)
url='https://developers.google.com/protocol-buffers/'
license=('BSD')
depends=('gcc-libs' 'glibc' 'zlib')
makedepends=('unzip' 'python-setuptools')
source=("https://github.com/protocolbuffers/$pkgname/releases/download/v$pkgver/$pkgname-all-$pkgver.tar.gz")
sha512sums=('1d3695cf2afa9181cec190d435615780353ecf7422046f517dfe1316ee92cca01676be0008481c80009912a3df0cd1aeaaea40ffcc3a569ccf0831d8b5e35ab0')

prepare() {
  cd "$pkgbase-$pkgver"
  autoreconf -vfi

# Don't fail tests on deprecation warnings
  sed -e "/DeprecationWarning/d" -i python/google/protobuf/internal/*test.py
}

build() {
  cd "$pkgbase-$pkgver"
  ./configure --prefix=/usr
  make
  cd python
  python setup.py build --cpp_implementation
}

check() {
  cd "$pkgbase-$pkgver"

  # https://bugzilla.redhat.com/show_bug.cgi?id=1838470
  # https://github.com/protocolbuffers/protobuf/issues/7514
  make check CXXFLAGS+=' -Wno-error=type-limits'

  cd python
  python setup.py test
}

package_protobuf() {
  replaces=('protobuf-cpp')
  provides=('libprotoc.so' 'libprotobuf.so' 'libprotobuf-lite.so')

  cd "$pkgbase-$pkgver"
  make DESTDIR="$pkgdir" install

  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -vDm 644 editors/protobuf-mode.el \
    -t "$pkgdir/usr/share/emacs/site-lisp/"
  install -vDm 644 editors/proto.vim \
    -t "${pkgdir}/usr/share/vim/vimfiles/syntax"
}

package_python-protobuf() {
  pkgdesc='Python 3 bindings for Google Protocol Buffers'
  depends=('python' 'python-six' "protobuf=$pkgver")

  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
  cd "$pkgbase-$pkgver"
  (
    cd python
    PYTHONPATH="$PWD/build/lib.linux-$CARCH-${python_version}:PYTHONPATH" \
    python setup.py install --skip-build \
                            --cpp_implementation \
                            --optimize=1 \
                            --root="$pkgdir"
  )
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
