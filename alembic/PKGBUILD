# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>

pkgname=alembic
pkgver=1.8.0
pkgrel=3
pkgdesc="An open framework for storing and sharing scene data"
arch=(x86_64 powerpc64le powerpc)
url="http://www.alembic.io/"
license=('BSD')
depends=('imath' 'boost-libs' 'hdf5')
makedepends=('cmake' 'boost' 'ninja')
source=($pkgname-$pkgver.tar.gz::https://github.com/alembic/alembic/archive/${pkgver}.tar.gz)
sha512sums=('23fc881863741f7fd081342f5c53c1ec3d00ab300ba8cd10e4a659ee820a9ed1244cf7cf2fd40482f6be32b9d7df44128363246e942bd03c802f3a2a81533b57')

build() {
  cd "${pkgname}-${pkgver}"

  [ "${CARCH}" == 'powerpc' ] && EXTRA_CMAKE_FLAGS='-DCMAKE_CXX_FLAGS="-latomic"'

  cmake . \
    -Bbuild \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_HDF5=ON ${EXTRA_CMAKE_FLAGS}

  ninja -C build
}

package() {
  cd "${pkgname}-${pkgver}"

  install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

  DESTDIR="${pkgdir}/" ninja -C build install
}

# vim:set ts=2 sw=2 et:
