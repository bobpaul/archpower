# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=qt5-declarative
_basever=5.15.13
pkgver=5.15.13+kde+r30
pkgrel=1
_commit=b99568135aa60de96ca2e121dc2e8d83fb1ca886
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://www.qt.io'
license=('GPL3' 'LGPL3' 'FDL' 'custom')
pkgdesc='Classes for QML and JavaScript languages'
depends=('qt5-base')
makedepends=('python' 'vulkan-headers' 'git')
groups=('qt5')
conflicts=('qtchooser')
_pkgfqn=qtdeclarative
_pkgfqn=${pkgname/5-/}
source=(git+https://invent.kde.org/qt/qt/$_pkgfqn#commit=$_commit
        qtdeclarative-5.15.2-riscv-atomic.patch
        big-endian-qtquick-software.patch)
sha256sums=('7ed5dd98f8123b7678d347ff6b55d7475a1af00b3f5d4d7476029c12a241df41'
            '4e5dbd2c0afa081aa6aa7bc70ab205bbdfd86f91e574183f1a7a98372d31d680'
            '39cc449ad6b5303773902094d86fad27e814922c4e63c5f7bd175a909ae21728')

pkgver() {
  cd $_pkgfqn
  echo "$_basever+kde+r"`git rev-list --count v$_basever-lts-lgpl..$_commit` | sed -e 's|+kde+r0||'
}

prepare() {
  mkdir -p build

  cd $_pkgfqn
  patch -Np1 -i ${srcdir}/qtdeclarative-5.15.2-riscv-atomic.patch
  patch -Np2 -i ${srcdir}/big-endian-qtquick-software.patch
}

build() {
  cd build

  case "${CARCH}" in
    riscv64) CFLAGS+=' -latomic'; CXXFLAGS+=' -latomic'; LDFLAGS+=' -latomic' ;;
  esac

  mkdir -p ${srcdir}/.tmp
  export TMPDIR=${srcdir}/.tmp

  qmake ../${_pkgfqn} CONFIG+=fat-static-lto
  make
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install

  # Symlinks for backwards compatibility
  for b in "$pkgdir"/usr/bin/*; do
    ln -s $(basename $b) "$pkgdir"/usr/bin/$(basename $b)-qt5
  done

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "$pkgdir/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt5-base "$pkgdir"/usr/share/licenses/${pkgname}
}
