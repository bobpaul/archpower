# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgbase=qt5-base
pkgname=(qt5-base qt5-xcb-private-headers)
_basever=5.15.12
pkgver=5.15.12+kde+r147
pkgrel=1
_commit=8907dedc858cc344d770a2e826d6acc516429540
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://www.qt.io'
license=('GPL3' 'LGPL3' 'FDL' 'custom')
pkgdesc='A cross-platform application and UI framework'
depends=('libjpeg-turbo' 'xcb-util-keysyms' 'xcb-util-renderutil' 'libgl' 'fontconfig' 'xdg-utils'
         'shared-mime-info' 'xcb-util-wm' 'libxrender' 'libxi' 'sqlite' 'xcb-util-image' 'mesa'
         'tslib' 'libinput' 'libxkbcommon-x11' 'libproxy' 'libcups' 'double-conversion' 'md4c')
makedepends=('mariadb-libs' 'unixodbc' 'postgresql-libs' 'alsa-lib' 'gst-plugins-base-libs'
             'gtk3' 'libpulse' 'cups' 'freetds' 'vulkan-headers' 'git')
optdepends=('qt5-svg: to use SVG icon themes'
            'qt5-wayland: to run Qt applications in a Wayland session'
            'postgresql-libs: PostgreSQL driver'
            'mariadb-libs: MariaDB driver'
            'unixodbc: ODBC driver'
            'freetds: MS SQL driver'
            'gtk3: GTK platform plugin'
            'perl: for fixqt4headers and syncqt')
conflicts=('qtchooser')
groups=('qt5')
_pkgfqn=${pkgbase/5-/}
options=(!distcc)
source=(git+https://invent.kde.org/qt/qt/$_pkgfqn#commit=$_commit
        work-around-pyside2-brokenness.patch
        qmake-cflags.patch
        qmake-config.patch
        big-endian-scroll.patch
        qfloat16.patch
        link_fbclient.diff
        qmenu_set_transient_parent.diff
        sql_odbc_more_unicode_checks.diff
        cast_types_for_egl_x11_test.diff
        a11y_root.diff)
sha256sums=('SKIP'
            '6a4ec2bfcf4e7cff73346762b252cc28be5ca0ed79fde5e69350efe229b43adc'
            '5411edbe215c24b30448fac69bd0ba7c882f545e8cf05027b2b6e2227abc5e78'
            '4abc22150fa3e06b2fdcec32146abc9be4e316692aa4d5bd5aa53b4b726783fa'
            '797c1930756e7a63074b7fdb85ad911f1a8e006d55008c5da4c6b39126c24a71'
            '441f7ae61e1d36af9574eea34292c02021a8ada7809cc699a177ffc39c2c9792'
            '66bf7d40b296857872d18c42af2b0e36d90ce0a5040649334cb908312386ae15'
            'b01c5df9c203396cf8d8a2ff02e2cb39f0004d2a2867709f33c2d1386ada99d8'
            '302001e3959d790e9d881decb0b9c49e86511d6c8d84a83947688014134cad0b'
            '01ddfcafae74667433f03fafac7af561bccaf44eb1bbaa9acd91173f5bc45ef8'
            '24bc960dd400090a704fdc66a698a79e07417fe532ec0a017da94d6bf5c10989')

pkgver() {
  cd $_pkgfqn
  echo "$_basever+kde+r"`git rev-list --count v$_basever-lts-lgpl..$_commit` | sed -e 's|+kde+r0||'
}

prepare() {
  cd ${_pkgfqn}

  patch -p1 < ../work-around-pyside2-brokenness.patch
  patch -p1 < ../qmake-cflags.patch # Use system CFLAGS in qmake
  patch -p1 < ../qmake-config.patch # Don't strip binaries with qmake and use -ltcg, cf. QTBUG-73834

  patch -p2 < ../big-endian-scroll.patch
  patch -p1 < ../qfloat16.patch
  patch -p1 < ../link_fbclient.diff
  patch -p1 < ../qmenu_set_transient_parent.diff
  patch -p1 < ../sql_odbc_more_unicode_checks.diff
  patch -p1 < ../cast_types_for_egl_x11_test.diff
  patch -p1 < ../a11y_root.diff
}

build() {
  cd ${_pkgfqn}

  ./configure -confirm-license -opensource -v \
    -prefix /usr \
    -docdir /usr/share/doc/qt \
    -headerdir /usr/include/qt \
    -archdatadir /usr/lib/qt \
    -datadir /usr/share/qt \
    -sysconfdir /etc/xdg \
    -examplesdir /usr/share/doc/qt/examples \
    -plugin-sql-{psql,mysql,sqlite,odbc} \
    -system-sqlite \
    -openssl-linked \
    -nomake examples \
    -no-rpath \
    -dbus-linked \
    -system-harfbuzz \
    -journald \
    -libproxy \
    -no-mimetype-database \
    -no-use-gold-linker \
    -no-reduce-relocations \
    -no-strip \
    -ltcg
# No configure flag for fat static libs with lto
  bin/qmake CONFIG+=fat-static-lto -- -redo
  make
}

package_qt5-base() {
  pkgdesc='A cross-platform application and UI framework'
  depends+=(qt5-translations)

  cd ${_pkgfqn}
  make INSTALL_ROOT="${pkgdir}" install

  install -Dm644 LICENSE* -t "$pkgdir"/usr/share/licenses/$pkgbase

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "${pkgdir}/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  # Fix wrong qmake path in pri file
  sed -i "s|${srcdir}/${_pkgfqn}|/usr|" \
    "${pkgdir}"/usr/lib/qt/mkspecs/modules/qt_lib_bootstrap_private.pri

  # Symlinks for backwards compatibility
  for b in "${pkgdir}"/usr/bin/*; do
    ln -s $(basename $b) "${pkgdir}"/usr/bin/$(basename $b)-qt5
  done
}

package_qt5-xcb-private-headers() {
  pkgdesc='Private headers for Qt5 Xcb'

  depends=("qt5-base=$pkgver")
  optdepends=()
  groups=()
  conflicts=()

  cd ${_pkgfqn}
  install -d -m755 "$pkgdir"/usr/include/qtxcb-private
  cp -r src/plugins/platforms/xcb/*.h "$pkgdir"/usr/include/qtxcb-private/
}
