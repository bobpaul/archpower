# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=qt5-webengine
_basever=5.15.10
pkgver=5.15.14
pkgrel=4
arch=(x86_64 powerpc64le) #FIXME: powerpc64)
url='https://www.qt.io'
license=('LGPL3' 'LGPL2.1' 'BSD')
pkgdesc='Provides support for web applications using the Chromium browser project'
depends=('qt5-webchannel' 'qt5-location' 'libxcomposite' 'libxrandr' 'pciutils' 'libxss' 'libxkbfile' 
         'libevent' 'snappy' 'nss' 'libxslt' 'minizip' 'ffmpeg' 're2' 'libvpx' 'libxtst' 'ttf-font')
makedepends=('git' 'python' 'gperf' 'jsoncpp' 'ninja' 'qt5-tools' 'poppler' 'pipewire' 'nodejs' 'clang' 'gn')
optdepends=('pipewire: WebRTC desktop sharing under Wayland')
groups=('qt' 'qt5')
_pkgfqn=${pkgname/5-/}
source=(git+https://code.qt.io/qt/qtwebengine.git#tag=v${pkgver}-lts
        git+https://code.qt.io/qt/qtwebengine-chromium.git
        git+https://chromium.googlesource.com/catapult#commit=5eedfe23148a234211ba477f76fc2ea2e8529189
        qt5-webengine-ffmpeg5.patch
        xxxx-skia-bigendian.patch
        xxxx-ppc64le-sandbox-linux-stat.patch
        xxxx-qtwebengine-ppc64.patch
        qtwebengine-5.15.2-enable-ppc64.patch
        qtwebengine-5.15.2_p20210521-clang-libc++.patch
        001-disable-fatal-warnings.patch
        002-extra-gn.patch
        003-chromium-87-v8-icu68.patch
        004-disable-git.patch
        005-pdfium-system-lcms2.patch
        006-clang14.patch
        007-gcc12-includes.patch
        008-jumbo-build.patch
        009-build-chromium-without-python-2.patch
        010-build-without-python-2.patch
        011-chromium-drop-catapult.patch
        012-drop-catapult.patch
        013-port-to-pipewire-0.3.patch
        014-widevine.patch
        016-python3.11.patch)
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'c50d3019626183e753c53a997dc8a55938847543aa3178d4c51f377be741c693'
            'c331c74cecf8833b2d262ffa05a6ecb63c5a51a23dc008c21f7d2efd0eb06ebc'
            '6f6a39bcf7c4739341d4b9910553b72d5afef07abb99473859141d312f239bb2'
            '76d99f3ed901581c756bb8b5c74513ab944840e680ac310e5463116d3cf4551e'
            '6a859d37d2971bee39792f6a0e69ed1fed986400070c1f795c147d89c7ad948e'
            '64f786a919c7c145033139e6effe236459dad7b7bb5b52428e00fc70592ce05d'
            '19dac26d6753f4cbe1fe2bcb911d8124ea082f55ff86b45cdcba294d3c199249'
            'a4b567637278307c6ae9fa0245937f2d13a4b714159e2a5a5c307d3a312b251f'
            '4bd5364a5f45c68e0bd452dad7a6f638142503da8c43aaeb15d577aa09369c7c'
            'ccba1dbcd42931554782524a9ce8215c47e4c96e1587940a864d292690334c63'
            '391f899137cabf4c90ff3bc3a1cf9687d6a273e7bd42618760e7f09c7e0d3e32'
            '544bfb64044501847f62d9eaacf7a6f701109aa2548d343a10f66c90fe930a33'
            '235450cdde1adfdd4694005185d93950d9e51016b40bdfab66d4be333b9e3e28'
            'bcdd2d5912f34edc3dd34a3af9f470c622372d9d3066b49e53b64ec4d8f3b795'
            '06f1d2febebebaeae390f11081dc9fbe89873a4906c55039dc22c20043922355'
            'cb8d33ae0a73d88a8ae28baf6082a73b967fb6aecdc9ab081007075dc81966b1'
            '116e07065bebf8fbd265ad83bb74815db5e19e806db32e9d74a13b38d541788f'
            'd1240a8da8929a19fafc22d01f852564ba4ab8ea4a84554b2ac2b410477b8499'
            '6c8018c69fff486ec4c9657d07cc6ad0cef6fd3afdabb48c39ff0c2aa489feb0'
            'fb1ebbfca6095e2735b28dcb728a48942239f4afd55f51487eff9a7b243f470a'
            '652a612144ef4d87b6b2a4098f56ba6db1201e1a241259d0a227123cb0e566a2')

prepare() {
  mkdir -p build

  cd ${_pkgfqn}
  git submodule init
  git submodule set-url src/3rdparty "$srcdir"/qtwebengine-chromium
  git submodule set-branch --branch 87-based src/3rdparty
  git -c protocol.file.allow=always submodule update

  patch -d src/3rdparty -Np1 -i ${srcdir}/qt5-webengine-ffmpeg5.patch

# Gentoo patchset which supporting more platforms
#  https://dev.gentoo.org/~asturm/distfiles/qtwebengine-5.15.8_p20230313-patchset.tar.xz
  patch -Np1 -i ${srcdir}/qtwebengine-5.15.2-enable-ppc64.patch
  patch -Np1 -i ${srcdir}/qtwebengine-5.15.2_p20210521-clang-libc++.patch
  patch -Np1 -i ${srcdir}/001-disable-fatal-warnings.patch
  patch -Np1 -i ${srcdir}/002-extra-gn.patch
  patch -Np1 -i ${srcdir}/003-chromium-87-v8-icu68.patch
  patch -Np1 -i ${srcdir}/004-disable-git.patch
  patch -Np1 -i ${srcdir}/005-pdfium-system-lcms2.patch
  patch -Np1 -i ${srcdir}/006-clang14.patch
  patch -Np1 -i ${srcdir}/007-gcc12-includes.patch
  patch -Np1 -i ${srcdir}/008-jumbo-build.patch
  patch -Np1 -i ${srcdir}/009-build-chromium-without-python-2.patch
  patch -Np1 -i ${srcdir}/010-build-without-python-2.patch
  patch -Np1 -i ${srcdir}/011-chromium-drop-catapult.patch
  patch -Np1 -i ${srcdir}/012-drop-catapult.patch
  patch -Np1 -i ${srcdir}/013-port-to-pipewire-0.3.patch
  patch -Np1 -i ${srcdir}/014-widevine.patch
  patch -Np1 -i ${srcdir}/016-python3.11.patch

  patch -Np1 -i ${srcdir}/xxxx-ppc64le-sandbox-linux-stat.patch
  patch -Np1 -i ${srcdir}/xxxx-qtwebengine-ppc64.patch

# Yolo big-endian
  [ "${CARCH}" = 'powerpc64' ] && patch -Np1 -i ${srcdir}/xxxx-skia-bigendian.patch

# Update catapult for python3 compatibility
  rm -r src/3rdparty/chromium/third_party/catapult
  mv "$srcdir"/catapult src/3rdparty/chromium/third_party
}

build() {
  cd build

  pushd ${srcdir}/qtwebengine/src/3rdparty/chromium/third_party/libvpx
  mkdir -p source/config/linux/ppc64
  mkdir -p source/libvpx/test
  touch source/libvpx/test/test.mk
  ./generate_gni.sh
  popd

  case "${CARCH}" in
    powerpc64)
      export CFLAGS+=" -DI_ACKNOWLEDGE_SKIA_DOES_NOT_SUPPORT_BIG_ENDIAN=1"
      export CXXFLAGS+=" -DI_ACKNOWLEDGE_SKIA_DOES_NOT_SUPPORT_BIG_ENDIAN=1"
    ;;
  esac

  qmake ../${_pkgfqn} CONFIG+=force_debug_info -- \
    -proprietary-codecs \
    -system-ffmpeg \
    -webp \
    -spellchecker \
    -webengine-icu \
    -webengine-kerberos \
    -webengine-webrtc-pipewire
  make
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "$pkgdir/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  install -Dm644 "$srcdir"/${_pkgfqn}/src/3rdparty/chromium/LICENSE "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE.chromium

  # Fix cmake dependency versions
  sed -e "s|$pkgver\ |$_basever |" -i "$pkgdir"/usr/lib/cmake/*/*Config.cmake
}
