# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=chromium
pkgver=93.0.4577.82
pkgrel=2
_launcher_ver=8
_gcc_patchset=6
pkgdesc="A web browser built for speed, simplicity, and security"
arch=(x86_64 powerpc64le)
url="https://www.chromium.org/Home"
license=('BSD')
depends=('gtk3' 'nss' 'alsa-lib' 'xdg-utils' 'libxss' 'libcups' 'libgcrypt'
         'ttf-liberation' 'systemd' 'dbus' 'libpulse' 'pciutils' 'libva'
         'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('python' 'gn' 'ninja' 'clang' 'lld' 'gperf' 'nodejs' 'pipewire'
             'java-runtime-headless')
optdepends=('pipewire: WebRTC desktop sharing under Wayland'
            'kdialog: support for native dialogs in Plasma'
            'org.freedesktop.secrets: password storage backend on GNOME / Xfce'
            'kwallet: support for storing passwords in KWallet on Plasma')
source=(https://commondatastorage.googleapis.com/chromium-browser-official/$pkgname-$pkgver.tar.xz
        https://github.com/foutrelis/chromium-launcher/archive/v$_launcher_ver/chromium-launcher-$_launcher_ver.tar.gz
        https://github.com/stha09/chromium-patches/releases/download/chromium-${pkgver%%.*}-patchset-$_gcc_patchset/chromium-${pkgver%%.*}-patchset-$_gcc_patchset.tar.xz
        linux-sandbox-syscall-broker-use-struct-kernel_stat.patch
        linux-sandbox-fix-fstatat-crash.patch
        replace-blacklist-with-ignorelist.patch
        sql-make-VirtualCursor-standard-layout-type.patch
        chromium-93-ffmpeg-4.4.patch
        use-oauth2-client-switches-as-default.patch
        chromium-harfbuzz-3.0.0.patch
        skia-harfbuzz-3.0.0.patch
        0013-chromium-breakpad-fix-build-with-glibc-2.34.patch
        xxx-ppc64le-libvpx.patch
        xxx-ppc64le-sandbox-linux-stat.patch
        xxx-ppc64le-support.patch
        xxx-ppc64le-swiftshader.patch
        sandbox-sched_getparam.patch
        sandbox-membarrier.patch
        fix-narrowing-cast.patch
        remove-strip_binary.patch
        libc_malloc.patch
        browser-size_t.patch
        chromium-revert-drop-of-system-java.patch
        chromium-system-nodejs.patch
        chromium-webrtc-r0.patch
        cursed^Uscoped_file.patch
        no-execinfo.patch
        no-getcontext.patch
        webrtc-include.patch
        webrtc-size_t.patch)
sha256sums=('5d66214858fcba11a8f733d7a6fab61ed10e13e7df4ed37e63b66a0370fb2853'
            '213e50f48b67feb4441078d50b0fd431df34323be15be97c55302d3fdac4483a'
            'a44ffd9e25fcbd8b3cc778871890e4da6fe12600ad549c807e1d03f61f0cdf73'
            '268e18ad56e5970157b51ec9fc8eb58ba93e313ea1e49c842a1ed0820d9c1fa3'
            '253348550d54b8ae317fd250f772f506d2bae49fb5dc75fe15d872ea3d0e04a5'
            'd3344ba39b8c6ed202334ba7f441c70d81ddf8cdb15af1aa8c16e9a3a75fbb35'
            'dd317f85e5abfdcfc89c6f23f4c8edbcdebdd5e083dcec770e5da49ee647d150'
            '1a9e074f417f8ffd78bcd6874d8e2e74a239905bf662f76a7755fa40dc476b57'
            'e393174d7695d0bafed69e868c5fbfecf07aa6969f3b64596d0bae8b067e1711'
            '7ce947944a139e66774dfc7249bf7c3069f07f83a0f1b2c1a1b14287a7e15928'
            'dae11dec5088eb1b14045d8c9862801a342609c15701d7c371e1caccf46e1ffd'
            'd1ad185e25a62d50bbed0dbf131e52bead8a52ccc63a53642b3a4f62056a5458'
            '6b7ad5cd5d7d61b5441810688fc5bd99cb8198291b2252451d352a620a2e09bb'
            '16361d737c14135e464bf52e09d0b79c0e9ef84c364253e08f435e8e72d7b5c9'
            '043e007bcbd1d324b81cb1f82971bbd006edba3f4e144552c389e17d8f7a1979'
            'be9416182beb25f0835bae1dad5a26786f694dbda2e5106f83714be42add653a'
            'f5961ae09ddbdda69713d82e631bc3dab4884629f32f60ba201189c2c4269b39'
            'ba514c06cc03d728d2c1727fac85f2bbe953fe9720dcf8c2fa8de0ae4385e33b'
            '771395e3e84a4a1627945112b8c100c898221e04d86b76d4cff946530010e1a4'
            '89c4512db46368809f45f4e092279e87104ba5a7c6a812d001c606b0efd8855b'
            '612543dcef7fc6e45e1be2f3107d55d6194b20d39b7fd9f128050273eee91f40'
            'd2028d47da62f054eb83ab692f8801d726f87d22c9a96e7b96c779ed0aba9287'
            '4f74164ad790b00389565c638bf3e752017ca7ef4bb77a86a17b6e498de501de'
            '9dc885b7d719a7cf87f8d3ddcc33184b08bfd9827f696f00d194bfbead39f63d'
            '376bfa23760896cb006b2c5718a86c11ec8c62879fe3d9a5d409e8b89185e42d'
            '6cfec4a32160df642ffa4b3c1e10ba19e94f1004bac65a349fa273b0f684b8ac'
            'a76ec1a929b54d79b1fa643036fcbd9ff2c2cc99e7d7e1573122ca0b5e760347'
            '26111345b8d9bf87f7336c35aa401ff2f1b2911519e7b725e224c59f33593378'
            '80d9910ec018c517e4316419939aac9751ddae13b3cadf4712fdac770a899e81'
            '83904114e89c2b1ed55814bc07ba5a0ad198010b3cac9f223c72ac4fbac3646b')

# Possible replacements are listed in build/linux/unbundle/replace_gn_files.py
# Keys are the names in the above script; values are the dependencies in Arch
declare -gA _system_libs=(
  [ffmpeg]=ffmpeg
  [flac]=flac
  [fontconfig]=fontconfig
  [freetype]=freetype2
  [harfbuzz-ng]=harfbuzz
  [icu]=icu
  [libdrm]=
  [libjpeg]=libjpeg
  [libpng]=libpng
  #[libvpx]=libvpx
  [libwebp]=libwebp
  [libxml]=libxml2
  [libxslt]=libxslt
  [opus]=opus
  [re2]=re2
  [snappy]=snappy
  [zlib]=minizip
)
_unwanted_bundled_libs=(
  $(printf "%s\n" ${!_system_libs[@]} | sed 's/^libjpeg$/&_turbo/')
)
depends+=(${_system_libs[@]})

# Google API keys (see https://www.chromium.org/developers/how-tos/api-keys)
# Note: These are for Arch Linux use ONLY. For your own distribution, please
# get your own set of keys.
#
# Starting with Chromium 89 (2021-03-02) the OAuth2 credentials have been left
# out: https://archlinux.org/news/chromium-losing-sync-support-in-early-march/
_google_api_key=AIzaSyDwr302FpOSkGRpLlUpPThNTDPbXcIn_FM

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # Allow building against system libraries in official builds
  sed -i 's/OFFICIAL_BUILD/GOOGLE_CHROME_BUILD/' \
    tools/generate_shim_headers/generate_shim_headers.py

  # https://crbug.com/893950
  sed -i -e 's/\<xmlMalloc\>/malloc/' -e 's/\<xmlFree\>/free/' \
    third_party/blink/renderer/core/xml/*.cc \
    third_party/blink/renderer/core/xml/parser/xml_document_parser.cc \
    third_party/libxml/chromium/*.cc

  # Use the --oauth2-client-id= and --oauth2-client-secret= switches for
  # setting GOOGLE_DEFAULT_CLIENT_ID and GOOGLE_DEFAULT_CLIENT_SECRET at
  # runtime -- this allows signing into Chromium without baked-in values
  patch -Np1 -i ../use-oauth2-client-switches-as-default.patch

  # Fix build with glibc 2.34
  patch -Np2 -i ../0013-chromium-breakpad-fix-build-with-glibc-2.34.patch

  # Fix build with harfbuzz 3.0.0
  patch -Np1 -i ../chromium-harfbuzz-3.0.0.patch
  patch -Np1 -d third_party/skia -i ${srcdir}/skia-harfbuzz-3.0.0.patch

  # Fix build with older ffmpeg
  patch -Np1 -i ../chromium-93-ffmpeg-4.4.patch

  # Revert transition to -fsanitize-ignorelist (needs newer clang)
  patch -Rp1 -i ../replace-blacklist-with-ignorelist.patch

  # Upstream fixes
  patch -Np1 -i ../linux-sandbox-syscall-broker-use-struct-kernel_stat.patch
  patch -Np1 -i ../linux-sandbox-fix-fstatat-crash.patch

  # https://chromium-review.googlesource.com/c/chromium/src/+/2862724
  patch -Np1 -i ../sql-make-VirtualCursor-standard-layout-type.patch

  # Fixes for building with libstdc++ instead of libc++
  patch -Np1 -i ../patches/chromium-93-pdfium-include.patch
  patch -Np1 -i ../patches/chromium-90-ruy-include.patch
  patch -Np1 -i ../patches/chromium-93-HashPasswordManager-include.patch
  patch -Np1 -i ../patches/chromium-93-BluetoothLowEnergyScanFilter-include.patch
  patch -Np1 -i ../patches/chromium-93-ClassProperty-include.patch
  patch -Np1 -i ../patches/chromium-93-DevToolsEmbedderMessageDispatcher-include.patch
  patch -Np1 -i ../patches/chromium-93-ScopedTestDialogAutoConfirm-include.patch

  # powerpc64le support
  patch -Np1 -i ${srcdir}/xxx-ppc64le-libvpx.patch
  patch -Np1 -i ${srcdir}/xxx-ppc64le-sandbox-linux-stat.patch
  patch -Np1 -i ${srcdir}/xxx-ppc64le-support.patch
  patch -Np1 -i ${srcdir}/xxx-ppc64le-swiftshader.patch
  patch -Np1 -i ${srcdir}/sandbox-sched_getparam.patch
  patch -Np1 -i ${srcdir}/sandbox-membarrier.patch
  patch -Np1 -i ${srcdir}/fix-narrowing-cast.patch
  patch -Np1 -i ${srcdir}/remove-strip_binary.patch
  patch -Np1 -i ${srcdir}/libc_malloc.patch
  patch -Np1 -i ${srcdir}/browser-size_t.patch
  patch -Np1 -i ${srcdir}/chromium-revert-drop-of-system-java.patch
  patch -Np1 -i ${srcdir}/chromium-system-nodejs.patch
  patch -Np1 -i ${srcdir}/chromium-webrtc-r0.patch
  patch -Np1 -i ${srcdir}/cursed^Uscoped_file.patch
  patch -Np1 -i ${srcdir}/no-execinfo.patch
  patch -Np1 -i ${srcdir}/no-getcontext.patch
  patch -Np1 -i ${srcdir}/webrtc-include.patch
  patch -Np1 -i ${srcdir}/webrtc-size_t.patch

  # Link to system tools required by the build
  mkdir -p third_party/node/linux/node-linux-{x64,ppc64le}/bin
  ln -s /usr/bin/node third_party/node/linux/node-linux-x64/bin/
  ln -s /usr/bin/node third_party/node/linux/node-linux-ppc64le/bin/
  ln -s /usr/bin/java third_party/jdk/current/bin/

  pushd third_party/libvpx
  mkdir -p source/config/linux/ppc64
  # need PATH to find gn
  PATH="${wrksrc}/out/Release:$PATH" ./generate_gni.sh
  popd

  # Remove bundled libraries for which we will use the system copies; this
  # *should* do what the remove_bundled_libraries.py script does, with the
  # added benefit of not having to list all the remaining libraries
  local _lib
  for _lib in ${_unwanted_bundled_libs[@]}; do
    find "third_party/$_lib" -type f \
      \! -path "third_party/$_lib/chromium/*" \
      \! -path "third_party/$_lib/google/*" \
      \! -path "third_party/harfbuzz-ng/utils/hb_scoped.h" \
      \! -regex '.*\.\(gn\|gni\|isolate\)' \
      -delete
  done

  ./build/linux/unbundle/replace_gn_files.py \
    --system-libraries "${!_system_libs[@]}"
}

build() {
  make -C chromium-launcher-$_launcher_ver

  cd "$srcdir/$pkgname-$pkgver"

  export CC=clang
  export CXX=clang++
  export AR=ar
  export NM=nm

  unset CFLAGS CXXFLAGS LDFLAGS

  local _flags=(
    'custom_toolchain="//build/toolchain/linux/unbundle:default"'
    'host_toolchain="//build/toolchain/linux/unbundle:default"'
    'clang_use_chrome_plugins=false'
    'is_official_build=true' # implies is_cfi=true on x86_64
    'treat_warnings_as_errors=false'
    'fieldtrial_testing_like_official_build=true'
    'blink_enable_generated_code_formatting=false'
    'ffmpeg_branding="Chrome"'
    'proprietary_codecs=true'
    'rtc_use_pipewire=true'
    'link_pulseaudio=true'
    'use_gnome_keyring=false'
    'use_sysroot=false'
    'use_custom_libcxx=false'
    'enable_hangout_services_extension=true'
    'enable_widevine=true'
    'enable_nacl=false'
    'enable_jxl_decoder=false'
    "google_api_key=\"$_google_api_key\""
  )

  case "${CARCH}" in
    powerpc64le)
      _flags+=(
        'use_custom_libcxx=false'
        'use_lld=true' 
        'use_allocator="none"'
        'use_allocator_shim=false'
      )
    ;;
  esac

  if [[ -n ${_system_libs[icu]+set} ]]; then
    _flags+=('icu_use_data_file=false')
  fi

  if check_option strip y; then
    _flags+=('symbol_level=0')
  fi

  # Facilitate deterministic builds (taken from build/config/compiler/BUILD.gn)
  CFLAGS+='   -Wno-builtin-macro-redefined'
  CXXFLAGS+=' -Wno-builtin-macro-redefined'
  CPPFLAGS+=' -D__DATE__=  -D__TIME__=  -D__TIMESTAMP__='

  # Do not warn about unknown warning options
  CFLAGS+='   -Wno-unknown-warning-option'
  CXXFLAGS+=' -Wno-unknown-warning-option'

  LDFLAGS+=' -pthread'

  gn gen out/Release --args="${_flags[*]}"
  ninja ${MAKEFLAGS} -C out/Release chrome chrome_sandbox chromedriver mksnapshot
}

package() {
  cd chromium-launcher-$_launcher_ver
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -Dm644 LICENSE \
    "$pkgdir/usr/share/licenses/chromium/LICENSE.launcher"

  cd "$srcdir/$pkgname-$pkgver"

  install -D out/Release/chrome "$pkgdir/usr/lib/chromium/chromium"
  install -Dm4755 out/Release/chrome_sandbox "$pkgdir/usr/lib/chromium/chrome-sandbox"
  ln -s /usr/lib/chromium/chromedriver "$pkgdir/usr/bin/chromedriver"

  install -Dm644 chrome/installer/linux/common/desktop.template \
    "$pkgdir/usr/share/applications/chromium.desktop"
  install -Dm644 chrome/app/resources/manpage.1.in \
    "$pkgdir/usr/share/man/man1/chromium.1"
  sed -i \
    -e 's/@@MENUNAME@@/Chromium/g' \
    -e 's/@@PACKAGE@@/chromium/g' \
    -e 's/@@USR_BIN_SYMLINK_NAME@@/chromium/g' \
    "$pkgdir/usr/share/applications/chromium.desktop" \
    "$pkgdir/usr/share/man/man1/chromium.1"

  install -Dm644 chrome/installer/linux/common/chromium-browser/chromium-browser.appdata.xml \
    "$pkgdir/usr/share/metainfo/chromium.appdata.xml"
  sed -ni \
    -e 's/chromium-browser\.desktop/chromium.desktop/' \
    -e '/<update_contact>/d' \
    -e '/<p>/N;/<p>\n.*\(We invite\|Chromium supports Vorbis\)/,/<\/p>/d' \
    -e '/^<?xml/,$p' \
    "$pkgdir/usr/share/metainfo/chromium.appdata.xml"

  local toplevel_files=(
    chrome_100_percent.pak
    chrome_200_percent.pak
    resources.pak
    v8_context_snapshot.bin

    # ANGLE
    libEGL.so
    libGLESv2.so

    chromedriver
    crashpad_handler
  )

  if [[ -z ${_system_libs[icu]+set} ]]; then
    toplevel_files+=(icudtl.dat)
  fi

  cp "${toplevel_files[@]/#/out/Release/}" "$pkgdir/usr/lib/chromium/"
  install -Dm644 -t "$pkgdir/usr/lib/chromium/locales" out/Release/locales/*.pak
  install -Dm755 -t "$pkgdir/usr/lib/chromium/swiftshader" out/Release/swiftshader/*.so

  for size in 24 48 64 128 256; do
    install -Dm644 "chrome/app/theme/chromium/product_logo_$size.png" \
      "$pkgdir/usr/share/icons/hicolor/${size}x${size}/apps/chromium.png"
  done

  for size in 16 32; do
    install -Dm644 "chrome/app/theme/default_100_percent/chromium/product_logo_$size.png" \
      "$pkgdir/usr/share/icons/hicolor/${size}x${size}/apps/chromium.png"
  done

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/chromium/LICENSE"
}

# vim:set ts=2 sw=2 et:
