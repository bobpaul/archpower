# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=gdb
# gdb-common is a package that contains files common for all cross compiled versions
# of gdb (for arm/avr/...)
pkgname=(gdb gdb-common)
pkgver=11.1
pkgrel=2
pkgdesc='The GNU Debugger'
arch=(x86_64 powerpc64le powerpc riscv64)
url='https://www.gnu.org/software/gdb/'
license=(GPL3)
makedepends=(glibc gcc-libs texinfo python guile ncurses expat xz mpfr source-highlight
             readline)
source=(https://ftp.gnu.org/gnu/gdb/${pkgname}-${pkgver}.tar.xz{,.sig}
        sim-filter-out-SIGSTKSZ-PR-sim-28302.patch
        mte-detection.patch::"https://sourceware.org/git/?p=binutils-gdb.git;a=patch;h=7fd8546853e3f0333ba8d8238413aba7eb45c69f;hp=5fff6115feae7aaa23c0ae8d144e1c8418ee2ee1")

sha1sums=('6cb3361c7f2b22d6d2c25ab4992264aa41bc2471'
          'SKIP'
          '82f387d4ced66258bc0842e9ed2817f940b939f1')
b2sums=('6a639f6533f4008bfb469c23a26c4fcd039f1bc5dd33745f4876344451beb595d7a8843caeb5db70214924624c5b999f8831237d459bebf1cb2d2866f8b41ee2'
        'SKIP'
        '22d6eafef30519ad3e27726ff4847f34bf24b14b7c28d087cdd63260bdbe432628d3efcba2242c7651ad03f937acf384e363e4de50eeced838aa44d76be0efa0')
validpgpkeys=('F40ADB902B24264AA42E50BF92EDB04BFF325CF3') # Joel Brobecker

prepare() {
  cd gdb-$pkgver

  # hack! - libiberty configure tests for header files using "$CPP $CPPFLAGS"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure

  # gdb 11.1 with glibc >=2.34
  patch -Np1 -i ${srcdir}/sim-filter-out-SIGSTKSZ-PR-sim-28302.patch

  patch -p1 < ../mte-detection.patch # FS#72583
}

build() {
  cd gdb-$pkgver

  mkdir -p build && cd build
  ../configure \
    --prefix=/usr \
    --disable-nls \
    --enable-source-highlight \
    --enable-tui \
    --with-system-readline \
    --with-python=/usr/bin/python \
    --with-guile=guile-2.2 \
    --with-system-gdbinit=/etc/gdb/gdbinit
  make
}

package_gdb-common() {
  depends=(python guile)

  cd gdb-$pkgver/build
  make -C gdb/data-directory DESTDIR="$pkgdir" install
}

package_gdb() {
  depends=(glibc ncurses libncursesw.so gcc-libs expat xz mpfr source-highlight gdb-common=$pkgver
           readline libreadline.so guile python libelf)
  backup=(etc/gdb/gdbinit)

  cd gdb-$pkgver/build
  make -C gdb DESTDIR="$pkgdir" install
  make -C gdbserver DESTDIR="$pkgdir" install

  # install "custom" system gdbinit
  install -dm 755 "$pkgdir/etc/gdb"
  touch "$pkgdir/etc/gdb/gdbinit"

  # comes from gdb-common
  rm -r "$pkgdir/usr/share/gdb/"
}

# vim: ts=2 sw=2 et:
sha1sums=('6cb3361c7f2b22d6d2c25ab4992264aa41bc2471'
          'SKIP'
          '53ee4a91f59a5dff0ceb62492735923704f2d8e2'
          '1b773465886dd06f09d0e7a50b3a01de52adc754')
b2sums=('6a639f6533f4008bfb469c23a26c4fcd039f1bc5dd33745f4876344451beb595d7a8843caeb5db70214924624c5b999f8831237d459bebf1cb2d2866f8b41ee2'
        'SKIP'
        'a098ec064ebe4cc952a33b8cf274516c5aceb21457119e2336095ce312762b0cd43950e4801ac093de7d7a0c09d91adc363329ab5c56d96f5e17b5102a9ddce7'
        'f23983de92b935423d14703d3dd2c0c75f11ae95d8a5f94be929ccce501d4eeebdbc6c6108c7a625f6ed4e8c09a25bd7dcf4f864a81ef411fd3821bccf3730bd')
