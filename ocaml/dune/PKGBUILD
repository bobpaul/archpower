# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Konstantin Gizdov <arch at kge dot pw>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Jakob Gahde <j5lx@fmail.co.uk>

pkgname=dune
pkgver=2.8.4
pkgrel=1
pkgdesc="A composable build system for OCaml (formerly jbuilder)"
arch=(x86_64 powerpc64le powerpc)
url="https://github.com/ocaml/dune"
license=(Apache)
depends=(glibc "ocaml>=4.11.1" ocaml-findlib ocaml-csexp)
source=("${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('13b36eb6a943f9513987a23dab467b2e449cc3a4de35227f8323c3c724b27f19daac500602141ab22a04579243333c8484ce27a7f73e46a72e796d30fb1c3253')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    dune_release_pkgs='dune,dune-action-plugin,dune-build-info,dune-configurator,dune-glob,dune-private-libs,dune-site'
    make dune.exe
    ./dune.exe build -p $dune_release_pkgs --profile dune-bootstrap
}

# Tests requires a bunch of (currently) unpackaged dependencies
#check() {
#    cd ${pkgname}-${pkgver/b/-beta}
#    make test
#}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    # make DESTDIR="${pkgdir}" INSTALL_ARGS="--prefix=/usr --libdir='$(ocamlfind printconf destdir)'" install
    DESTDIR="${pkgdir}" ./dune.exe install --prefix "/usr" --libdir=$(ocamlfind printconf destdir)
    # Fix doc and man install
    rm -r "${pkgdir}"/usr/doc
    install -dm755 "${pkgdir}"/usr/share
    mv "${pkgdir}"/usr/{man,share/}
}
