# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: galiyosha@gmail.com
# Contributor: Jon Kristian Nilsen <jokr.nilsen@gmail.com>

pkgname=libffado
pkgver=2.4.4
pkgrel=3
pkgdesc="Driver for FireWire audio devices"
arch=(x86_64 powerpc64le powerpc riscv64)
url="http://www.ffado.org/"
license=('GPL3')
depends=('gcc-libs' 'glibc' 'glibmm' 'hicolor-icon-theme' 'libavc1394'
         'libiec61883' 'libxml++' 'python-dbus')
makedepends=('alsa-lib' 'dbus-c++' 'libconfig' 'scons')
#optdepends=('python-pyqt5: ffado-mixer')
provides=('libffado.so')
source=("http://www.ffado.org/files/${pkgname}-${pkgver}.tgz"
        disable-config-guess.patch)
sha256sums=('a47178cdc8c0c91e91edbaabe23d19ca12a752cbcf81c27314adb27cc00d60f0'
            '1979741980b71e8ff96c3ac07220fc49cb2cd8106398cf3826abd5e200ea1e3a')

prepare() {
  cd "${pkgname}-${pkgver}"

  #patch -Np1 -i ${srcdir}/disable-config-guess.patch
  cp /usr/share/autoconf/build-aux/config.guess admin/

  # Bugs #658052, #659226
  sed -i -e 's/^CacheDir/#CacheDir/' SConstruct

  # fix icon name, so it's coherent with XDG desktop file name
  sed -e 's/hi64-apps-ffado/ffado-mixer/g' \
      -i support/xdg/ffado.org-ffadomixer.desktop
  # fix id, so it's coherent with XDG desktop file name
  sed -e 's/ffado.org-ffadomixer.desktop/ffado-mixer.desktop/g' \
      -i support/xdg/ffado-mixer.appdata.xml
}

build() {
  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
  cd "${pkgname}-${pkgver}"

  scons PREFIX=/usr \
        DEBUG=no \
        CUSTOM_ENV=true \
        MANDIR='/usr/share/man' \
        PYTHON_INTERPRETER='/usr/bin/python3' \
        PYPKGDIR="/usr/lib/python${python_version}/site-packages" \
        UDEVDIR='/usr/lib/udev/rules.d'
	      DETECT_USERSPACE_ENV=false \
        ENABLE_OPTIMIZATION=false
}

package() {
  depends+=('libasound.so' 'libconfig++.so' 'libdbus-c++-1.so' )
  cd "${pkgname}-${pkgver}"
  scons DESTDIR="${pkgdir}" WILL_DEAL_WITH_XDG_MYSELF="True" install

  # XDG
  install -vDm 644 support/xdg/ffado.org-ffadomixer.desktop \
    "${pkgdir}/usr/share/applications/ffado-mixer.desktop"
  install -vDm 644 support/xdg/hi64-apps-ffado.png \
    "${pkgdir}/usr/share/icons/hicolor/64x64/apps/ffado-mixer.png"

  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" -vDm 644 {AUTHORS,README}
}

# vim:set ts=2 sw=2 et:
