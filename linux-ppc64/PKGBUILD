# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgbase=linux-ppc64
pkgver=6.8.8.arch1
pkgrel=1
pkgdesc='Linux (powerpc64le/ 4K page size)'
url='https://github.com/archlinux/linux'
arch=(any)
license=(GPL2)
makedepends=(
  bc
  cpio
  gettext
  libelf
  pahole
  perl
  python
  tar
  xz

  # cross compiler
  powerpc64-unknown-linux-gnu-gcc
)
options=('!debug' '!strip')
_srcname=linux-${pkgver%.*}
_srctag=v${pkgver%.*}-${pkgver##*.}
source=(
  https://cdn.kernel.org/pub/linux/kernel/v${pkgver%%.*}.x/${_srcname}.tar.{xz,sign}
  $url/releases/download/$_srctag/linux-$_srctag.patch.zst{,.sig}
  config  # the main kernel config file
  0001-skip-i915-debugfs-code-when-disabled.patch
  0002-hotfix-usb-audio.patch
  0003-ppc-radeon-qw-pm.patch
  0004-pcmcia-iomega-clik.patch
  0005-i915-diag.patch
  0006-i915-page-64k.patch
  0007-i915-ppc.patch
  0600-ps3-partition.patch
  0666-hotfix-ppc64-ps3-elfabi.patch
  0666-hotfix-ppc64-ps3-srr-clobber.patch
  0666-hotfix-ppc64-save-restore-funcs.patch
  0666-hotfix-windfarm_rm31.patch
  0700-ps3vram-mod-ps3fb.patch
)
validpgpkeys=(                                                                                                                                                                               
  ABAF11C65A2970B130ABE3C479BE3E4300411886  # Linus Torvalds                                                                                                                                 
  647F28654894E3BD457199BE38DBBDC86092693E  # Greg Kroah-Hartman                                                                                                                             
  83BC8889351B5DEBBB68416EB8AC08600F108CDF  # Jan Alexander Steffens (heftig)                                                                                                                
) 
# https://www.kernel.org/pub/linux/kernel/v6.x/sha256sums.asc
sha256sums=('1c4cdcb9d560fad1fb95db2cb8afbedc922f9ead848371fe40363b13f9f631ba'
            'SKIP'
            '3328bb421dc41135584adc6295ad64125c217dc57939d5397b29f53d16f02da4'
            'SKIP'
            '94b52af462a0a1e55b5ca91b00327e4013cde96a21b0c6928ae4b2dff1ee8dcc'
            'a787075104b4bfdca946926ee9dcb19a90fdbafc06f7c577b66c91ea2a319f68'
            'd1f25e9415a749268ede5ce2aa2595faa08d5bf6e29cee63b27b9334d5efc3a2'
            '07dbbf3e8f743e8d2ad90891a8b567392271db139e339529df1ab4387e0e6400'
            'b8a5d1e9dbed702d23bda79af3ac6f3d61228e123f7d6e5717779f2e28c4b677'
            '060a30a4148aa88701003fd56bb389971db8527fa376e3436094cddcdec76c24'
            '5dc5eb5772dd79ea4c87a60c2bafbb0503f20a3e4ddbf2075c02ca69e159e21d'
            '7d44df97c0e565cfec60b77c0ad582e7f0da54726cfedc149c0b49e45f3a5d8b'
            'af4b93931fe91591e67f6e011e5d670a432cb23f13fba18deffabf9f92cdaab5'
            '71f4cc41fc5ef3100e0a0c1a70df303de4cf5ab0a16593f5a8cb54dd035d43cd'
            'a8dedaa11c0adaceec41fe973bdd30eb48e22a468277bb9df75a18214a48f9e5'
            '8bbddc3b443fc68d407458d373226073e4262c178ebb14eb28c34a069544d267'
            '49186b0457740869214eafbda48d457d673106ec821a88cfb61d6129c4ac08ae'
            '57b4a6bae2797529f670372a267bd00d79829ad07e30efe4667eb1d2e02fc2fe')
b2sums=('3c04407c6875d1547c18024db7a020c450b332b10707c847208080819fa92624281fd9b628c1fefe7c16439f60d64dcc80010726a7d4320354b682ccaadb1f35'
        'SKIP'
        '019c9263802379310ab5112258bbf46977019183561a8b7170d270d10fd5b90132ab15912de96793a63d61f8cff47ea6059ef278139b39bca9be3d1655fde907'
        'SKIP'
        '26266f48aacc312b25e0bef804e2b0e93d41d75b4dac54913c3468293847a90b2d7067b0778999246bc3e8e91bb26dfe1ad99ed0c7e492fc1187795c369885d5'
        '2a289e09e0afebad8901d624ea1de7f6db0951a26c81b73dd3ca509aa88e9287faef145dd04c85e05f84e062e93ae03b2c9cb84b1d902f752b92e4451dd46b7e'
        '91b488e69c8b34c7347d135cde425e681faa6a329951648172ab00561e498a74063e345b897ea45142d167a2c02a27fee9ffe585f6a7919040b37f43547c1fc8'
        '9677bbe86e5bb846c1fef6065f854e72b97a6e2b3b0e547192159fd56fac8b879999c6d52a835ebf7566be00c1a78644112564a4f76ec1c912181474479ad0a0'
        '7af37e02f6b8233a72c03be8586abeabc253dae70208e8169b208f49360c96c63edc5d68c1dbb51d6480d8b1bfdf8ba5f3303d1b18e9ab453c01ebebf3cf19a7'
        'bae7c8f1b50b9a43c99b91496446e9e0eff9442e40c7501aa5c1f24587dd39bea5bc0c5b48e6439118dcc779e36fbfaf75b2d92617257ade5e30109df21ae102'
        '6dc95da4e14b9c4dd8c62480560c061a5c1a29ce1af8d9ee1e73401f79339e1b6c4f8339568d57414a51341f382c1ee1a636bdaaf70bca72692d62c0a03670f8'
        '45ca140885508e53bc779fe650e84702928980b7b3cedd63d68558a20a9504e93cb2e78ca20e613917d17732b6e18c3fb37b348cfdf5e1a5325b827986268a1e'
        '0ba24344e4f4859f0ddbdc29f1001f20bcd17b5d59f30240869967e37365246d75ac1c4b4676b2a3bab9568f5431e6645a71aecfce7512bee9d6b75c6f1e51bc'
        '079ffe75189b4c82acd5694e5c85407de2b7c42aa4e406ee0408109ad12b3dc7fcceb38ba56b8ad5660a5920af85b7b0e139f9cbb4840c21c03c59f80d2ec920'
        '99f073264fc5c49dcf09220b14cc20aae174a156a13b74860a405581fd08ce0d3824de792053653d0aacf6860dd9a92844b8fb8433c0d18dc175452be59c0168'
        '0ebf435f84f646b2e5dd0c40b00b447cb86f71226a4ccacda3f87341019be5b559b315c141c0efeafff3adb1621e56a278812f55fb5e89518deba57d0f2e22fb'
        '99656109ed282ba45066d0cdc3515d11e1e660b7eadac7d8dccadb2429e929f77280d57cf800fa3e7326465e3060695325fecac72c21fcec4cd356fe2801b60c'
        '3f439ca0b52da98a69d90cfc48c958ab1613118b5218d8002e8163998f6c64616ebc4d5cb2b2892d9ed113632e224921576e76d827b59186c5a4c94e5641b40f')

export KBUILD_BUILD_HOST=archpower
export KBUILD_BUILD_USER=$pkgbase
export KBUILD_BUILD_TIMESTAMP="$(date -Ru${SOURCE_DATE_EPOCH:+d @$SOURCE_DATE_EPOCH})"

prepare() {
  cd $_srcname

  echo "Setting version..."
  echo "-$pkgrel" > localversion.10-pkgrel
  echo "${pkgbase#linux}" > localversion.20-pkgname

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    src="${src%.zst}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done

  echo "Setting config..."
  cp ../config .config
  ARCH=powerpc CROSS_COMPILE=powerpc64-unknown-linux-gnu- make olddefconfig
  diff -u ../config .config || :

  ARCH=powerpc CROSS_COMPILE=powerpc64-unknown-linux-gnu- make -s kernelrelease > version
  echo "Prepared $pkgbase version $(<version)"
}

build() {
  cd $_srcname
  ARCH=powerpc CROSS_COMPILE=powerpc64-unknown-linux-gnu- make all
}

_package() {
  pkgdesc="The $pkgdesc kernel and modules"
  depends=(
    coreutils
    initramfs
    kmod
  )
  optdepends=(
    'wireless-regdb: to set the correct wireless channels of your country'
    'linux-firmware: firmware images needed for some devices'
  )
  provides=(
    KSMBD-MODULE
    VIRTUALBOX-GUEST-MODULES
    WIREGUARD-MODULE
  )
  replaces=(
    virtualbox-guest-modules-arch
    wireguard-arch
  )

  cd $_srcname
  local modulesdir="$pkgdir/usr/lib/modules/$(<version)"

  echo "Installing boot image..."
  # systemd expects to find the kernel here to allow hibernation
  # https://github.com/systemd/systemd/commit/edda44605f06a41fb86b7ab8128dcf99161d2344
  install -Dm644 "$(ARCH=powerpc CROSS_COMPILE=powerpc64-unknown-linux-gnu- make -s image_name)" "$modulesdir/vmlinuz"

  # Used by mkinitcpio to name the kernel
  echo "$pkgbase" | install -Dm644 /dev/stdin "$modulesdir/pkgbase"

  echo "Installing modules..."
  ARCH=powerpc CROSS_COMPILE=powerpc64-unknown-linux-gnu- \
  ZSTD_CLEVEL=19 make INSTALL_MOD_PATH="$pkgdir/usr" INSTALL_MOD_STRIP=1 \
    DEPMOD=/doesnt/exist modules_install  # Suppress depmod

  # remove build link
  rm "$modulesdir"/build
}

_package-headers() {
  pkgdesc="Headers and scripts for building modules for the $pkgdesc kernel"
  depends=(pahole)

  cd $_srcname
  local builddir="$pkgdir/usr/lib/modules/$(<version)/build"

  echo "Installing build files..."
  install -Dt "$builddir" -m644 .config Makefile Module.symvers System.map \
    localversion.* version vmlinux
  install -Dt "$builddir/kernel" -m644 kernel/Makefile
  install -Dt "$builddir/arch/powerpc" -m644 arch/powerpc/Makefile
  cp -t "$builddir" -a scripts

  echo "Installing headers..."
  cp -t "$builddir" -a include
  cp -t "$builddir/arch/powerpc" -a arch/powerpc/include
  install -Dt "$builddir/arch/powerpc/kernel" -m644 arch/powerpc/kernel/asm-offsets.s

  install -Dt "$builddir/drivers/md" -m644 drivers/md/*.h
  install -Dt "$builddir/net/mac80211" -m644 net/mac80211/*.h

  # https://bugs.archlinux.org/task/13146
  install -Dt "$builddir/drivers/media/i2c" -m644 drivers/media/i2c/msp3400-driver.h

  # https://bugs.archlinux.org/task/20402
  install -Dt "$builddir/drivers/media/usb/dvb-usb" -m644 drivers/media/usb/dvb-usb/*.h
  install -Dt "$builddir/drivers/media/dvb-frontends" -m644 drivers/media/dvb-frontends/*.h
  install -Dt "$builddir/drivers/media/tuners" -m644 drivers/media/tuners/*.h

  # https://bugs.archlinux.org/task/71392
  install -Dt "$builddir/drivers/iio/common/hid-sensors" -m644 drivers/iio/common/hid-sensors/*.h

  echo "Installing KConfig files..."
  find . -name 'Kconfig*' -exec install -Dm644 {} "$builddir/{}" \;

  echo "Removing unneeded architectures..."
  local arch
  for arch in "$builddir"/arch/*/; do
    [[ $arch = */powerpc/ ]] && continue
    echo "Removing $(basename "$arch")"
    rm -r "$arch"
  done

  echo "Removing documentation..."
  rm -r "$builddir/Documentation"

  echo "Removing broken symlinks..."
  find -L "$builddir" -type l -printf 'Removing %P\n' -delete

  echo "Removing loose objects..."
  find "$builddir" -type f -name '*.o' -printf 'Removing %P\n' -delete

  echo "Stripping build tools..."
  local file
  while read -rd '' file; do
    case "$(file -Sib "$file")" in
      application/x-sharedlib\;*)      # Libraries (.so)
        powerpc64-unknown-linux-gnu-strip -v $STRIP_SHARED "$file" ;;
      application/x-archive\;*)        # Libraries (.a)
        powerpc64-unknown-linux-gnu-strip -v $STRIP_STATIC "$file" ;;
      application/x-executable\;*)     # Binaries
        powerpc64-unknown-linux-gnu-strip -v $STRIP_BINARIES "$file" ;;
      application/x-pie-executable\;*) # Relocatable binaries
        powerpc64-unknown-linux-gnu-strip -v $STRIP_SHARED "$file" ;;
    esac
  done < <(find "$builddir" -type f -perm -u+x ! -name vmlinux -print0)

  echo "Stripping vmlinux..."
  powerpc64-unknown-linux-gnu-strip -v $STRIP_STATIC "$builddir/vmlinux"

  echo "Adding symlink..."
  mkdir -p "$pkgdir/usr/src"
  ln -sr "$builddir" "$pkgdir/usr/src/$pkgbase"
}

pkgname=(
  "$pkgbase"
  "$pkgbase-headers"
)
for _p in "${pkgname[@]}"; do
  eval "package_$_p() {
    $(declare -f "_package${_p#$pkgbase}")
    _package${_p#$pkgbase}
  }"
done

# vim:set ts=8 sts=2 sw=2 et:
