# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: SÃ©bastien Luttringer
# Contributer: Nicolas Martyanoff <khaelin@gmail.com>

pkgname=lksctp-tools
pkgver=1.0.20
pkgrel=1
pkgdesc='An implementation of the SCTP protocol'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://github.com/sctp/lksctp-tools'
license=('GPL-2.0-or-later' 'LGPL-2.1-only')
makedepends=('git')
depends=('glibc' 'bash')
source=("git+https://github.com/sctp/lksctp-tools.git#tag=v$pkgver")
sha256sums=('0f1895b1c5ed5f8880f7597781aa1d2f77fda9ebac475f85434c6c1ec8f7887e')

build() {
  cd $pkgname
  ./bootstrap
  ./configure --prefix=/usr
  make
}

check() {
  cd $pkgname/src/func_tests
  make v4test
  make v6test
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
