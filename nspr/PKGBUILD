# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

pkgname=nspr
pkgver=4.35
pkgrel=1.2
pkgdesc="Netscape Portable Runtime"
url="https://developer.mozilla.org/en-US/docs/Mozilla/Projects/NSPR"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(MPL GPL)
depends=(glibc sh)
makedepends=(zip mercurial)
_revision=b563bfc16c887c48b038b7b441fcc4e40a126d3b
source=("hg+https://hg.mozilla.org/projects/nspr#revision=$_revision"
        nspr-4.35-b1815947.patch
        nspr-4.35-parenthesis.patch)
sha256sums=('SKIP'
            '32c1180a2fbe2e7dad6d09f381a60d5408ba7e01a652c57a6cf235952a135442'
            '11956ba078030661c6201965a202be0ddc6065e9793d075394579d67d4630a92')

pkgver() {
  cd nspr
  hg id -t -r. | sed 's/^NSPR_//;s/_RTM$//;s/_/./g'
}

prepare() {
  cd nspr
  patch -Np1 -i ${srcdir}/nspr-4.35-b1815947.patch
  patch -Np1 -i ${srcdir}/nspr-4.35-parenthesis.patch
}

build() {
  cd nspr

  case "${CARCH}" in
    powerpc) ;;
    *) _options=(--enable-64bit) ;;
  esac

  unset CFLAGS CXXFLAGS

  ./configure \
      --prefix=/usr \
      --libdir=/usr/lib \
      --includedir=/usr/include/nspr \
      --enable-optimize="-O2" \
      --disable-debug ${_options[@]}
  make ${SOURCE_DATE_EPOCH:+
    SH_NOW="${SOURCE_DATE_EPOCH}000000"
    SH_DATE="$(date --utc --date="@$SOURCE_DATE_EPOCH" '+%Y-%m-%d %T')"
  }
}

package() {
  cd nspr
  make DESTDIR="$pkgdir" install
  ln -s nspr.pc "$pkgdir/usr/lib/pkgconfig/mozilla-nspr.pc"

  rm -r "$pkgdir"/usr/include/nspr/md
  rm "$pkgdir"/usr/bin/{compile-et.pl,prerr.properties}
}

# vim:set sw=2 et:
