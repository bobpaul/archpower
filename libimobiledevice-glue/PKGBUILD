# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=libimobiledevice-glue
pkgver=1.2.0
pkgrel=1
pkgdesc='A library with common code used by libraries and tools around the libimobiledevice project'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://libimobiledevice.org/'
license=('LGPL-2.1-or-later')
depends=('glibc' 'libplist')
makedepends=('git')
source=("git+https://github.com/libimobiledevice/libimobiledevice-glue.git#tag=$pkgver")
b2sums=('a5c2e1a64b559eacb2ad988f4caf06fcb6b8f0ae653ea5e1738f7b41bc143e3327ef1bd3718b0364911c0b2c549b8188a66c59c81583e0392acc5e10f6e16167')

prepare() {
  cd $pkgname
  autoreconf -fi
}

build() (
  cd $pkgname
  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
)

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
