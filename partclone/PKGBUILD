# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jonathan Steel <jsteel at archlinux.org>
# Contrinutor: Dan Serban
# Contributor: Franz Burgmann
# Contributor: Todd Partridge (Gen2ly)
# Contributor: Ivan Sichmann Freitas

pkgname=partclone
pkgver=0.3.19
pkgrel=1
pkgdesc="Utilities to save and restore used blocks on a partition"
arch=(x86_64 powerpc64le powerpc)
url="https://partclone.org"
license=('GPL')
depends=('ntfs-3g' 'nilfs-utils')
source=($pkgname-$pkgver.tar.gz::https://github.com/Thomas-Tsai/partclone/archive/$pkgver.tar.gz)
md5sums=('c4e469396a402810f97370fbfa462934')

build() {
  cd $pkgname-$pkgver

  ./autogen
  ./configure --prefix=/usr --enable-extfs --enable-fat \
    --enable-hfsp --enable-btrfs --enable-ncursesw --enable-ntfs \
    --enable-exfat --enable-f2fs --enable-minix --enable-nilfs2 --enable-xfs \
    --sbindir=/usr/bin

  make
}

package() {
  cd $pkgname-$pkgver

  make PREFIX=/usr DESTDIR="$pkgdir" install
}
