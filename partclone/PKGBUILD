# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jonathan Steel <jsteel at archlinux.org>
# Contrinutor: Dan Serban
# Contributor: Franz Burgmann
# Contributor: Todd Partridge (Gen2ly)
# Contributor: Ivan Sichmann Freitas

pkgname=partclone
pkgver=0.3.33
pkgrel=1
pkgdesc="Utilities to save and restore used blocks on a partition"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://partclone.org"
license=('GPL')
depends=('ntfs-3g' 'nilfs-utils')
source=($pkgname-$pkgver.tar.gz::https://github.com/Thomas-Tsai/partclone/archive/$pkgver.tar.gz
        $pkgname-$pkgver-check-for-byteswap_h.patch::https://patch-diff.githubusercontent.com/raw/Thomas-Tsai/partclone/pull/257.patch)
sha256sums=('b5ebae1dd3763d8dc719835e46c4517419692f66e7748050faab38f8761d4719'
            'a186482eddc3e4ca99a230fdabd5c0346e27ea816fbdd74ae5cb4d4f11c98be4')

prepare() {
  cd $pkgname-$pkgver

  patch -Np1 -i $srcdir/$pkgname-$pkgver-check-for-byteswap_h.patch

  ./autogen

}

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --enable-extfs \
    --enable-fat \
    --enable-hfsp \
    --enable-btrfs \
    --enable-ncursesw \
    --enable-ntfs \
    --enable-exfat \
    --enable-f2fs \
    --enable-minix \
    --enable-nilfs2 \
    --enable-xfs \
    --sbindir=/usr/bin
  make
}

package() {
  cd $pkgname-$pkgver
  make PREFIX=/usr DESTDIR="$pkgdir" install
}
