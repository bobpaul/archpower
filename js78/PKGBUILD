# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=js78
pkgver=78.13.0
pkgrel=1
pkgdesc="JavaScript interpreter and libraries - Version 78"
arch=(x86_64 powerpc64le powerpc)
url="https://developer.mozilla.org/en-US/docs/Mozilla/Projects/SpiderMonkey"
license=(MPL)
depends=(gcc-libs readline zlib sh)
makedepends=(zip autoconf2.13 python-setuptools python-psutil rust llvm clang lld)
checkdepends=(mercurial git)
_relver=${pkgver}esr
_whissi_patches_ver=78esr-patches-15
source=(https://archive.mozilla.org/pub/firefox/releases/$_relver/source/firefox-$_relver.source.tar.xz{,.asc}
        0001-Fixes-for-LTO-PGO-support.patch
        rust-configure.patch
        https://dev.gentoo.org/~whissi/mozilla/patchsets/firefox-${_whissi_patches_ver}.tar.xz)
sha256sums=('19f9a9e2c2473cb37d57a49a1b2661a8236c4930df9b251d5e9ae274292b4f6c'
            'SKIP'
            '926a573eabe3b6177363b795ed5e5641b4a4ebc6d7cfa093f11e1779ac0f897c'
            'd6eb063743f07ba8aaacf7c5f915253422f8096560c37c3c529ed1c34910a002'
            '4417b233d40105ef2908d34ffc288fb9da4af3bbeadc3d6732c229c011e81b4c')
validpgpkeys=('14F26682D0916CDD81E37B6D61B7B526D98F0353') # Mozilla Software Releases <release@mozilla.com>

# Make sure the duplication between bin and lib is found
COMPRESSZST+=(--long)

prepare() {
  cd firefox-$pkgver
  mkdir obj

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  local configure_args=(
    --prefix=/usr
    --disable-debug
    --disable-debug-symbols
    --disable-jemalloc
    --disable-strip
    --enable-hardening
    --enable-optimize
    --enable-readline
    --enable-release
    --enable-rust-simd
    --enable-shared-js
    --enable-tests
    --with-intl-api
    --with-system-zlib
    --enable-linker=lld
    --without-system-icu
  )
  case "${CARCH}" in
    powerpc)
      CXXFLAGS+=" -Wno-class-memaccess"
      LDFLAGS+=" -Wl,-z,stack-size=1048576 -latomic"
      configure_args+=(
        --disable-jit
        --with-system-icu
      )
      ;;
  esac

  export CC=clang
  export CXX=clang++
  export AR=llvm-ar
  export NM=llvm-nm
  export RANLIB=llvm-ranlib

  cd firefox-$pkgver/obj

  if [ "${CARCH}" != 'powerpc' ]; then
    # Do 3-tier PGO
    echo "Building instrumented JS..."
    sh ../js/src/configure "${configure_args[@]}" \
      --enable-profile-generate=cross
    make

    echo "Profiling instrumented JS..."
    (
      local js="$PWD/dist/bin/js"
      export LLVM_PROFILE_FILE="$PWD/js-%p-%m.profraw"

      cd ../js/src/octane
      "$js" run.js

      cd ../../../third_party/webkit/PerformanceTests/ARES-6
      "$js" cli.js

      cd ../SunSpider/sunspider-0.9.1
      "$js" sunspider-standalone-driver.js
    )

    llvm-profdata merge -o merged.profdata *.profraw

    stat -c "Profile data found (%s bytes)" merged.profdata
    test -s merged.profdata

    echo "Removing instrumented JS..."
    make clobber

    echo "Building optimized JS..."
    sh ../js/src/configure "${configure_args[@]}" \
      --enable-lto=cross \
      --enable-profile-use=cross \
      --with-pgo-profile-path="$PWD/merged.profdata"
  else
    sh ../js/src/configure "${configure_args[@]}"
  fi

  make
}

check() {
  local jstests_extra_args=(
    --format=none
    --exclude-random
    --wpt=disabled
  ) jittest_extra_args=(
    --format=none
    --timeout 300
  ) jittest_test_args=(
    basic
  )

  cd firefox-$pkgver/obj
  make -C js/src check-jstests check-jit-test \
    JSTESTS_EXTRA_ARGS="${jstests_extra_args[*]}" \
    JITTEST_EXTRA_ARGS="${jittest_extra_args[*]}" \
    JITTEST_TEST_ARGS="${jittest_test_args[*]}" || return 0
}

package() {
  cd firefox-$pkgver/obj
  make DESTDIR="$pkgdir" install
  rm -f "$pkgdir"/usr/lib/*.ajs
  find "$pkgdir"/usr/{lib/pkgconfig,include} -type f -exec chmod -c a-x {} +
}

# vim:set ts=2 sw=2 et:
