# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>

pkgname=k3s
pkgver=1.28.3+k3s2
pkgrel=1
pkgdesc='Lightweight Kubernetes'
arch=(x86_64 powerpc64le)
url='https://github.com/rancher/k3s'
license=(APACHE)
makedepends=(docker git go setconf)
source=(
  "git+https://github.com/k3s-io/k3s.git#tag=v${pkgver}"
)
sha256sums=('SKIP')

MYARCH="${CARCH}"
case "${CARCH}" in
  powerpc64le) MYARCH=ppc64le ;;
  x86_64)      MYARCH=amd64   ;;
esac

prepare() {
  setconf "$pkgname/k3s.service" ExecStart '/usr/bin/k3s server'
}

build() {
  SKIP_IMAGE=true SKIP_VALIDATE=true SKIP_AIRGAP=true \
  make -C $pkgname
}

package() {
  install -Dm755 $pkgname/dist/artifacts/k3s-${MYARCH} "$pkgdir/usr/bin/k3s"
  install -Dm644 $pkgname/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 $pkgname/k3s.service "$pkgdir/usr/lib/systemd/system/k3s.service"
}

# vim: ts=2 sw=2 et:
