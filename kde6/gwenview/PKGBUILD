# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=gwenview
pkgver=24.01.95
pkgrel=1
pkgdesc='A fast and easy to use image viewer'
url='https://apps.kde.org/gwenview/'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(GPL-2.0-or-later LGPL-2.0-or-later)
depends=(baloo
         cfitsio
         exiv2
         gcc-libs
         glibc
         kcolorscheme
         kcompletion
         kconfig
         kconfigwidgets
         kcoreaddons
         kfilemetadata
         kguiaddons
         ki18n
         kiconthemes
#        kimageannotator
         kio
         kitemmodels
         kitemviews
         kjobwidgets
         kparts
         kservice
         kwidgetsaddons
         kxmlgui
         lcms2
         libjpeg-turbo
         libkdcraw
         libpng
         libtiff
         libx11
         phonon-qt6
         plasma-activities
         purpose
         qt6-base
         qt6-svg
         qt6-wayland
         solid
         wayland)
optdepends=('kamera: import pictures from gphoto2 cameras'
            'kimageformats: support for dds, xcf, exr, psd, and more image formats'
            'qt6-imageformats: support for tiff, webp, and more image formats')
makedepends=(extra-cmake-modules
             kdoctools
             wayland-protocols)
groups=(kde-applications
        kde-graphics)
source=(https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('ee0aee8fcc2a36e38b2f5d324476f11b508234f61ac81c9e321988249a1eddda'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DGWENVIEW_SEMANTICINFO_BACKEND="Baloo" \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
