# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Patryk Kowalczyk < patryk at kowalczyk dot ws>

pkgbase=libseccomp
pkgname=(libseccomp)
[ "${CARCH}" == 'x86_64' ] && pkgname+=(python-libseccomp)
pkgver=2.5.1
pkgrel=2
pkgdesc='Enhanced seccomp library'
arch=(x86_64 powerpc64le powerpc)
license=('LGPL2.1')
url="https://github.com/seccomp/libseccomp"
depends=('glibc')
checkdepends=('valgrind')
makedepends=('gperf')
[ "${CARCH}" == 'x86_64' ] && makedepends+=('cython' 'python-setuptools')
source=(https://github.com/seccomp/${pkgname}/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz{,.asc}
        add-5.8-powerpc-syscall-headers.patch)
sha256sums=('ee307e383c77aa7995abc5ada544d51c9723ae399768a97667d4cdb3c3a30d55'
            'SKIP'
            '37715c38206ff837402c759d4ebddf83cf1ff36a0bcd607b86737db7e4b1e6d3')
b2sums=('683ae7536c0cba36f4d30640b42361171fc34b7cb04985ea56e64369df29c440361a0205385b14580cc0e481e0f9ffd0b0e8ebd4ac98817ed59298db6b274c35'
        'SKIP'
        '58df0819370ad9631195436940d484c61c39979f3a53dd4e083d4b7d7b23e382f77bd8fe8963a2d749924ad3da1dbb5b1a1875738685234f452821dcbc986998')
validpgpkeys=(
  '7100AADFAE6E6E940D2E0AD655E45A5AE8CA7C8A' # Paul Moore <paul@paul-moore.com>
  '47A68FCE37C7D7024FD65E11356CE62C2B524099' # Tom Hromatka <tom.hromatka@oracle.com>
)

prepare() {
  cd ${pkgbase}-${pkgver}

  patch -Np1 -i ${srcdir}/add-5.8-powerpc-syscall-headers.patch

  autoreconf -fiv
}

build() {
  cd ${pkgbase}-${pkgver}
  ./configure --prefix=/usr
  make
  
  if [ "${CARCH}" == 'x86_64' ]; then
    cd src/python
    env VERSION_RELEASE=${pkgver} python setup.py build
  fi
}

check() {
  cd ${pkgbase}-${pkgver}
  make check
}

package_libseccomp() {
  provides=('libseccomp.so')
  cd ${pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 CHANGELOG README.md SECURITY.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

package_python-libseccomp() {
  depends=('python' 'libseccomp.so')
  cd ${pkgbase}-${pkgver}/src/python
  env VERSION_RELEASE=${pkgver} python setup.py install --root="${pkgdir}" --prefix=/usr -O1 --skip-build
}

# vim: ts=2 sw=2 et:
