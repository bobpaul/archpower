# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgname=mesa-revyos
pkgdesc="RevyOS patched mesa for riscv PowerVR"
pkgver=21.2.1
pkgrel=1
arch=(riscv64)
makedepends=('python-mako' 'libxml2' 'libx11' 'xorgproto' 'libdrm' 'libxshmfence' 'libxxf86vm'
             'libxdamage' 'wayland' 'wayland-protocols' 'zstd' 'elfutils'
             'libglvnd' 'libunwind' 'libxrandr' 'valgrind' 'cmake' 'meson' 'llvm14')
url="https://www.mesa3d.org/"
license=('custom')
source=(https://mesa.freedesktop.org/archive/mesa-${pkgver}.tar.xz{,.sig}
        LICENSE
        https://mirror.iscas.ac.cn/revyos/revyos-gles-21/pool/main/m/mesa/mesa_21.2.1-2revyos2.debian.tar.xz)
sha512sums=('d4056287ec86f7a95ce534a251a1ccbc3a3b08a2f7112152def2f054fc8a9424501d5883c463554ee95fe2dafb832613efd7145e989ee8281948233942730c2c'
            'SKIP'
            'f9f0d0ccf166fe6cb684478b6f1e1ab1f2850431c06aa041738563eb1808a004e52cdec823c103c9e180f03ffc083e95974d291353f0220fe52ae6d4897fecc7'
            'eb6e6e02c5ae5bd5df95028b7cec3bea8c69ed12a28331eb07338f8337bf9f2a443ccc68b09d86e131fe91020ca7fbc7bf3eca5c16dca1cbbc528d9c88d88146')
#validpgpkeys=('57551DE15B968F6341C248F68D8E31AFC32428A6') # Eric Engestrom <eric@engestrom.ch>
validpgpkeys=('71C4B75620BC75708B4BDB254C95FAAB3EB073EC') # Dylan Baker <dylan@pnwbakers.com>

prepare() {
  cd mesa-$pkgver
  grep -v '^#' ../debian/patches/series | sed '/^\s*$/d' >series.stripped
  for patch in $(cat series.stripped); do patch -Np1 -i ../debian/patches/$patch; done
}

build() {
  export PATH=/usr/lib/llvm14/bin:$PATH

  arch-meson mesa-$pkgver build \
    -D b_lto=true \
    -D b_ndebug=true \
    -D gallium-drivers=r300,r600,swrast \
    -D vulkan-drivers=amd,pvr \
    -D dri3=enabled \
    -D egl=enabled \
    -D gbm=enabled \
    -D gles1=disabled \
    -D gles2=enabled \
    -D glvnd=true \
    -D glx=dri \
    -D libunwind=enabled \
    -D llvm=enabled \
    -D lmsensors=disabled \
    -D osmesa=true \
    -D shared-glapi=enabled \
    -D microsoft-clc=disabled \
    -D valgrind=enabled ${_meson_options[@]}

  # Print config
  meson configure build

  ninja -C build
  meson compile -C build
}

package() {
  depends=('libglvnd' 'libdrm' 'zstd' 'wayland' 'libxshmfence' 'libxxf86vm' 'libxfixes' 'llvm14-libs')
  conflicts=('mesa')
  provides=("mesa=${pkgver}" "opengl-driver")

  DESTDIR="${pkgdir}" meson install -C build

  install -m644 -Dt "${pkgdir}/usr/share/licenses/${pkgname}" LICENSE
}
