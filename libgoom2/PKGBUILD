# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: danitool

pkgname=libgoom2
pkgver=2k4
pkgrel=6
pkgdesc="Shared library part of the Goom visualization plugin"
arch=(x86_64 powerpc64le powerpc)
url="http://www.fovea.cc/?page=details_produits&id=goom"
license=('LGPL')
depends=('glibc')
options=('!emptydirs' '!makeflags')
source=("http://downloads.sourceforge.net/project/goom/goom2k4/0/goom-2k4-0-src.tar.gz"
        be-modern.patch)
sha512sums=('790e3ab8dee122320ad8b3ae15f6a1cd2780222d5ae97979f614f16ba73b4b853960e7945ef9053ad9eddf9e29e69d0c52f0875d07c5c6c7cee41d764bf4ea5f'
            'ab9face5351c95666d34a72a943c32c46711944d0c2e165cd10e4fd9f4360bf1911deabf9a97bb378effc26137cc59db26fd8abd567878742d754e58a833a715')

prepare() {
  cd "${srcdir}/goom2k4-0"
  patch -Np1 -i ${srcdir}/be-modern.patch
  libtoolize -fiv
  autoreconf -fiv
}

build() {
  cd "${srcdir}/goom2k4-0"
  ./configure --build=$CHOST \
	      --prefix=/usr \
              --without-xmms \
              --without-sdl
  make
}

package() {
  cd "${srcdir}/goom2k4-0"

  make DESTDIR="${pkgdir}" install
}
