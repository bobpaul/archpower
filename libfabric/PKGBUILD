# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

pkgname=libfabric
pkgver=2.0.0
pkgrel=1
pkgdesc="User-space API for OpenFabrics Interfaces (OFI)"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://ofiwg.github.io/libfabric/"
license=('BSD-2-Clause OR GPL-2.0-or-later')
depends=(glibc numactl)
options=(!lto)
source=(https://github.com/ofiwg/libfabric/releases/download/v${pkgver}/libfabric-${pkgver}.tar.bz2
        fix-page-mask.patch::https://github.com/Cryolitia-Forks/libfabric/commit/f6fb959e1f1efa350962fe4ae444c33fc91e2451.patch)
sha512sums=('4d3067fde9d5063119618367234341dce0fe9c812bc4c761eac3d686590a19b6fa8ec857928a3dcd369bce043586e7147e97e6554206e3c34a80f93cbe094962'
            '6e803c4155fd0adfb2e4d20ad7090bb8c9f2045cd78597f074c7662f43e5ad20ec408d9647ffe472da85cda23fcf8fdd67ea6709c17fa25790d46f70435e24f4')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 -i ../fix-page-mask.patch
  autoreconf -fvi
}

build() {
  cd ${pkgname}-${pkgver}

  case "${CARCH}" in
    powerpc) export CFLAGS+=' -Wno-incompatible-pointer-types -latomic' ;;
  esac

  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make test
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -vDm 644 COPYING -t "$pkgdir/usr/share/licenses/$pkgname/"
}
