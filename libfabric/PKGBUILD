# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=libfabric
pkgver=1.15.1
pkgrel=1
pkgdesc="User-space API for OpenFabrics Interfaces (OFI)"
arch=(x86_64 powerpc64le powerpc riscv64)
url="https://ofiwg.github.io/libfabric/"
license=(GPL2)
depends=(glibc gcc-libs)
depends_x86_64=(numactl)
options=(!lto)
source=(https://github.com/ofiwg/libfabric/releases/download/v${pkgver}/libfabric-${pkgver}.tar.bz2)
sha512sums=('241682b83bdec26cdf5fe918c445732bcd123c01798d1973c4c6bff0d4c7ca271a782ff8af8f9bcaf1560580804c3088a4c18fdf9fbc58d9c3d98f4377903818')

prepare() {
  cd ${pkgname}-${pkgver}
  autoreconf -fvi
}

build() {
  cd ${pkgname}-${pkgver}


  CONFIGURE_FLAGS="--enable-opx=no"
  case "${CARCH}" in
    powerpc)
      export CXXFLAGS+=' -latomic'
      export CFLAGS+=' -latomic'
    ;;
    x86_64)
      CONFIGURE_FLAGS="--enable-ops=yes"
    ;;
  esac

  ./configure --prefix=/usr ${CONFIGURE_FLAGS}
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make test
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
