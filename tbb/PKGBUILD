# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Denis Martinez <deuns.martinez AT gmail.com>
# Contributor: Bogdan Burlacu <bogdan.burlacu AT pm.me>

pkgname=tbb
pkgver=2021.5.0
pkgrel=1
pkgdesc='High level abstract threading library'
arch=(x86_64 powerpc64le powerpc riscv64)
url='https://www.threadingbuildingblocks.org/'
license=('Apache')
depends=('gcc-libs' 'hwloc')
makedepends=('cmake' 'inetutils' 'ninja' 'python' 'swig')
conflicts=('intel-tbb')
provides=("intel-tbb=$pkgver")
replaces=('intel-tbb')
source=(https://github.com/oneapi-src/oneTBB/archive/v$pkgver/$pkgname-$pkgver.tar.gz
        tbb-ppc-atomic.patch
        tbb-2021.4.0-lto.patch)
sha512sums=('0e7b71022e397a6d7abb0cea106847935ae79a1e12a6976f8d038668c6eca8775ed971202c5bd518f7e517092b67af805cc5feb04b5c3a40e9fbf972cc703a46'
            '7287d7d09e3a00c0ac03a5caab4e7580756aa5737ece40b6fcc21379efdb5bcce007e7a16f40d012312396ef02e6c1f09f98e69fb0d0767f47423a1aeddb6e7f'
	    'd65c84a3246dcf5a7c799fd87566834842adfd104bca2c0b0cbd9e2dcae0b900a189154ca9d9c95351c32ff9014cee15f0eb924228e486b58fdcfc624b12d46b')

prepare() {
  cd ${srcdir}/oneTBB-${pkgver}
  patch -Np1 -i ${srcdir}/tbb-ppc-atomic.patch
  patch -Np1 -i ${srcdir}/tbb-2021.4.0-lto.patch
}

build() {
  cd oneTBB-$pkgver

  case "${CARCH}" in
    powerpc|riscv64) EXTRA_CMAKE_FLAGS='-DCMAKE_CXX_FLAGS="-latomic" -DTBB_STRICT=OFF' ;;
    powerpc64le) EXTRA_CMAKE_FLAGS="-DTBB_STRICT=OFF" ;;
  esac

  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DTBB4PY_BUILD=ON -DTBB_ENABLE_IPO=OFF ${EXTRA_CMAKE_FLAGS} .
  ninja all python_build
}

package() {
  cd oneTBB-$pkgver
  DESTDIR="$pkgdir" ninja install
}
