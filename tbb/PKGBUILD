# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Denis Martinez <deuns.martinez AT gmail.com>
# Contributor: Bogdan Burlacu <bogdan.burlacu AT pm.me>

pkgname=tbb
pkgver=2021.13.0
pkgrel=1
pkgdesc='High level abstract threading library'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://www.threadingbuildingblocks.org/'
license=('Apache')
depends=('gcc-libs' 'hwloc')
makedepends=('cmake' 'inetutils' 'ninja' 'python' 'swig' 'python-setuptools')
conflicts=('intel-tbb')
provides=("intel-tbb=$pkgver")
replaces=('intel-tbb')
source=(https://github.com/oneapi-src/oneTBB/archive/v$pkgver/$pkgname-$pkgver.tar.gz
        tbb-ppc-atomic.patch)
sha512sums=('1f536b98c4eb331c3ba21e4b36362c932d7852b974ea7237b5f7bffb0e996d76f688c7ac534cb56f5866cfdffd5d3f015521b4c2ca21e584503070782ca82a2f'
            '7287d7d09e3a00c0ac03a5caab4e7580756aa5737ece40b6fcc21379efdb5bcce007e7a16f40d012312396ef02e6c1f09f98e69fb0d0767f47423a1aeddb6e7f')

prepare() {
  cd ${srcdir}/oneTBB-${pkgver}
  patch -Np1 -i ${srcdir}/tbb-ppc-atomic.patch
}

build() {
  cd oneTBB-$pkgver

  # TBB_STRICT is ON by default and turns on -Werror, but `-DTBB_STRICT=OFF` is currently broken:
  # https://github.com/oneapi-src/oneTBB/issues/847
  # Upstream doesn't support GCC 12 yet:
  # https://github.com/oneapi-src/oneTBB/issues/823#issuecomment-1128997690
  # So we turn off the problematic new warnings in GCC 12 manually.
  export CXXFLAGS="$CXXFLAGS -Wno-error=uninitialized -Wno-error=address"

  case "${CARCH}" in
    powerpc) EXTRA_CMAKE_FLAGS='-DCMAKE_CXX_FLAGS="-latomic" -DTBB_STRICT=OFF' ;;
    riscv64) EXTRA_CMAKE_FLAGS='-DCMAKE_CXX_FLAGS="-latomic" -DTBB_TEST=OFF -DTBB_STRICT=OFF' ;;
    powerpc64*) EXTRA_CMAKE_FLAGS='-DTBB_STRICT=OFF' ;;
  esac

  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DTBB4PY_BUILD=ON ${EXTRA_CMAKE_FLAGS} .
  ninja all python_build
}

package() {
  cd oneTBB-$pkgver
  DESTDIR="$pkgdir" ninja install
}
