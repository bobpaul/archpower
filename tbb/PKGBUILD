# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Denis Martinez <deuns.martinez AT gmail.com>
# Contributor: Bogdan Burlacu <bogdan.burlacu AT pm.me>

pkgname=tbb
pkgver=2021.4.0
pkgrel=2.1
pkgdesc='High level abstract threading library'
arch=(x86_64 powerpc64le powerpc riscv64)
url='https://www.threadingbuildingblocks.org/'
license=('Apache')
depends=('gcc-libs' 'hwloc')
makedepends=('cmake' 'inetutils' 'ninja' 'python' 'swig')
conflicts=('intel-tbb')
provides=("intel-tbb=$pkgver")
replaces=('intel-tbb')
source=(https://github.com/oneapi-src/oneTBB/archive/v$pkgver/$pkgname-$pkgver.tar.gz
        tbb-ppc-atomic.patch)
sha512sums=('96da2bc351fd64dfa854f8e6cabc1c4e53af3d55760e99d6f83ad53779c727af333d13d6be0828ed70371cf796498d2063e9dd0e4b2f6451623bb5d28ccbf20b'
            '7287d7d09e3a00c0ac03a5caab4e7580756aa5737ece40b6fcc21379efdb5bcce007e7a16f40d012312396ef02e6c1f09f98e69fb0d0767f47423a1aeddb6e7f')

prepare() {
  patch -d oneTBB-$pkgver -p1 < tbb-ppc-atomic.patch
}

build() {
  cd oneTBB-$pkgver

  case "${CARCH}" in
    powerpc) EXTRA_CMAKE_FLAGS='-DCMAKE_CXX_FLAGS="-latomic" -DTBB_STRICT=OFF' ;;
    powerpc64le) EXTRA_CMAKE_FLAGS="-DTBB_STRICT=OFF" ;;
    x86_64) EXTRA_CMAKE_FLAGS="-DTBB4PY_BUILD=ON" ;;
  esac

  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr ${EXTRA_CMAKE_FLAGS} .
  ninja
}

package() {
  cd oneTBB-$pkgver
  DESTDIR="$pkgdir" ninja install
}
