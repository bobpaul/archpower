# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Denis Martinez <deuns.martinez AT gmail.com>
# Contributor: Bogdan Burlacu <bogdan.burlacu AT pm.me>

pkgname=tbb
pkgver=2021.3.0
pkgrel=2
pkgdesc='High level abstract threading library'
arch=(x86_64 powerpc64le powerpc)
url='https://www.threadingbuildingblocks.org/'
license=('Apache')
depends=('gcc-libs')
makedepends=('cmake' 'inetutils' 'ninja')
conflicts=('intel-tbb')
provides=("intel-tbb=$pkgver")
replaces=('intel-tbb')
source=(https://github.com/oneapi-src/oneTBB/archive/v$pkgver/$pkgname-$pkgver.tar.gz
        tbb-gcc-11-433.patch::https://github.com/oneapi-src/oneTBB/pull/433.patch
        tbb-gcc-11-447.patch::https://github.com/oneapi-src/oneTBB/pull/447.patch
        tbb-non-x86.patch::https://patch-diff.githubusercontent.com/raw/oneapi-src/oneTBB/pull/461.patch
        tbb-ppc-atomic.patch)
sha512sums=('969bc8d1dcf50bd12f70633d0319e46308eb1667cdc6f0503b373a35dcb2fe6b2adf59c26bd3c8e2a99a8d2d8b9f64088db5a43e784218b163b3661d12908c0e'
            'bd6bfd46c74f45c786c20f72654b554fd20bd105c0739e2e98b17b801f07ee149581df0c4e6f8ed2111dad9da8a9361b5e532572be30604dda32d49102bdca03'
            '756faf918c56115007241e607590d1d370d45bca9313125987d739a271e022a47043f4bd4f47633c9239e1033299876899c3769d7ae17cfaefb114a5462cda95'
	    '2e0b69b6ff768b33c2c1e4069b37876fb1593cf44cd777078071c9840d2207848c3590886a05c180199b7a4cc6806024a21f62decb3d78665019a6ed8a448049'
            '7287d7d09e3a00c0ac03a5caab4e7580756aa5737ece40b6fcc21379efdb5bcce007e7a16f40d012312396ef02e6c1f09f98e69fb0d0767f47423a1aeddb6e7f')

prepare() {
  patch -d oneTBB-$pkgver -p1 < tbb-gcc-11-433.patch
  patch -d oneTBB-$pkgver -p1 < tbb-gcc-11-447.patch
  patch -d oneTBB-$pkgver -p1 < tbb-non-x86.patch
  [ "${CARCH}" == 'powerpc' ] && patch -d oneTBB-$pkgver -p1 < tbb-ppc-atomic.patch
}

build() {
  cd oneTBB-$pkgver

  [ "${CARCH}" == 'powerpc' ] && EXTRA_CMAKE_FLAGS='-DCMAKE_CXX_FLAGS="-latomic"'

  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr ${EXTRA_CMAKE_FLAGS} .
  ninja
}

package() {
  cd oneTBB-$pkgver
  DESTDIR="$pkgdir" ninja install
}
