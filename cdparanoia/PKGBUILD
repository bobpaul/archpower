# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=cdparanoia
pkgver=10.2
pkgrel=8
pkgdesc="Compact Disc Digital Audio extraction tool"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="http://www.xiph.org/paranoia/"
license=('GPL')
depends=('glibc')
options=('!makeflags' '!staticlibs')
source=("https://downloads.xiph.org/releases/cdparanoia/cdparanoia-III-$pkgver.src.tgz"
        gcc.patch
        autoconf-init-ac_cv_objext.patch
        makefile.in-install.patch
        byteswap-null-check.diff)
sha512sums=('4ab0a0f5ef44d56c1af72d1fc1035566a1a89c4eeddb9e8baea675fe51c06138d913342afc8bed167d9fa55672fa25a2763ce21f7e24c1232e4739aff20733a7'
            '27bf216e8b5b9d6971e3999166544025f166587df06065b89657dac62d0d53c01b76f53146c1e9a400dd71460567fadb8fd4ce00576836f972914d69e8855909'
            '20c0630447543573d3f5538942291c700fea16b75d1d568d80bdc97c9b816fee4ed5a12cd646e3047e06bb34b9652a8c9523d48c5bd8e2f0e5947c9115489b8f'
            'fe37e717e9803a895bb4cf7cfffd764253aa0db22891c7b2c2f5e1eb3cfebaedf56a98853fb47e02a24d6ee7e6a5457ccfaa845be75428a68cd1b1fbd68b7924'
            'b2af6748bd9cebc5ff0ff6035440bf534b0c24449e01638291c3a18588bdf0cb10d665f7053c753154e108ad739e3a9083a2bca9022266eb5f30a9954cbf64e2')

prepare() {
  cd cdparanoia-III-$pkgver
  patch -Np1 -i "$srcdir"/gcc.patch
  patch -Np1 -i "$srcdir"/autoconf-init-ac_cv_objext.patch
  patch -Np1 -i "$srcdir"/makefile.in-install.patch
  patch -Np1 -i "$srcdir"/byteswap-null-check.diff

  autoreconf -fiv
}

build() {
  cd cdparanoia-III-$pkgver
  CFLAGS+=' -Du_int16_t=uint16_t -Du_int32_t=uint32_t' \
  ./configure --build=$CHOST --prefix=/usr --mandir=/usr/share/man
  make
}

package() {
  cd cdparanoia-III-$pkgver
  make prefix="$pkgdir"/usr MANDIR="$pkgdir"/usr/share/man install
}
