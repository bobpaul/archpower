# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=libplacebo
pkgver=3.104.0
pkgrel=2
pkgdesc='Reusable library for GPU-accelerated video/image rendering primitives'
url='https://github.com/haasn/libplacebo'
arch=(x86_64 powerpc64le powerpc)
license=('LGPL2.1')
depends=('vulkan-icd-loader' 'glslang' 'lcms2' 'liblcms2.so' 'shaderc' 'libshaderc_shared.so'
         'libepoxy' 'libglvnd' 'glibc' 'gcc-libs')
makedepends=('meson' 'ninja' 'vulkan-headers' 'python' 'python-mako')
provides=('libplacebo.so')
source=(https://code.videolan.org/videolan/libplacebo/-/archive/v${pkgver}/libplacebo-v${pkgver}.tar.gz
        libplacebo-blacklist-fuchsia.patch)
sha512sums=('896ade7d87fe02924cbf5a0f2b39a2054c8b9eb7a5f0adf574d0ec3dc9239020f8c69450501775002f7697250da595bf24a7d2d7039eb396073411c6fce2edfb'
            '148f9a8c81f433f7efd2b16d6bdd910a8565a2b74249d0ec4e4074c424df2cf9e1e64f382dc82f6cb402b6c63ed4439ee8fcfbd4150041d188945b3fa21fc3a9')
b2sums=('dfd743470eb679ddf14b517f856dec1a1df1df88788d8222aade4b31f8d625c66a6bfeb0ff95df2eec531fa6950d68b3ca578cb244104a2591090ad8dcbca6a8'
        'f22c5728f6a391f0e46af2dd0f3450a7782ec48d4dfced52b4cfb4c1fb073d97cc09b1440b09bec0cb65e830f4ea06051d3425808f5883e239c29d899055ce31')

prepare() {
  cd ${pkgbase}-v${pkgver}
  patch -Np1 -i ${srcdir}/libplacebo-blacklist-fuchsia.patch
}

build() {
  cd ${pkgbase}-v${pkgver}
  CXXFLAGS+=" -I/usr/include/glslang"
  arch-meson build \
    -D tests=true \
    -D vulkan=enabled \
    -D glslang=enabled \
    -D shaderc=enabled \
    -D lcms=enabled
  ninja -C build
}

check() {
  cd ${pkgbase}-v${pkgver}
  ninja -C build test
}

package() {
  cd ${pkgbase}-v${pkgver}
  DESTDIR="${pkgdir}" ninja -C build install
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
