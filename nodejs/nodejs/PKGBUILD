# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: James Campos <james.r.campos@gmail.com>
# Contributor: BlackEagle < ike DOT devolder AT gmail DOT com >
# Contributor: Dongsheng Cai <dongsheng at moodle dot com>
# Contributor: Masutu Subric <masutu.arch at googlemail dot com>
# Contributor: TIanyi Cui <tianyicui@gmail.com>

pkgname=nodejs
pkgver=15.11.0
pkgrel=1
pkgdesc='Evented I/O for V8 javascript'
arch=(x86_64 powerpc64le powerpc)
url='https://nodejs.org/'
license=('MIT')
# c-ares: wait until a newer release than 1.16.1
depends=('openssl' 'zlib' 'icu' 'libuv' 'libnghttp2') # 'c-ares' 'http-parser' 'v8')
makedepends=('python' 'procps-ng')
provides=('npm')
conflicts=('npm')
source=("nodejs-$pkgver.tar.gz::https://github.com/nodejs/node/archive/v$pkgver.tar.gz")
sha512sums=('25985b38301dd9de042dba3384f1acbfeedff804ab9e27a0b30bdda2fbc00c9ca46be744ad263995fe5549e6af0369523704859fc70eaff9fa73d2b439e7a678')

build() {
  cd node-$pkgver

  case "${CARCH}" in
    powerpc) CPU=ppc ;;
    powerpc64le) CPU=ppc64 ;;
    x86_64) CPU=x64 ;;
  esac

  ./configure \
    --dest-cpu=${CPU} \
    --prefix=/usr \
    --with-intl=system-icu \
    --shared-openssl \
    --shared-zlib \
    --shared-libuv \
    --experimental-http-parser \
    --shared-nghttp2
    # --shared-cares
    # --shared-v8
    # --shared-http-parser

  make
}

check() {
  cd node-$pkgver
  make test || :
}

package() {
  cd node-$pkgver

  make DESTDIR="$pkgdir" install

  install -D -m644 LICENSE \
    "$pkgdir"/usr/share/licenses/nodejs/LICENSE
}

# vim:set ts=2 sw=2 et:
