# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: James Campos <james.r.campos@gmail.com>
# Contributor: BlackEagle < ike DOT devolder AT gmail DOT com >
# Contributor: Dongsheng Cai <dongsheng at moodle dot com>
# Contributor: Masutu Subric <masutu.arch at googlemail dot com>
# Contributor: TIanyi Cui <tianyicui@gmail.com>

pkgname=nodejs
pkgver=16.5.0
pkgrel=1
pkgdesc='Evented I/O for V8 javascript'
arch=(x86_64 powerpc64le)
url='https://nodejs.org/'
license=('MIT')
depends=('openssl' 'zlib' 'icu' 'libuv' 'libnghttp2' 'c-ares') # 'http-parser' 'v8')
provides=('npm')
conflicts=('npm')
makedepends=('python' 'procps-ng')
source=("https://github.com/nodejs/node/archive/v$pkgver/nodejs-$pkgver.tar.gz")
sha512sums=('cc5c5681101766dba6701906cbcd862634e02acbc966f8fe33b2a0edc69a4e603846ba1a65d476b0b2860d7d77f449cb4324b3bc6041ebf049d82f28bfafc0dd')

build() {
  cd node-$pkgver
   case "${CARCH}" in
     powerpc64le) CPU=ppc64 ;;
     x86_64) CPU=x64 ;;
   esac

  ./configure \
    --prefix=/usr \
    --with-intl=system-icu \
    --dest-cpu=${CPU} \
    --shared-openssl \
    --shared-zlib \
    --shared-libuv \
    --experimental-http-parser \
    --shared-nghttp2
    # --shared-cares
    # --shared-v8
    # --shared-http-parser

  make
}

check() {
  cd node-$pkgver
  make test || :
}

package() {
  cd node-$pkgver

  make DESTDIR="$pkgdir" install

  install -D -m644 LICENSE \
    "$pkgdir"/usr/share/licenses/nodejs/LICENSE
}

# vim:set ts=2 sw=2 et:
