# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Laurent Carlier <lordheavym@gmail.com>

pkgname=spirv-tools
pkgver=2021.4
pkgrel=1.1
pkgdesc="API and commands for processing SPIR-V modules"
arch=(x86_64 powerpc64le powerpc riscv64)
url="https://www.khronos.org/vulkan/"
license=('custom')
groups=(vulkan-devel)
depends=('gcc-libs')
makedepends=('cmake' 'python' 'git' 'ninja')
source=("${pkgname}::git+https://github.com/KhronosGroup/SPIRV-Tools.git#tag=v${pkgver}"
        "${pkgname}-headers::git+https://github.com/KhronosGroup/SPIRV-Headers.git"
        "${pkgname}-pr4622.patch::https://patch-diff.githubusercontent.com/raw/KhronosGroup/SPIRV-Tools/pull/4622.patch")
sha256sums=('SKIP'
            'SKIP'
            '39659e39695712cfaf18fffed4f6bbe76ead0514081ecc9384e0b682648dccd1')

prepare() {
  cd ${srcdir}/${pkgname}
  # addresses QTBUG-93101
  patch -Np1 -i ${srcdir}/${pkgname}-pr4622.patch
  rm -rf build && mkdir build
}

build() {
  cd ${srcdir}/${pkgname}/build

  cmake .. \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DCMAKE_BUILD_TYPE=Release \
      -DSPIRV_WERROR=Off \
      -DBUILD_SHARED_LIBS=ON \
      -DSPIRV_TOOLS_BUILD_STATIC=OFF \
      -DSPIRV-Headers_SOURCE_DIR="${srcdir}"/${pkgname}-headers
  ninja
}

package() {
  cd ${srcdir}/${pkgname}/build

  DESTDIR="${pkgdir}" ninja install

  install -dm755 "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m644 ../LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/
}
