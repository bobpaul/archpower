# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=libgovirt
epoch=2
pkgver=0.3.9
pkgrel=1
pkgdesc='GObject-based library to access oVirt REST API'
arch=(x86_64 powerpc64le powerpc riscv64)
url='https://gitlab.gnome.org/GNOME/libgovirt'
license=(LGPL)
depends=(librest)
makedepends=(gobject-introspection meson)
source=("https://gitlab.gnome.org/GNOME/libgovirt/-/archive/v${pkgver}/libgovirt-v${pkgver}.tar.bz2"
        libgovirt-no-werror.patch)
b2sums=('383660540bcde90e4406e086f8acc98af233b83cd8a8d7f3634fa1de4e6cc43ceeae2f476a16a65732c3cf5c302a2f108b9c5b1d67b39b9999d1de48af22da24'
        'd8504ea0cdc1ad423b2fe1805db0b168684be1a6aa447288a2abfe65198d395f23b892ccb0e98c5a98f9e85ce9c352efd2bb16c8b1ffa739f38d0840579dee8d')

prepare() {
  cd ${pkgname}-v${pkgver}
  patch -Np1 -i ${srcdir}/libgovirt-no-werror.patch
}
  
build() {
  arch-meson ${pkgname}-v${pkgver} build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "${pkgdir}"
}
