# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=adobe-source-sans-pro-fonts
pkgver=3.028
pkgrel=1
pkgdesc="Sans-serif font family for user interface environments"
url="https://adobe-fonts.github.io/source-sans-pro/"
arch=(any)
license=(custom)
_relver=${pkgver}R
_tarname=source-sans-$_relver
_oldver=3.006R
_oldname=source-sans-$_oldver
source=("$_tarname.tar.gz::https://github.com/adobe-fonts/source-sans-pro/archive/$_relver.tar.gz"
        "$_oldname.tar.gz::https://github.com/adobe-fonts/source-sans-pro/archive/$_oldver.tar.gz")
sha256sums=('23df371ce8ce0c5c57e750f1b01af6a31e437fe1737df0501b8181162642686a'
            '45dbc4994892710367e801869d8ad6b26589030977b660f6a968de36147ffd32')

package() {
  # "Source Sans Pro"
  cd $_oldname
  install -Dt "$pkgdir/usr/share/fonts/${pkgname%-fonts}" -m644 OTF/*.otf VAR/*.otf
  cd ..

  # "Source Sans 3"
  cd $_tarname
  install -Dt "$pkgdir/usr/share/fonts/${pkgname%-fonts}" -m644 OTF/*.otf VAR/*.otf
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 LICENSE.md
}

# vim:set ts=2 sw=2 et:
