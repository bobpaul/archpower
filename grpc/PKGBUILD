# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Aleksey Filippov <sarum9in@gmail.com>
# Contributor: Victor Aur√©lio Santos <victoraur.santos@gmail.com>

pkgbase='grpc'
pkgname=('grpc' 'python-grpcio' 'php-grpc' 'php7-grpc' 'grpc-cli')
pkgver=1.38.0
_gtestver=c9ccac7cb7345901884aabf5d1a786cfa6e2f397
_abseilcppver=997aaf3a28308eba1b9156aa35ab7bca9688e9f6
pkgrel=2
pkgdesc="High performance, open source, general RPC framework that puts mobile and HTTP/2 first."
arch=(x86_64 powerpc64le powerpc)
url='https://grpc.io'
license=('BSD')
makedepends=('re2' 're2c' 'protobuf' 'php' 'php7' 'c-ares' 'openssl' 'chrpath' 'cython'
             'cmake' 'benchmark' 'python-setuptools')
source=("https://github.com/grpc/grpc/archive/v$pkgver/$pkgbase-$pkgver.tar.gz"
        "https://github.com/google/googletest/archive/$_gtestver/googletest-$_gtestver.tar.gz"
	"https://github.com/abseil/abseil-cpp/archive/$_abseilcppver/abseil-cpp-$_abseilcppver.tar.gz")
sha256sums=('abd9e52c69000f2c051761cfa1f12d52d8b7647b6c66828a91d462e796f2aede'
            '443d383db648ebb8e391382c0ab63263b7091d03197f304390baac10f178a468'
	    '35f22ef5cb286f09954b7cc4c85b5a3f6221c9d4df6b8c4a1e9d399555b366ee')

prepare() {
  cd "$srcdir/$pkgbase-$pkgver"

  ln -sf "$srcdir/googletest-$_gtestver/"{googlemock,googletest} \
    third_party/googletest

  rm -rf third_party/abseil-cpp
  ln -s "$srcdir/abseil-cpp-$_abseilcppver/" third_party/abseil-cpp

  mkdir build

  cp -a src/php{,7}
}

build() {
  cd "$srcdir/$pkgbase-$pkgver/build"

  case "${CARCH}" in
    powerpc) EXTRA_CMAKE_FLAGS="-DCMAKE_CXX_STANDARD_LIBRARIES=-latomic" ;;
  esac

  cmake \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_CXX_FLAGS="${CXXFLAGS} -DNDEBUG" \
    -DgRPC_INSTALL=ON \
    -DBUILD_SHARED_LIBS=ON \
    -DgRPC_BUILD_TESTS=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DgRPC_CARES_PROVIDER=package \
    -DgRPC_PROTOBUF_PROVIDER=package \
    -DgRPC_SSL_PROVIDER=package \
    -DgRPC_ZLIB_PROVIDER=package \
    -DgRPC_RE2_PROVIDER=package \
    -DgRPC_BENCHMARK_PROVIDER=package ${EXTRA_CMAKE_FLAGS} \
    ..

  make
  export LDFLAGS="$LDFLAGS -L$PWD"

  # PHP
  cd "$srcdir/$pkgbase-$pkgver/src/php/ext/$pkgbase"
  phpize
  ./configure --enable-grpc="$srcdir/$pkgbase-$pkgver"
  make

  # PHP7
  cd "$srcdir/$pkgbase-$pkgver/src/php7/ext/$pkgbase"
  phpize7
  ./configure --enable-grpc="$srcdir/$pkgbase-$pkgver"
  make

  # Python
  cd "$srcdir/$pkgbase-$pkgver"
  GRPC_PYTHON_BUILD_WITH_CYTHON=1 \
  GRPC_PYTHON_BUILD_SYSTEM_OPENSSL=1 \
  GRPC_PYTHON_BUILD_SYSTEM_ZLIB=1 \
  GRPC_PYTHON_BUILD_SYSTEM_CARES=1 \
  GRPC_PYTHON_BUILD_SYSTEM_RE2=1 \
  GRPC_BUILD_WITH_BORING_SSL_ASM=0 \
  python setup.py build
}

package_grpc() {
  depends=('c-ares' 'protobuf' 'openssl' 're2')
  provides=(
    'libupb.so'
    'libgrpcpp_channelz.so'
    'libgrpc_unsecure.so'
    'libgrpc_plugin_support.so'
    'libgrpc.so'
    'libgrpc++_unsecure.so'
    'libgrpc++_reflection.so'
    'libgrpc++_error_details.so'
    'libgrpc++_alts.so'
    'libgrpc++.so'
    'libgpr.so'
    'libaddress_sorting.so'
  )

  cd "$srcdir/$pkgbase-$pkgver/build"
  make DESTDIR="$pkgdir" install
  install -Dm644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python-grpcio() {
  pkgdesc='Python language bindings for grpc, remote procedure call (RPC) framework'
  depends=('c-ares' 'python' 're2')

  cd "$srcdir/$pkgbase-$pkgver"
  python setup.py install -O1 --skip-build --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_php-grpc() {
  pkgdesc='gRPC module for PHP'
  depends=('grpc' 'php')
  backup=('etc/php/conf.d/grpc.ini')

  # Install PHP extension.
  cd "$srcdir/$pkgbase-$pkgver/src/php/ext/$pkgbase"
  make install "INSTALL_ROOT=$pkgdir"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  # Remove RPATH
  chrpath -d "$pkgdir/usr/lib/php/modules/grpc.so"

  echo ';extension=grpc.so' >grpc.ini
  install -Dm644 grpc.ini "$pkgdir/etc/php/conf.d/grpc.ini"
}

package_php7-grpc() {
  pkgdesc='gRPC module for PHP'
  depends=('grpc' 'php7')
  backup=('etc/php7/conf.d/grpc.ini')

  # Install PHP extension.
  cd "$srcdir/$pkgbase-$pkgver/src/php7/ext/$pkgbase"
  make install "INSTALL_ROOT=$pkgdir"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  # Remove RPATH
  chrpath -d "$pkgdir/usr/lib/php7/modules/grpc.so"

  echo ';extension=grpc.so' >grpc.ini
  install -Dm644 grpc.ini "$pkgdir/etc/php7/conf.d/grpc.ini"
}

package_grpc-cli() {
  pkgdesc='gRPC protocol buffers cli'
  depends=('grpc')

  cd "$srcdir/$pkgbase-$pkgver"
  install -dm0755 "$pkgdir/usr/lib"
  cp -a build/libgrpc++_test_config.so* "$pkgdir/usr/lib/"

  for solib in build/third_party/abseil-cpp/absl/flags/*.so; do
    install -t "$pkgdir/usr/lib" "$solib"
  done

  install -Dm0755 build/grpc_cli "$pkgdir/usr/bin/grpc_cli"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
