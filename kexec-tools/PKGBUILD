# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Contributor: Camille Moncelier <pix@devlife.org>, simo <simo@archlinux.org>
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=kexec-tools
pkgver=2.0.28
pkgrel=1
pkgdesc="Load another kernel from the currently executing Linux kernel"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://kernel.org/pub/linux/utils/kernel/kexec/"
source=("git+https://git.kernel.org/pub/scm/utils/kernel/kexec/kexec-tools.git#tag=v${pkgver}"
        ppc64-elfv2.patch
        riscv64.patch)
makedepends=('git')
depends=('zlib' 'xz')
license=('GPL2')

prepare() {
  cd $pkgname

  patch -Np1 -i ${srcdir}/ppc64-elfv2.patch
  #patch -Np1 -i ${srcdir}/riscv64.patch

  ./bootstrap
}

build() {
  cd $pkgname
  CFLAGS+=' -fcommon' # https://wiki.gentoo.org/wiki/Gcc_10_porting_notes/fno_common
  ./configure --prefix=/usr --sbindir=/usr/bin --without-lzma
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  install -D -m644 kexec/kexec.8 "$pkgdir"/usr/share/man/man8/kexec.8
}

md5sums=('SKIP'
         '5f101da9fb7b65350a66018d5088cceb'
         '2b5bd610b30d5e6586000cfdc707cb93')
