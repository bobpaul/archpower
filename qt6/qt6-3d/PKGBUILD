# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=qt6-3d
_qtver=6.1.2
pkgver=${_qtver/-/}
pkgrel=1
arch=(x86_64 powerpc64le powerpc)
url='https://www.qt.io'
license=(GPL3 LGPL3 FDL custom)
pkgdesc='C++ and QML APIs for easy inclusion of 3D graphics'
depends=(qt6-base)
makedepends=(cmake qt6-declarative qt6-shadertools vulkan-headers) # assimp
optdepends=('qt6-shadertools: RHI renderer' 'qt6-declarative: QML bindings')
groups=(qt6)
_pkgfn="${pkgname/6-/}-everywhere-src-$_qtver"
source=(https://download.qt.io/official_releases/qt/${pkgver%.*}/$_qtver/submodules/$_pkgfn.tar.xz
        qt6-3d-gcc11.patch)
sha256sums=('632e6dfa3af4e9ef6d8c54843e5db62b07b3d8a3435d2687e4dd256efd8f0d2d'
            'b056d66a24956755c2aaddc4adddd71f5a945ff9795b0cbf9f43ab4d43b4f8f5')

prepare() {
  patch -d $_pkgfn -p1 < qt6-3d-gcc11.patch # Fix build with GCC 11
}

build() {
  cmake -B build -S $_pkgfn
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt6-base "$pkgdir"/usr/share/licenses/$pkgname
}
