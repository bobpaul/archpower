# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=qt6-quick3d
_qtver=6.2.3
pkgver=${_qtver/-/}
pkgrel=3
arch=(x86_64 powerpc64le riscv64)
url='https://www.qt.io'
license=(GPL3)
pkgdesc='Qt module and API for defining 3D content in Qt Quick'
depends=(qt6-quicktimeline)
makedepends=(cmake ninja qt6-shadertools assimp)
optdepends=('qt6-shadertools: for shadergen' 'assimp: assimp import plugin')
groups=(qt6)
options=(debug)
_pkgfn="${pkgname/6-/}-everywhere-src-$_qtver"
source=(https://download.qt.io/official_releases/qt/${pkgver%.*}/$_qtver/submodules/$_pkgfn.tar.xz
        qt6-quick3d-assimp-5.1.patch)
sha256sums=('c45294f917232320a2cf67ee25b518f6ff3cdf989b8cecfb2ed73569ab28661e'
            '49eb8feec3fdd1972266cf97fd8d40f0eb7e544c1957569c4b9f182348b17492')

prepare() {
  patch -d $_pkgfn -p1 < qt6-quick3d-assimp-5.1.patch # Fix build with assimp 5.1
}

build() {
  case "${CARCH}" in
    riscv64) _cmake_options+=(-DCMAKE_CXX_FLAGS="-latomic") ;;
  esac

  cmake -B build -S $_pkgfn -G Ninja ${_cmake_options}
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt6-base "$pkgdir"/usr/share/licenses/$pkgname
}
