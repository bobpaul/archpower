# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=qt6-shadertools
_qtver=6.2.0
pkgver=${_qtver/-/}
pkgrel=1
arch=(x86_64 powerpc64le powerpc)
url='https://www.qt.io'
license=(GPL3)
pkgdesc='Provides functionality for the shader pipeline that allows Qt Quick to operate on Vulkan, Metal, and Direct3D, in addition to OpenGL'
depends=(qt6-base)
makedepends=(cmake ninja)
groups=(qt6)
_pkgfn="${pkgname/6-/}-everywhere-src-$_qtver"
source=(https://download.qt.io/official_releases/qt/${pkgver%.*}/$_qtver/submodules/$_pkgfn.tar.xz)
sha256sums=('5f66d43610a3a6739fc360d836a2c045135107c0ecd40eb3ed18ce5f3dd79c42')

build() {
  case "${CARCH}" in
    powerpc) PLATFORM_CMAKE_FLAGS="-DNOGLSL=ON -DNOHLSL=ON -DNOMSL=ON" ;;
  esac


  cmake -B build -S $_pkgfn -G Ninja ${PLATFORM_CMAKE_FLAGS}
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt6-base "$pkgdir"/usr/share/licenses/$pkgname
}
