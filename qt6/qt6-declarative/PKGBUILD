# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=qt6-declarative
_pkgver=6.8.0
pkgver=6.8.0
pkgrel=4
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://www.qt.io'
license=(GPL3 LGPL3 FDL custom)
pkgdesc='Classes for QML and JavaScript languages'
depends=(gcc-libs
         glibc
         qt6-base
         qt6-svg)
makedepends=(cmake
             git
             ninja
             python
             qt6-languageserver
             qt6-shadertools
             qt6-tools)
groups=(qt6)
options=(!debug)
_pkgfn=${pkgname/6-/}
source=(git+https://code.qt.io/qt/$_pkgfn#tag=v$_pkgver
        big-endian-qtquick-software.patch
        qtdeclarative-6.7.3-QTBUG-125053.patch
        qtdeclarative-6.8.0-QTBUG-129797.patch)
sha256sums=('89b497818a0e36bf08f5a42324dcde1b441b5a0ee1c9b6cfa38fb281479de3ed'
            'd4ef5f1c361345e7ae6cf41143a1d6ae66e9f9d5c77b1c75e96c0e2164735e25'
            '7f12ff4220f796a723562c812bf268a28a0ab64381c0d1b4905d828267856eee'
            'e44dfbd573cfaae15e8f03683062250d5cf87663803ebb26db6bb238f38834eb')

prepare() {
  cd $_pkgfn
# Fix crashes in Plasma
  git cherry-pick -n 0ae3697cf40bcd3ae1de20621abad17cf6c5f52d
  git cherry-pick -n 861a7921ed7833a90f77b5fca436c789a4f9517c

  patch -Np1 -i ${srcdir}/big-endian-qtquick-software.patch
  patch -Np1 -i ${srcdir}/qtdeclarative-6.7.3-QTBUG-125053.patch
  patch -Np1 -i ${srcdir}/qtdeclarative-6.8.0-QTBUG-129797.patch
}

build() {
  cmake -B build -S $_pkgfn -G Ninja \
    -DQT_ALLOW_DOWNLOAD=OFF \
    -DINSTALL_PUBLICBINDIR=usr/bin \
    -DCMAKE_MESSAGE_LOG_LEVEL=STATUS \
    -DQT_BUILD_EXAMPLES_BY_DEFAULT=OFF \
    -DCMAKE_CXX_FLAGS="$CXXFLAGS -ffat-lto-objects"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

# Install symlinks for user-facing tools
  cd "$pkgdir"
  mkdir usr/bin
  while read _line; do
    ln -s $_line
  done < "$srcdir"/build/user_facing_tool_links.txt

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt6-base "$pkgdir"/usr/share/licenses/$pkgname
}
