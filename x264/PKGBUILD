# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Paul Mattal <paul@archlinux.org>

pkgname=x264
pkgver=0.164.r3191.4613ac3
pkgrel=1
epoch=3
pkgdesc='Open Source H264/AVC video encoder'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url=https://www.videolan.org/developers/x264.html
license=(GPL-2.0-only)
depends=(liblsmash.so)
makedepends=(
  git
)
makedepends_x86_64=(
  nasm
)
provides=(
  libx264
  libx264.so
)
conflicts=(
  libx264
  libx264-10bit
  libx264-all
)
replaces=(
  libx264
  libx264-10bit
  libx264-all
)
_commit=4613ac3c15fd75cebc4b9f65b7fb95e70a3acce1
source=(git+https://code.videolan.org/videolan/x264.git#commit=${_commit})
sha256sums=('2a83dab4d8939877b080b12b2898e9b7158585d913a2f7cf8c2bc3d0b636a263')

pkgver() {
  cd x264
  ./version.sh | grep X264_POINTVER | sed -r 's/^#define X264_POINTVER "([0-9]+\.[0-9]+)\.([0-9]+) (.*)"$/\1.r\2.\3/'
}

build() {
  cd x264
  ./configure \
    --prefix=/usr \
    --enable-shared \
    --enable-pic \
    --enable-lto \
    --disable-avs
  make
}

package() {
  make -C x264 DESTDIR="${pkgdir}" BASHCOMPLETIONSDIR="/usr/share/bash-completion/completions" install-cli install-lib-shared install-bashcompletion
}

# vim: ts=2 sw=2 et:
