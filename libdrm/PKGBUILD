# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=libdrm
pkgver=2.4.120
pkgrel=1
pkgdesc="Userspace interface to kernel DRM services"
url="https://dri.freedesktop.org/"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=('custom')
depends=('libpciaccess' 'glibc')
makedepends=('python-docutils' 'meson' 'cairo')
optdepends=('cairo: needed for modetest tool')
replaces=('libdrm-new' 'libdrm-nouveau')
source=(https://dri.freedesktop.org/$pkgname/$pkgname-$pkgver.tar.xz{,.sig}
        COPYING
        0001-fix-export-symbols.patch
        0003-libsync-add-support-for-pre-v4.7-kernels.patch
        0004-Add-sync_fence_info-and-sync_pt_info.patch
        0005-xf86drm-add-support-for-populating-drm-formats.patch
        0006-Add-sync_file_info-and-sync_get_fence_info.patch)
sha512sums=('6dc16e5134a669eeb59debb1dc2d15b857483ab7476dc2b94bd05a32d8953f046f5656f6cf9e1a63e97e7156fb65ebb58b6a29fe45cb6326058baaf820626e70'
            'SKIP'
            'b0ca349b882a4326b19f81f22804fabdb6fb7aef31cdc7b16b0a7ae191bfbb50c7daddb2fc4e6c33f1136af06d060a273de36f6f3412ea326f16fa4309fda660'
            'b43d88cb73cb6f27fc426427d3ec18465813ac317630c2da0f8b608a4776ee111e4b0ba92ec7b1e36d00ef57ab90a44fc67922fd676364541de61c4de77b5ba1'
            '08b383319336e2064e13c0bb6d809c47e3949169d4b7aff8ac210e41455c31393b8563f8d32b72341fc6ebae6a834b7d8200e583bcd4c1e42d208ddf71a5d38e'
            '77579c20c130f99b8771169c8ccfbbe837a132c26de398d98d62701dd9e0992f1d1add67069f68033d2b43f2c8eed88c4528e46f4ef572c22119249c4cd6b7e0'
            '83240c951fe54c615e90277e5df5a754629a2725f09c38ca81664f34fc5fff08a7f4f809e815acfd086cab700a2b8062190cacfd27b2d924c0858fb1fee45d01'
            '176cf420fd27bb3418bed8887c86b49df42969357cf01d872b1abdfa5b8564694bf1410b9b094542f7068ab96ef0336b86205c0bedcf5658e5fe95d02d65e4c8')
#validpgpkeys=('10A6D91DA1B05BD29F6DEBAC0C74F35979C486BE') # David Airlie <airlied@redhat.com>
validpgpkeys=('34FF9526CFEF0E97A340E2E40FDE7BE0E88F5E48') # emersion <contact@emersion.fr>
#validpgpkeys=('E9E246A89E65374EB8355796B8B5FA0EFC1EC2FE') # "Leo Liu <leo.liu@amd.com>"
#validpgpkeys=('899A810900B815807742E6D895A677A230AC4AA9') # Bas Nieuwenhuizen <bas@basnieuwenhuizen.nl>
#validpgpkeys=('2617F6F8DEBB0A7A15C405C983FD600E340FB5FF') # Samuel Pitoiset <samuel.pitoiset@gmail.com>

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ${srcdir}/0001-fix-export-symbols.patch
  patch -Np1 -i ${srcdir}/0003-libsync-add-support-for-pre-v4.7-kernels.patch
  patch -Np1 -i ${srcdir}/0004-Add-sync_fence_info-and-sync_pt_info.patch
  patch -Np1 -i ${srcdir}/0005-xf86drm-add-support-for-populating-drm-formats.patch
  patch -Np1 -i ${srcdir}/0006-Add-sync_file_info-and-sync_get_fence_info.patch
}

build() {
  arch-meson $pkgname-$pkgver build \
    -D udev=true \
    -D etnaviv=enabled \
    -D freedreno=disabled \
    -D vc4=disabled \
    -D valgrind=disabled \
    -D install-test-programs=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 COPYING
}
