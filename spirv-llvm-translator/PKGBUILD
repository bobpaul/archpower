# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Daniel Bermond <dbermond@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

_srcname=SPIRV-LLVM-Translator
pkgname=${_srcname,,}
pkgver=16.0.0
pkgrel=1
pkgdesc="Tool and a library for bi-directional translation between SPIR-V and LLVM IR"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://github.com/KhronosGroup/SPIRV-LLVM-Translator"
license=(custom)
depends=(llvm-libs spirv-tools)
makedepends=(git cmake llvm spirv-headers)
checkdepends=(python python-setuptools clang)
source=(https://github.com/KhronosGroup/SPIRV-LLVM-Translator/archive/v${pkgver}.tar.gz
        spirv-llvm-translator-16.0.0-llvm-link-llvm-dylib.patch
        spirv-llvm-translator-16.0.0-ld_library_path.patch)
sha256sums=('305fac5bb8efdad9054f0d27b5b765aca8b3349a500e2ba0c927763e42badc2b'
            '62d9b1d1246918fdafea198bd3e80d80b05b0ab0e4c09c5bf39a57867dac9a1a'
            'a36ce0c02bf3939199966ca049c2269d2c00e753ff906f74a2ca8e3712c24602')
options=(!distcc)

prepare() {
  cd SPIRV-LLVM-Translator-${pkgver}
  patch -Np1 -i ${srcdir}/spirv-llvm-translator-16.0.0-llvm-link-llvm-dylib.patch
  patch -Np1 -i ${srcdir}/spirv-llvm-translator-16.0.0-ld_library_path.patch
}

build() {
  cmake -B build -S SPIRV-LLVM-Translator-${pkgver} \
    -DBUILD_SHARED_LIBS=ON \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_POSITION_INDEPENDENT_CODE=ON \
    -DCMAKE_SKIP_RPATH=ON \
    -DLLVM_INCLUDE_TESTS=ON \
    -DLLVM_EXTERNAL_LIT=/usr/bin/lit \
    -DLLVM_EXTERNAL_SPIRV_HEADERS_SOURCE_DIR=/usr/include/spirv/ \
    -Wno-dev
  make -C build
}

check() {
  LD_LIBRARY_PATH="${srcdir}/build/lib/SPIRV" make -C build test
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  install -Dm755 build/tools/llvm-spirv/llvm-spirv -t "${pkgdir}"/usr/bin
  install -Dm644 SPIRV-LLVM-Translator-${pkgver}/LICENSE.TXT -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
