# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# vim: set ts=2 sw=2 et:
# Maintainer:

pkgname=pacman
pkgver=6.0.0
pkgrel=3.1
pkgdesc="A library-based package manager with dependency support"
arch=(x86_64 powerpc64le powerpc)
url="https://www.archlinux.org/pacman/"
license=('GPL')
groups=('base-devel')
depends=('bash' 'glibc' 'libarchive' 'curl' 'gpgme' 'archpower-keyring' 'brotli')
makedepends=('meson' 'asciidoc' 'doxygen')
checkdepends=('python' 'fakechroot')
optdepends=('perl-locale-gettext: translation support in makepkg-template')
provides=('libalpm.so')
backup=(etc/pacman.conf
        etc/makepkg.conf)
options=('strip' 'debug')
validpgpkeys=('6645B0A8C7005E78DB1D7864F99FFE0FEAE999BD'  # Allan McRae <allan@archlinux.org>
              'B8151B117037781095514CA7BBDFFC92306B1121') # Andrew Gregory (pacman) <andrew@archlinux.org>
source=(https://sources.archlinux.org/other/pacman/$pkgname-$pkgver.tar.xz{,.sig}
        pacman-6.0.0-fix-404-download.patch::https://git.archlinux.org/pacman.git/patch/?id=3401f9e142ac4c701cd98c52618cb13164f2146b
        pacman-6.0.0-fix-key-import-double-free.patch::https://git.archlinux.org/pacman.git/patch/?id=542910d684191eb7f25ddc5d3d8fe3060028a267
        pacman.conf
        pacman.conf.ppc32
        makepkg.conf)
sha256sums=('004448085a7747bdc7a0a4dd5d1fb7556c6b890111a06e029ab088f9905d4808'
            'SKIP'
            'fe7e037e1b84bfa5bc401650d148c2a2e87d827705a6ec18a596ff5eea3cc0fd'
            'a51b57dd47818d97e29ac0b8604b4b409916bbe6029adfbb03ac7e7c2b2a0819'
            '81bf2288212917533671d4056aec9126d48a212af8ec0ebe73c5169a5ffdad68'
            '52d89af5dab145bc505f39176395f5aecc345fe9246718e56b2dd9a7b8ddde0b'
            '196139a68b36e1511684373f05e8d58ee88643e092c1ec735616b2de329e6546')

prepare() {
  cd "$pkgname-$pkgver"
  patch -p1 -i "$srcdir"/pacman-6.0.0-fix-404-download.patch
  patch -p1 -i "$srcdir"/pacman-6.0.0-fix-key-import-double-free.patch
}

build() {
  cd "$pkgname-$pkgver"

  meson --prefix=/usr \
        --buildtype=plain \
        -Ddoc=enabled \
        -Ddoxygen=enabled \
        -Dscriptlet-shell=/usr/bin/bash \
        -Dldconfig=/usr/bin/ldconfig \
        build

  meson compile -C build
}

check() {
  cd "$pkgname-$pkgver"

  meson test -C build
}

package() {
  cd "$pkgname-$pkgver"

  DESTDIR="$pkgdir" meson install -C build


  sed "s@^Architecture.*@Architecture = ${CARCH}@g" -i "$srcdir/pacman.conf"
  sed "s@%ARCH%@${CARCH}@g" -i "$srcdir/makepkg.conf"

  case "${CARCH}" in
    powerpc64le)
      _CFLAGS="-O3 -pipe -mcpu=power8 -mabi=altivec -maltivec"
      ;;
    *)
      _CFLAGS="-O3 -pipe"
      ;;
  esac
  sed "s@%CFLAGS%@${_CFLAGS}@g" -i "$srcdir/makepkg.conf"

  case "${CARCH}" in
    powerpc) PACMAN_CONF="pacman.conf.ppc32" ;;
    *)       PACMAN_CONF="pacman.conf" ;;
  esac

  # install Arch specific stuff
  install -dm755 "$pkgdir/etc"
  install -m644 "$srcdir/${PACMAN_CONF}" "$pkgdir/etc/pacman.conf"
  install -m644 "$srcdir/makepkg.conf" "$pkgdir/etc"
}
