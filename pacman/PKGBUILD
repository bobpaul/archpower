# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# vim: set ts=2 sw=2 et:
# Maintainer: Dan McGee <dan@archlinux.org>
# Maintainer: Dave Reisner <dreisner@archlinux.org>

pkgname=pacman
pkgver=5.2.2
pkgrel=4
pkgdesc="A library-based package manager with dependency support"
arch=(powerpc)
url="https://www.archlinux.org/pacman/"
license=('GPL')
groups=('base-devel')
depends=('bash' 'glibc' 'libarchive' 'curl' 'brotli'
         'gpgme' 'archpower-keyring')
makedepends=('asciidoc')
checkdepends=('python' 'fakechroot')
optdepends=('perl-locale-gettext: translation support in makepkg-template')
provides=('libalpm.so')
backup=(etc/pacman.conf
        etc/makepkg.conf)
options=('strip' 'debug')
validpgpkeys=('6645B0A8C7005E78DB1D7864F99FFE0FEAE999BD'  # Allan McRae <allan@archlinux.org>
              'B8151B117037781095514CA7BBDFFC92306B1121') # Andrew Gregory (pacman) <andrew@archlinux.org>
source=(https://sources.archlinux.org/other/pacman/$pkgname-$pkgver.tar.gz{,.sig}
        pacman.conf
        pacman.conf.ppc32
        makepkg.conf
        pacman-5.2.2-fix-strip-messing-up-file-attributes.patch::'https://git.archlinux.org/pacman.git/patch/?id=88d054093c1c99a697d95b26bd9aad5bc4d8e170')
sha256sums=('bb201a9f2fb53c28d011f661d50028efce6eef2c1d2a36728bdd0130189349a0'
            'SKIP'
            'dba8797c50584f5bf9f23658b647db3528adb3ebe4165fecef7d3273bc1e2720'
            '0a1ba8067d04e747a110cc482b62762b7ee6ed95643a1c7ef030d8d8620e3be2'
            'e045f48a0d223bfc4bd17db380179d37c93004b20c963c619e8d52505f95a548'
            '871fd97b3f13f1718358e4b8e046a56c0262c9042b5e3b5d60835606735798bd')

prepare() {
  cd "$pkgname-$pkgver"
  patch -Np1 < ../pacman-5.2.2-fix-strip-messing-up-file-attributes.patch
}

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc \
    --localstatedir=/var --enable-doc \
    --with-scriptlet-shell=/usr/bin/bash \
    --with-ldconfig=/usr/bin/ldconfig
  make V=1
}

check() {
  make -C "$pkgname-$pkgver" check
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  # install Arch specific stuff
  install -dm755 "$pkgdir/etc"

  sed "s@^Architecture.*@Architecture = ${CARCH}@g" -i "$srcdir/pacman.conf"
  sed "s@%ARCH%@${CARCH}@g" -i "$srcdir/makepkg.conf"

  case "${CARCH}" in
    powerpc64le)
      _CFLAGS="-O2 -pipe -mcpu=power8 -mabi=altivec -maltivec"
      ;;
    *)
      _CFLAGS="-O2 -pipe"
  esac
  sed "s@%CFLAGS%@${_CFLAGS}@g" -i "$srcdir/makepkg.conf"

  case "${CARCH}" in
    powerpc) PACMAN_CONF="pacman.conf.ppc32" ;;
    *)       PACMAN_CONF="pacman.conf" ;;
  esac

  install -m644 "$srcdir/${PACMAN_CONF}" "$pkgdir/etc/pacman.conf"
  install -m644 "$srcdir/makepkg.conf" "$pkgdir/etc"
}
