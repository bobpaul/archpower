# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Adrian Perez de Castro <aperez@igalia.com>

pkgname=mold
pkgver=1.6.0
pkgrel=2
pkgdesc='A Modern Linker'
arch=(x86_64 powerpc64le riscv64)
url='https://github.com/rui314/mold'
license=('AGPL3')
# xxhash is bundled
depends=('gcc-libs' 'mimalloc' 'openssl' 'zlib' 'tbb')
makedepends=('git' 'python' 'cmake' 'mold')
checkdepends=('clang' 'libdwarf')
options=('debug')
_commit='323ad30e25c2c81efdb07ab76601a119335a40c8'
source=("$pkgname::git+$url.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

build() {
  mkdir -p ${srcdir}/.tmp
  export TMPDIR=${srcdir}/.tmp

  USE_MOLD=ON
  case "${CARCH}" in
    riscv64) USE_MOLD=OFF
  esac

  cmake \
  -S "$pkgname" \
  -B build \
  -DCMAKE_BUILD_TYPE='None' \
  -DCMAKE_INSTALL_PREFIX='/usr' \
  -DCMAKE_INSTALL_LIBEXECDIR='lib' \
  -DMOLD_USE_SYSTEM_MIMALLOC=ON \
  -DMOLD_USE_SYSTEM_TBB=ON \
  -DMOLD_LTO=ON \
  -DMOLD_USE_MOLD=${USE_MOLD}

  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
