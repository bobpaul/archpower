# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Adrian Perez de Castro <aperez@igalia.com>

pkgname=mold
pkgver=1.11.0
pkgrel=1
_commit='cca255e6be069cdbc135c83fd16036d86b98b85e'
pkgdesc='A Modern Linker'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://github.com/rui314/mold'
license=('AGPL3')
# xxhash is bundled
depends=('gcc-libs' 'mimalloc' 'openssl' 'zlib' 'tbb')
makedepends=('git' 'python' 'mold' 'cmake')
checkdepends=('clang' 'libdwarf')
options=(!distcc)
source=("$pkgname::git+$url.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

build() {
  mkdir -p ${srcdir}/.tmp
  export TMPDIR=${srcdir}/.tmp

  cmake \
  -S "$pkgname" \
  -B build \
  -D CMAKE_BUILD_TYPE='None' \
  -D CMAKE_INSTALL_PREFIX='/usr' \
  -D CMAKE_INSTALL_LIBEXECDIR='lib' \
  -D MOLD_USE_SYSTEM_MIMALLOC=ON \
  -D MOLD_USE_SYSTEM_TBB=ON \
  -D MOLD_LTO=ON

  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
