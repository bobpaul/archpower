# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Adrian Perez de Castro <aperez@igalia.com>

pkgname=mold
pkgver=2.35.1
pkgrel=1
pkgdesc='A Modern Linker'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://github.com/rui314/mold'
license=('MIT')
# xxhash is bundled
depends=('glibc' 'gcc-libs' 'mimalloc' 'zlib' 'tbb' 'zstd')
makedepends=('git' 'python' 'cmake' 'mold')
checkdepends=('clang' 'libdwarf')
options=(!debug !distcc)
source=("$pkgname::git+$url.git#tag=v$pkgver")
b2sums=('095f2384e97d4292c3436e7240583fc1cd1a091e3ed0cca1eff8063b5a2f5b337816f416a6f31268626ce749b460ad0149f02d97555f0c4439b4ca5ac1857de1')

build() {
  export LDFLAGS="${CFLAGS/-Wl,--no-warn-rwx-segments -Wl,--no-warn-execstack/}"

  case "${CARCH}" in
    powerpc64)
      _cmake_options=(
        -D MOLD_USE_MOLD=OFF
      )
    ;;
    *)
      _cmake_options=(
        -D MOLD_LTO=ON
        -D MOLD_USE_MOLD=ON
      )
    ;;
  esac

  cmake \
  -S "$pkgname" \
  -B build \
  -D CMAKE_BUILD_TYPE='None' \
  -D CMAKE_INSTALL_PREFIX='/usr' \
  -D CMAKE_INSTALL_LIBEXECDIR='lib' \
  -D MOLD_USE_SYSTEM_MIMALLOC=ON \
  -D MOLD_USE_SYSTEM_TBB=ON \
  -D MOLD_LTO=ON ${_cmake_options[@]}

  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
