# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Fabian Schoelzel <myfirstname.mylastname@googlemail.com>
# Contributor: funkyou <spamopfer@nickname.berlin.de>
# Contributor: tardo <tardo@nagi-fanboi.net>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=mlt
pkgver=7.12.0
pkgrel=2
pkgdesc='An open source multimedia framework'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://www.mltframework.org'
license=(GPL)
depends=(libebur128)
optdepends=('sdl_image: SDL1 plugin'
            'sdl2: SDL2 plugin'
            'libsamplerate: libavresample plugin'
            'sox: SOX (Audio Swiss Army Knife) plugin'
            'ffmpeg: ffmpeg plugin'
            'vid.stab: video stabilize plugin'
            'qt5-svg: Qt5 plugins'
            'jack: JACK sound output plugin'
            'ladspa: LADSPA plugins'
            'libexif: auto rotate plugin'
            'frei0r-plugins: for additional effects'
            'opencv: openCV plugin'
            'rubberband: audio pitch plugin'
            'gdk-pixbuf2: gdk plugin'
            'pango: gdk plugin'
            'rtaudio: rtaudio plugin'
            'python: python bindings')
#FIXME: movit on powerpc64
optdepends_powerpc=('movit: opengl plugin')
optdepends_powerpc64le=('movit: opengl plugin')
optdepends_riscv64=('movit: opengl plugin')
optdepends_x86_64=('movit: opengl plugin')
makedepends=(cmake ladspa frei0r-plugins libdv sdl_image libsamplerate sox ffmpeg vid.stab qt5-svg
             jack libexif python swig eigen opencv rubberband gdk-pixbuf2 pango rtaudio imagemagick)
makedepends_powerpc=(movit)
makedepends_powerpc64le=(movit)
makedepends_riscv64=(movit)
makedepends_x86_64=(movit)
source=(https://github.com/mltframework/mlt/releases/download/v$pkgver/mlt-$pkgver.tar.gz)
sha256sums=('48b385e83cbd5bf68bfc88631273868fbee36a41b3b7e2acd97f12b095b0083c')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DMOD_OPENCV=ON \
    -DMOD_GLAXNIMATE=ON \
    -DSWIG_PYTHON=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
