# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=spglib
pkgver=2.3.1
pkgrel=1
pkgdesc='C library for finding and handling crystal symmetries'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://spglib.readthedocs.io/en/latest/'
license=(BSD-3-Clause)
depends=(glibc)
makedepends=(cmake
             gcc-fortran
             gtest
             python
             python-numpy)
optdepends=('gcc-libs: Fortran interface'
            'python-numpy: Python interface')
source=(https://github.com/spglib/spglib/archive/v$pkgver/$pkgname-$pkgver.tar.gz
        fix-duplicate-libs.patch)
sha256sums=('c295dbea7d2fc9e50639aa14331fef277878c35f00ef0766e688bfbb7b17d44c'
            '3f649edcda3fb466f2c51ab10a6b255e171826e1c695fcb98de58a9c90bbc34e')

prepare() {
  patch -d $pkgname-$pkgver -p1 < fix-duplicate-libs.patch # Don't duplicate shared libs in python tree
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSPGLIB_WITH_Fortran=ON \
    -DSPGLIB_WITH_Python=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 "$srcdir"/$pkgname-$pkgver/COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
