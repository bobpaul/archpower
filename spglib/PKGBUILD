# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=spglib
pkgver=2.0.1
pkgrel=1
pkgdesc='C library for finding and handling crystal symmetries'
arch=(x86_64 powerpc64le powerpc riscv64)
url='https://atztogo.github.io/spglib/'
license=(custom)
depends=(gcc-libs)
makedepends=(cmake)
source=(https://github.com/spglib/spglib/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('d7407c0d67174a0c5e41a82ed62948c43fcaf1b5529f97238d7fadd1123ffe22')

build() {
  case "${CARCH}" in
    riscv64) _cmake_options=(-DUSE_OMP=OFF) ;;
  esac

  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr ${_cmake_options[@]}
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 "$srcdir"/$pkgname-$pkgver/COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
