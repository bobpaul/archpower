# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=spglib
pkgver=1.16.5
pkgrel=1
pkgdesc='C library for finding and handling crystal symmetries'
arch=(x86_64 powerpc64le powerpc riscv64)
url='https://atztogo.github.io/spglib/'
license=(custom)
depends=(gcc-libs)
makedepends=(cmake gcc-objc)
source=(https://github.com/atztogo/spglib/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('1bbde03b6b78da756c07f458bd90d84f3c253841b9b0632db5b72c5961e87aef')
options=(!distcc !ccache)

build() {
  case "${CARCH}" in
    riscv64) _cmake_options=(-DUSE_OMP=OFF) ;;
  esac

  cmake -B build -S $pkgname-$pkgver \
        -DCMAKE_INSTALL_PREFIX=/usr ${_cmake_options[@]}
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 "$srcdir"/$pkgname-$pkgver/COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
