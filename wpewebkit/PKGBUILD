# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=wpewebkit
pkgver=2.32.0
pkgrel=2
pkgdesc="Web content engine for embedded devices"
url="https://wpewebkit.org"
arch=(x86_64 powerpc64le powerpc)
license=(custom)
depends=(cairo fontconfig freetype2 glib2 harfbuzz harfbuzz-icu icu libjpeg
         libepoxy libgcrypt libsoup libxml2 zlib libpng sqlite libwebp libwpe
         atk at-spi2-atk openjpeg2 woff2 libtasn1 libxslt libsystemd bubblewrap
         libseccomp xdg-dbus-proxy gstreamer gst-plugins-base-libs libxkbcommon
         wayland wpebackend-fdo)
makedepends=(cmake ninja gtk-doc python ruby systemd gst-plugins-bad gperf
             wayland-protocols)
optdepends=('geoclue: Geolocation support'
            'gst-plugins-good: media decoding'
            'gst-plugins-bad: media decoding'
            'gst-libav: nonfree media decoding')
source=($url/releases/$pkgname-$pkgver.tar.xz{,.asc}
        unhide-deprecated-api.patch)
sha256sums=('6cfb18af9a180eeffffcaf34fea68d867ee59f633d811ced92bbead2d184b6ea'
            'SKIP'
            '48ee7bee0b50776c8153c20b96854bd5086e0a7d04e621fd57ea7fc6c690c3a2')
validpgpkeys=('D7FCF61CF9A2DEAB31D81BD3F3D322D0EC4582C3'  # Carlos Garcia Campos <cgarcia@igalia.com>
              '5AA3BC334FD7E3369E7C77B291C559DBE4C9123B') # Adrián Pérez de Castro <aperez@igalia.com>

prepare() {
  cd $pkgname-$pkgver

  # Use deprecated API for ubrk_safeClone until ICU 69 is officially supported
  patch -Np1 -i ../unhide-deprecated-api.patch
}

build() {
  case "${CARCH}" in
    powerpc)
      LD_FLAGS+=' -Wl,--no-keep-memory'
      CFLAGS="${CFLAGS/-O?/-Os}"
      CXXFLAGS="${CXXFLAGS/-O?/-Os}"
      EXTRA_CMAKE_FLAGS="-DENABLE_JIT=OFF
                         -DUSE_SYSTEMD=OFF
                         -DUSE_LD_GOLD=OFF
                         -DCMAKE_CXX_FLAGS_RELEASE=-DNDEBUG
                         -DCMAKE_CXX_FLAGS_DEBUG=-DNDEBUG
                         -DCMAKE_CXX_FLAGS_RELWITHDEBINFO=-DNDEBUG
                         -DCMAKE_C_FLAGS_RELEASE=-DNDEBUG
                         -DCMAKE_C_FLAGS_DEBUG=-DNDEBUG
                         -DCMAKE_C_FLAGS_RELWITHDEBINFO=-DNDEBUG
                         -DCMAKE_LINKER=${CHOST}-gcc"
    ;;
  esac

  cmake -S $pkgname-$pkgver -B build -G Ninja \
    -DPORT=WPE \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DCMAKE_SKIP_RPATH=ON \
    -DENABLE_GTKDOC=ON \
    -DENABLE_MINIBROWSER=ON ${EXTRA_CMAKE_FLAGS}
  cmake --build build
}

package() {
  depends+=(libwpe-1.0.so libWPEBackend-fdo-1.0.so)
  provides+=(libWPEWebKit-1.0.so)

  DESTDIR="$pkgdir" cmake --install build

  cd $pkgname-$pkgver
  find Source -name 'COPYING*' -or -name 'LICENSE*' -print0 | sort -z |
    while IFS= read -d $'\0' -r _f; do
      echo "### $_f ###"
      cat "$_f"
      echo
    done |
    install -Dm644 /dev/stdin "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set sw=2 et:
