# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Contributor: Alexander 'hatred' Drozdov <adrozdoff@gmail.com>
# Contributor: toha257 <toha257@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Tavian Barnes <tavianator@tavianator.com>
# AUR Maintainer: Stefan Schmidt <thrimbor.github@gmail.com>

_target_arch=x86_64
_target=${_target_arch}-pc-linux-gnu
pkgname=${_target}-linux-api-headers
pkgver=5.13.17
pkgrel=1
pkgdesc="Kernel headers sanitized for use in userspace (${_target})"
arch=(any)
url='http://www.gnu.org/software/libc'
license=(GPL2)
makedepends=(rsync make)
source=(https://www.kernel.org/pub/linux/kernel/v5.x/linux-${pkgver}.tar.xz)
sha256sums=('4ae70d23ed53bad6d53a3a159d235b8696595466f221986025f4a0ce111b0a1b')
validpgpkeys=('ABAF11C65A2970B130ABE3C479BE3E4300411886'   # Linus Torvalds
              '647F28654894E3BD457199BE38DBBDC86092693E')  # Greg Kroah-Hartman

build() {
  cd linux-${pkgver}

  make ARCH=x86 mrproper
  make ARCH=x86 headers_check
}

package() {
  cd linux-${pkgver}
  make INSTALL_HDR_PATH="$pkgdir/usr/$_target" ARCH=x86 headers_install

  # use headers from libdrm
  rm -rf "$pkgdir/usr/$_target/include/drm"

  # clean-up unnecessary files generated during install
  find ${pkgdir} \( -name .install -o -name ..install.cmd \) -delete
}
