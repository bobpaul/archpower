# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgbase=zlib
pkgname=(zlib minizip)
epoch=1
pkgver=1.2.12
pkgrel=1
arch=(x86_64 powerpc64le powerpc riscv64)
license=('custom')
url="https://www.zlib.net/"
depends=('glibc')
options=('staticlibs')  # needed by binutils testsuite
source=("https://zlib.net/zlib-${pkgver}.tar.gz"{,.asc})
sha256sums=('91844808532e5ce316b3c010929493c0244f3d37593afd6de04f71821d5136d9'
            'SKIP')
validpgpkeys=('5ED46A6721D365587791E2AA783FCD8E58BCAFBA')

prepare() {
	cd "${srcdir}/zlib-$pkgver"
	grep -A 24 '^  Copyright' zlib.h > LICENSE
}

build() {
	cd "${srcdir}/zlib-$pkgver"

	CFLAGS+=" -ffat-lto-objects"
	./configure --prefix=/usr
	make

	cd contrib/minizip
	cp Makefile Makefile.orig
	cp ../README.contrib readme.txt
	autoreconf --install
	./configure --prefix=/usr --enable-static=no
	make
}

check() {
	cd "${srcdir}/zlib-$pkgver"
	make test

	cd contrib/minizip
	make -f Makefile.orig test
}

package_zlib() {
	pkgdesc='Compression library implementing the deflate compression method found in gzip and PKZIP'

	cd "${srcdir}/zlib-$pkgver"
	make install DESTDIR="${pkgdir}"
	install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/zlib/LICENSE"
}

package_minizip() {
	pkgdesc='Mini zip and unzip based on zlib'
	depends=('zlib')

	cd "${srcdir}/zlib-$pkgver/contrib/minizip"
	make install DESTDIR="${pkgdir}"
	install -D -m644 "${srcdir}/zlib-$pkgver/LICENSE" "${pkgdir}/usr/share/licenses/minizip/LICENSE"

	# https://github.com/madler/zlib/pull/229
	rm "${pkgdir}/usr/include/minizip/crypt.h"
}
