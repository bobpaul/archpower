# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=tcsh
pkgver=6.24.00
pkgrel=1
pkgdesc='Enhanced version of the Berkeley C shell.'
arch=(x86_64 powerpc64le powerpc riscv64)
url='https://www.tcsh.org/'
license=('BSD')
depends=('ncurses' 'libxcrypt')
backup=('etc/csh.cshrc'
        'etc/csh.login')
install='tcsh.install'
source=("https://astron.com/pub/tcsh/tcsh-${pkgver}.tar.gz"
        'csh.cshrc'
        'csh.login')
sha256sums=('60be2c504bd8f1fa6e424b1956495d7e7ced52a2ac94db5fd27f4b6bfc8f74f0'
            '19093459a348fac6f7e28952cd7ebf0d7039267de7642b4f9b4b249998dd1901'
            '0eb360906c014118b38933dabc3487d28f4cf926b8fdf89576f7be33ee79d1ba')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc --bindir=/usr/bin
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" mandir=/usr/share/man install install.man

  install -Dm0644 "${srcdir}/csh.cshrc" "${pkgdir}/etc/csh.cshrc"
  install -Dm0644 "${srcdir}/csh.login" "${pkgdir}/etc/csh.login"
  install -Dm0644 Copyright "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  ln -s tcsh "${pkgdir}/usr/bin/csh"
  ln -s tcsh.1 "${pkgdir}/usr/share/man/man1/csh.1"
}

