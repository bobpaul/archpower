# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

pkgname=pixman
pkgver=0.43.0
pkgrel=1
pkgdesc="The pixel-manipulation library for X and cairo"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://cgit.freedesktop.org/pixman/"
license=('custom')
depends=('glibc')
makedepends=('meson' 'libpng')
provides=('libpixman-1.so')
source=(https://xorg.freedesktop.org/releases/individual/lib/${pkgname}-${pkgver}.tar.xz)
sha512sums=('a90399b8c6aec218abb2f419fb5d6894acf9f4c7acb4fd3893b0b7c805ba47c82ee7efb363be59bb1a15b6997b2dddb7dba062a165503b035e1124fff1b271c9')
#validpgpkeys=('') # Maarten Lankhorst <maarten.lankhorst@linux.intel.com>

build() {
  case "${CARCH}" in
    powerpc*) EXTRA_FLAGS='-D mmx=disabled -D sse2=disabled -D ssse3=disabled -D vmx=enabled' ;;
    riscv64)  EXTRA_FLAGS='-D mmx=disabled -D sse2=disabled -D ssse3=disabled' ;;
    x86_64)   EXTRA_FLAGS='-D vmx=disabled' ;;
  esac

  arch-meson $pkgbase-$pkgver build \
    -D loongson-mmi=disabled \
    -D vmx=disabled \
    -D arm-simd=disabled \
    -D a64-neon=disabled \
    -D neon=disabled \
    -D iwmmxt=disabled \
    -D mips-dspr2=disabled \
    -D gtk=disabled ${EXTRA_FLAGS}
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 $pkgname-$pkgver/COPYING
}
