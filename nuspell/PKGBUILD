# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Conributor: MÃ©lanie Chauvel (ariasuni) <perso at hack-libre dot org>

pkgname=nuspell
pkgver=5.1.2
pkgrel=2
pkgdesc='Fast and safe spellchecking C++ library'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://nuspell.github.io/'
license=('LGPL3')
depends=('icu')
makedepends=('cmake')
makedepends_x86_86=('pandoc')
checkdepends=('catch2')
source=(${pkgname}-${pkgver}.tar.gz::"https://github.com/nuspell/nuspell/archive/v${pkgver}.tar.gz")
sha512sums=('d6c1155ccf361a82207395507eb1beeee85954c18defa3dbe60932dc7463e9f5103cb35c1bb5698204286098ff86a06791fc948f1a1979150b90d8db619c988d')

# 1st rebuild enchant
# 2nd rebuild epiphany against nuspell/enchant that checkdepens on both

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${pkgbase}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DBUILD_SHARED_LIBS=ON
  make
}

check() {
  cd build
  make test
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
