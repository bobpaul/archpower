# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>

# toolchain build order: linux-api-headers->glibc->binutils->gcc->binutils->glibc

pkgname=linux-api-headers
pkgver=5.13.17
pkgrel=1
pkgdesc='Kernel headers sanitized for use in userspace'
arch=(any)
url='https://www.gnu.org/software/libc'
license=(GPL2)
makedepends=(rsync)
source=(https://www.kernel.org/pub/linux/kernel/v${pkgver:0:1}.x/linux-${pkgver}.tar.{xz,sign})
sha256sums=('4ae70d23ed53bad6d53a3a159d235b8696595466f221986025f4a0ce111b0a1b'
            'SKIP')
validpgpkeys=('ABAF11C65A2970B130ABE3C479BE3E4300411886'   # Linus Torvalds
              '647F28654894E3BD457199BE38DBBDC86092693E')  # Greg Kroah-Hartman

build() {
  cd linux-$pkgver

  ARCH=powerpc make mrproper
  ARCH=powerpc make headers_check
}

package() {
  cd linux-$pkgver
  ARCH=powerpc make INSTALL_HDR_PATH="$pkgdir/usr" headers_install

  # seems nothing properly fixes permissions
  find "$pkgdir/usr/include" -type d -exec chmod 0755 {} \;
  find "$pkgdir/usr/include" -type f -exec chmod 0644 {} \;

  # use headers from libdrm
  rm -r "$pkgdir/usr/include/drm"
}
