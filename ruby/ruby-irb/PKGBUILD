# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Anatol Pomozov

_gemname=irb
pkgname=ruby-$_gemname
pkgver=1.3.4
pkgrel=4
pkgdesc="Interactive Ruby command-line tool for REPL (Read Eval Print Loop)."
arch=('any')
url='https://github.com/ruby/irb'
license=('BSD-2')
depends=('ruby' 'ruby-reline')
makedepends=('ruby-rdoc')
options=('!emptydirs')
source=("https://rubygems.org/downloads/$_gemname-$pkgver.gem")
noextract=("$_gemname-$pkgver.gem")
sha512sums=('bdc0932bed93c3a20e91084ac101d383e07973c0beb6bb2a2dc639198d02be3b9d36a27ecec3646d5c4eacbfec2caf4f34eed10091b353b0842605976f8e077a')

package() {
  cd "$srcdir"

  local _gemdir="$(ruby -e'puts Gem.default_dir')"
  HOME="/tmp" GEM_HOME="$_gemdir" GEM_PATH="$_gemdir" gem install -N --no-user-install --ignore-dependencies \
     -i "$pkgdir/$_gemdir" -n "$pkgdir/usr/bin" "$_gemname-$pkgver.gem"
  rm "$pkgdir/$_gemdir/cache/$_gemname-$pkgver.gem"
  install -D -m644 "$pkgdir/$_gemdir/gems/$_gemname-$pkgver/LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"

  install -D -m644 "$pkgdir/$_gemdir/gems/$_gemname-$pkgver/man/irb.1" "$pkgdir/usr/share/man/man1/irb.1"
}
