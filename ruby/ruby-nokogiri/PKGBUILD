# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

_gemname='nokogiri'
pkgname="ruby-${_gemname}"
pkgver=1.16.6
pkgrel=2
pkgdesc='Nokogiri (é‹¸) is an HTML, XML, SAX, and Reader parser'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://nokogiri.org'
license=('MIT')
depends=(
  libxslt
  ruby
  ruby-mini_portile2
  ruby-racc
)
makedepends=(
  ruby-rdoc
)
checkdepends=(
  ruby-bundler
  ruby-minitest-reporters
  ruby-rake-compiler
  ruby-rake-compiler-dock
  ruby-ruby_memcheck
  ruby-simplecov
)
options=('!emptydirs')
source=("https://github.com/sparklemotion/${_gemname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('c90e0dcd9c90980344551a5b660e15cbdf1f42a139c123f936c728952f0d8d07f8ca4810cf6f8dd51070bff66398dca3cb29ed60f8e2242930233dd594efe843')
b2sums=('5a2becbbaf52d753569d778fcf2c84b521a30aa52abeb923970dbc8abea56277b807e70538032edfb883552c9385b7c225885cdbab111462f51a741a0b8d3bd5')

prepare() {
  cd "${_gemname}-${pkgver}"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' "${_gemname}.gemspec"

  # we don't do jruby
  sed --in-place '/\.java/d' "${_gemname}.gemspec"
  sed --in-place '/\.jar/d' "${_gemname}.gemspec"

  # we don't have rubocop in the repos
  rm --verbose rakelib/rubocop.rake
}

build() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  export CFLAGS+=' -ffat-lto-objects'

  gem build "${_gemname}.gemspec" -- --use-system-libraries

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${_gemname}-${pkgver}.gem" \
    -- \
    --use-system-libraries

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "tmp_install/${_gemdir}/cache/" \
    "tmp_install/${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install/${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install/${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install/${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  GEM_HOME="tmp_install/${_gemdir}" rake test
}

package() {
  cd "${_gemname}-${pkgver}"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 LICENSE* --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}
