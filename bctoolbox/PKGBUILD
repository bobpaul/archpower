# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Sergej Pupykin <arch+pub@sergej.pp.ru>

pkgname=bctoolbox
pkgver=4.4.34
pkgrel=1
pkgdesc="A library written in C that allows you to create and run audio and video streams"
arch=(x86_64 powerpc64le powerpc)
url="https://github.com/BelledonneCommunications/bctoolbox"
license=(GPL)
depends=(mbedtls bcunit)
makedepends=(cmake)
source=($pkgname-$pkgver.tar.gz::"https://github.com/BelledonneCommunications/bctoolbox/archive/$pkgver.tar.gz")
sha256sums=('7106b2c551371e58a07e39cf5fcd88fecaf2e6ce08bef28232d1ffa47b745114')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_STATIC=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
