# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Jakub Schmidtke <sjakub@gmail.com>

pkgname=firefox
pkgver=111.0.1
pkgrel=1
pkgdesc="Standalone web browser from mozilla.org"
arch=(x86_64 powerpc64le powerpc64 riscv64)
license=(MPL GPL LGPL)
url="https://www.mozilla.org/firefox/"
depends=(gtk3 libxt mime-types dbus-glib ffmpeg nss ttf-font libpulse jemalloc)
makedepends=(unzip zip diffutils mesa imake inetutils xorg-server-xvfb
             autoconf2.13 rust clang llvm jack gtk2 nodejs cbindgen
             python-setuptools python-psutil
             wasi-compiler-rt wasi-libc wasi-libc++ wasi-libc++abi gn)
makedepends_powerpc64=(lld)
makedepends_powerpc64le=(lld)
makedepends_x86_64=(lld)
optdepends=('networkmanager: Location detection via available WiFi networks'
            'libnotify: Notification integration'
            'pulseaudio: Audio support'
            'speech-dispatcher: Text-to-Speech'
            'hunspell-en_US: Spell checking, American English')
options=(!ccache !emptydirs !makeflags strip)
source=(https://archive.mozilla.org/pub/firefox/releases/$pkgver/source/firefox-$pkgver.source.tar.xz{,.asc}
        $pkgname.desktop
        identity-icons-brand.svg
        0001-Don-t-use-build-id.patch 
        0002-Fortify-sources-properly.patch 
        0003-bmo-847568-Support-system-harfbuzz.patch 
        0004-bmo-847568-Support-system-graphite2.patch 
        0005-bmo-1559213-Support-system-av1.patch 
        0006-bmo-1516803-Fix-building-sandbox.patch 
        0015-Make-PGO-use-toolchain.patch 
        0016-bmo-1516081-Disable-watchdog-during-PGO-builds.patch 
        0017-bmo-1516803-force-one-LTO-partition-for-sandbox-when.patch 
        0019-build-Disable-Werror.patch 
        0020-LTO-Only-enable-LTO-for-Rust-when-complete-build-use.patch 
        0021-Enable-FLAC-on-platforms-without-ffvpx-via-ffmpeg.patch 
        0022-bmo-1670333-OpenH264-Fix-decoding-if-it-starts-on-no.patch 
        0023-bmo-1663844-OpenH264-Allow-using-OpenH264-GMP-decode.patch 
        0025-bmo-1559213-fix-system-av1-libs.patch 
        0026-bmo-1196777-Set-GDK_FOCUS_CHANGE_MASK.patch 
        0027-bmo-1754469-memory_mozalloc_throw.patch 
        0028-rhbz-2115253-vaapi-fixes.patch 
        0029-bgo-860033-firefox-wayland-no-dbus.patch 
        0030-qm-qm-fix-sqlite3-on-ppc-with-clang.patch 
        0031-fix-building-gcc-pgo.patch 
        0032-bmo-1775202-ppc64-webrtc-missing-conditions-109.patch 
        big-endian-image-decoders.patch 
        ppc.patch 
        mozilla-bmo1005535.patch
        skia-sucks1.patch                                                                                  
        skia-sucks2.patch                                                                                  
        skia-sucks3.patch)
sha256sums=('84a4f3aba62df6e0451cdd28f8f1e59840d77c4062311947b0e59325c2ebdce8'
            'SKIP'
            'a9e5264257041c0b968425b5c97436ba48e8d294e1a0f02c59c35461ea245c33'
            'a9b8b4a0a1f4a7b4af77d5fc70c2686d624038909263c795ecc81e0aec7711e9'
            '10dd9b88e4a80c43e74580e6d097848bef0da94d66f5faf8250256d6d2a1bfae'
            '92d88a6cfd0bbf9a07f1171e6c3f65b8fb6863e286639867009f3f8712682a31'
            '79ba7b03993adbb724cd5ad3d6e4c60c9c7db52e87932a2bcbfbdf8aeba109a5'
            '27961704acd49fde267aaccd3067361658fb236d917e48bdc05a0d5380153292'
            '7c6db1dd501c99cd5d4ba59e66dd1f5d3065008b2c343e733201982de0a89ac6'
            '8e74496c183f0b8f13a55d50c502d583315872d43893c8649c38f7b3eea526eb'
            '7b45e98356e6afbb67e0ff005cedd5b2022093bf1716ef0936ab3f6615f787da'
            '1d713370fe5a8788aa1723ca291ae2f96635b92bc3cb80aea85d21847c59ed6d'
            '7f070620a393d283b488c263bf8a871c1a0afe5801d963316e14474c27dd3d6d'
            'e773d271fd7c74a725fb2709d6e4132dcfcf4df5a90c8c27d89a0d58330f7968'
            '18fc8bab428137702d6774a93d3cc8d8f6b8ea68e1d86b32591e2c242e80e767'
            '40cd47e73ac3e87f0f072f164ccd81d857c0cdb57c60e2077233952219d9b6e8'
            'e828b8b48c55601de0fc3c244ff83ee5a0d48267de7c14eeeabc26487d8e747d'
            '26f950b6681eb67234649668d9d67ad4b6c5ca5025457f440778ff31066cfdf2'
            '31ba77f95e5fe8414aeeef6858c8a774fc83f8cbbf032c0b82910de55f4304f4'
            'b47ea62937d7f87485e0f372c0110785bbb6f4dbc79d3d139d54887cac0773e3'
            'ba1013f0b5a76312802ac62cc5dbc35ecdcd3d4946df78c4b3fc144afc829023'
            'f2b19e14d8add13930e2ce89fa5e1b252ac979c8177a78a6fa3eb4ae2ad56633'
            'efdf58e256b298bf8f0c0b7ead119830e99d1b2bf29ba1e43a9e0eeb8ac2e538'
            '5f0ca8811b4f4967b311270ccf0f5e1aca5917bec37eb54f2b241d0da1641a30'
            '2e197ac0f56bbeba1c346e138fdadb8547a0191173336d39d59a800a2a797731'
            '790353302d38ec8af7e85cfde5f4b921086c29fa818e07be996baa87dffccf90'
            '0df637d44218524e213814d15d623f27724d8e405390173f6cc385e145110bfe'
            'cea57582d6cd7c34e99226bccafc87dbf7368186a5d76eb0836d4ddbce3bc81f'
            '5a49be04bc93123e234085cdc6bd4f1a2184decfcde12f6fe7b3c32b7bf648d7'
            'c36b73c23975ef72b6f562c4e9199e4958ea5083473926562b96ea255c08b148'
            'a4ad198fc98acb0a16a013f1ae0a09ab1b96d9b49eadc52045138de5ae2c5ee2'
            '5a0fea05cf1828f102b0b73878a27c0d05b0cf21c81e1a2f463242a0d3121251')
validpgpkeys=('14F26682D0916CDD81E37B6D61B7B526D98F0353') # Mozilla Software Releases <release@mozilla.com>

# Google API keys (see http://www.chromium.org/developers/how-tos/api-keys)
# Note: These are for Arch Linux POWER use ONLY. For your own distribution, please
# get your own set of keys. Feel free to contact alex.bldck@gmail.com for
# more information.
_google_api_key=AIzaSyDgkw4O3LM0Jnr2N7Wq2NG7iUVzRU5sBaA

# Mozilla API keys (see https://location.services.mozilla.com/api)
# Note: These are for Arch Linux POWER use ONLY. For your own distribution, please
# get your own set of keys. Feel free to contact alex.bldck@gmail.com for
# more information.
_mozilla_api_key=de0473f2-d53f-46da-956c-6aff61bda3ab

prepare() {
  mkdir -p mozbuild
  cd ${srcdir}/firefox-$pkgver

  patch -Np1 -i ${srcdir}/0001-Don-t-use-build-id.patch 
  patch -Np1 -i ${srcdir}/0002-Fortify-sources-properly.patch 
  patch -Np1 -i ${srcdir}/0003-bmo-847568-Support-system-harfbuzz.patch 
  patch -Np1 -i ${srcdir}/0004-bmo-847568-Support-system-graphite2.patch 
  patch -Np1 -i ${srcdir}/0005-bmo-1559213-Support-system-av1.patch 
  patch -Np1 -i ${srcdir}/0006-bmo-1516803-Fix-building-sandbox.patch 
  patch -Np1 -i ${srcdir}/0015-Make-PGO-use-toolchain.patch 
  patch -Np1 -i ${srcdir}/0016-bmo-1516081-Disable-watchdog-during-PGO-builds.patch 
  patch -Np1 -i ${srcdir}/0017-bmo-1516803-force-one-LTO-partition-for-sandbox-when.patch 
  patch -Np1 -i ${srcdir}/0019-build-Disable-Werror.patch 
  patch -Np1 -i ${srcdir}/0020-LTO-Only-enable-LTO-for-Rust-when-complete-build-use.patch 
  patch -Np1 -i ${srcdir}/0021-Enable-FLAC-on-platforms-without-ffvpx-via-ffmpeg.patch 
  patch -Np1 -i ${srcdir}/0022-bmo-1670333-OpenH264-Fix-decoding-if-it-starts-on-no.patch 
  patch -Np1 -i ${srcdir}/0023-bmo-1663844-OpenH264-Allow-using-OpenH264-GMP-decode.patch 
  patch -Np1 -i ${srcdir}/0025-bmo-1559213-fix-system-av1-libs.patch 
  patch -Np1 -i ${srcdir}/0026-bmo-1196777-Set-GDK_FOCUS_CHANGE_MASK.patch 
  patch -Np1 -i ${srcdir}/0027-bmo-1754469-memory_mozalloc_throw.patch 
  patch -Np1 -i ${srcdir}/0028-rhbz-2115253-vaapi-fixes.patch 
  patch -Np1 -i ${srcdir}/0029-bgo-860033-firefox-wayland-no-dbus.patch 
  patch -Np1 -i ${srcdir}/0030-qm-qm-fix-sqlite3-on-ppc-with-clang.patch 
  patch -Np1 -i ${srcdir}/0031-fix-building-gcc-pgo.patch 
  patch -Np1 -i ${srcdir}/0032-bmo-1775202-ppc64-webrtc-missing-conditions-109.patch 
  patch -Np0 -i ${srcdir}/big-endian-image-decoders.patch 
  patch -Np1 -i ${srcdir}/ppc.patch 
  patch -Np1 -i ${srcdir}/mozilla-bmo1005535.patch
  patch -Np1 -i ${srcdir}/skia-sucks1.patch
  patch -Np1 -i ${srcdir}/skia-sucks2.patch
  patch -Np1 -i ${srcdir}/skia-sucks3.patch

  sed -i 's/\("files":{\)[^}]*/\1/' third_party/rust/num-traits/.cargo-checksum.json

  echo -n "$_google_api_key" >$srcdir/google-api-key
  echo -n "$_mozilla_api_key" >$srcdir/mozilla-api-key

  cat >${srcdir}/mozconfig <<END
ac_add_options --enable-application=browser

ac_add_options --prefix=/usr
ac_add_options --enable-optimize
ac_add_options --enable-hardening
ac_add_options --enable-jemalloc
ac_add_options --enable-linker=bfd
export LDFLAGS="${LDFLAGS} -Wl,-rpath=/usr/lib/firefox"
mk_add_options MOZ_MAKE_FLAGS="${MAKEFLAGS}"

# Branding
ac_add_options --enable-official-branding
ac_add_options --enable-update-channel=release
ac_add_options --with-distribution-id=org.archlinuxpower
ac_add_options --with-unsigned-addon-scopes=app,system
export MOZ_APP_REMOTINGNAME=${pkgname//-/}
export MOZ_TELEMETRY_REPORTING=1
export MOZ_REQUIRE_SIGNING=1

# Keys
ac_add_options --with-google-location-service-api-keyfile=${srcdir}/google-api-key
ac_add_options --with-google-safebrowsing-api-keyfile=${srcdir}/google-api-key
ac_add_options --with-mozilla-api-keyfile=${srcdir}/mozilla-api-key

ac_add_options --with-system-nspr
ac_add_options --with-system-nss
ac_add_options --enable-system-pixman

# Features
ac_add_options --enable-alsa
ac_add_options --enable-jack
ac_add_options --disable-crashreporter
ac_add_options --disable-updater
ac_add_options --disable-tests
ac_add_options --disable-webrtc
END

  case "${CARCH}" in
    powerpc)
      echo "export LDFLAGS='${LDFLAGS} -Wl,-z,stack-size=1048576 -latomic -Wl,--no-keep-memory'" >> ${srcdir}/mozconfig 
      echo "export CXXFLAGS='${CXXFLAGS} -latomic -Wno-class-memaccess'" >> ${srcdir}/mozconfig
      # patch the rust debug level, this is hardcoded
      sed -i "s/debug_info = '2'/debug_info = '1'/" \
        build/moz.configure/toolchain.configure
    ;;
    riscv64)
      echo "export LDFLAGS='${LDFLAGS} -Wl,-z,stack-size=1048576 -Wl,--no-keep-memory'" >> ${srcdir}/mozconfig 
    ;;
    *)
      echo "ac_add_options --enable-rust-simd" >> ${srcdir}/mozconfig
      echo "export RUSTC_OPT_LEVEL=2" >> ${srcdir}/mozconfig
      echo "ac_add_options --enable-lto=full" >> ${srcdir}/mozconfig
      echo "ac_add_options MOZ_PGO=1" >> ${srcdir}/mozconfig
    ;;
  esac

  case "${CARCH}" in
    riscv64)
      echo "ac_add_options --disable-release" >> ${srcdir}/mozconfig
      echo "ac_add_options --disable-debug" >> ${srcdir}/mozconfig
      echo "ac_add_options --disable-jit" >> ${srcdir}/mozconfig
      echo "ac_add_options --without-wasm-sandboxed-libraries" >> ${srcdir}/mozconfig
      rm -fv ${srcdir}/firefox-${pkgver}/.cargo/config
    ;;
    x86_64)
      echo "ac_add_options --with-wasi-sysroot=/usr/share/wasi-sysroot" >> ${srcdir}/mozconfig
      echo "ac_add_options --enable-release" >> ${srcdir}/mozconfig
      echo "ac_add_options --without-wasm-sandboxed-libraries" >> ${srcdir}/mozconfig
      echo "export MOZILLA_OFFICIAL=1" >> ${srcdir}/mozconfig
      echo "export MOZ_TELEMETRY_REPORTING=1" >> ${srcdir}/mozconfig
      echo "export MOZ_REQUIRE_SIGNING=1" >> ${srcdir}/mozconfig
    ;;
    *)
      echo "ac_add_options --enable-release" >> ${srcdir}/mozconfig
      echo "ac_add_options --without-wasm-sandboxed-libraries" >> ${srcdir}/mozconfig
      echo "export MOZILLA_OFFICIAL=1" >> ${srcdir}/mozconfig
      echo "export MOZ_TELEMETRY_REPORTING=1" >> ${srcdir}/mozconfig
      echo "export MOZ_REQUIRE_SIGNING=1" >> ${srcdir}/mozconfig
    ;;
  esac

  mkdir ${srcdir}/.tmp
}

build() {
  cd firefox-$pkgver

  case "${CARCH}" in
    powerpc64*|riscv64)
      export CC=${CHOST}-gcc
      export CXX=${CHOST}-g++
      ;;
    *)
      export CC=clang
      export CXX=clang++
      ;;
  esac

  export TMPDIR=${srcdir}/.tmp

  export MOZCONFIG="$srcdir/mozconfig"
  export MOZ_NOSPAM=1
  export MOZBUILD_STATE_PATH="$srcdir/mozbuild"
  export MOZ_APP_REMOTINGNAME=${pkgname//-/}
  #export MACH_BUILD_PYTHON_NATIVE_PACKAGE_SOURCE=system

  # LTO needs more open files
  ulimit -n 4096

  rm -f old-configure
  xvfb-run -s "-screen 0 1920x1080x24 -nolisten local" \
  ./mach build

  #echo "Building symbol archive..."
  #xvfb-run -s "-screen 0 1920x1080x24 -nolisten local" \
  #./mach buildsymbols
}

package() {
  cd ${srcdir}/firefox-${pkgver}
  DESTDIR="$pkgdir" ./mach install || true

  local vendorjs="$pkgdir/usr/lib/$pkgname/browser/defaults/preferences/vendor.js"
  install -Dvm644 /dev/stdin "$vendorjs" <<END
// Use LANG environment variable to choose locale
pref("intl.locale.requested", "");

// Use system-provided dictionaries
pref("spellchecker.dictionary_path", "/usr/share/hunspell");

// Disable default browser checking.
pref("browser.shell.checkDefaultBrowser", false);

// Don't disable extensions in the application directory
pref("extensions.autoDisableScopes", 11);
END

  local distini="$pkgdir/usr/lib/$pkgname/distribution/distribution.ini"
  install -Dvm644 /dev/stdin "$distini" <<END
[Global]
id=archlinuxpower
version=1.0
about=Mozilla Firefox for Arch POWER 

[Preferences]
app.distributor=archlinuxpower
app.distributor.channel=$pkgname
app.partner.archlinuxpower=archlinuxpower
END

  install -Dvm644 ../$pkgname.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  local i theme=official
  for i in 16 22 24 32 48 64 128 256; do
    install -Dvm644 browser/branding/$theme/default$i.png \
      "$pkgdir/usr/share/icons/hicolor/${i}x${i}/apps/$pkgname.png"
  done
  install -Dvm644 browser/branding/$theme/content/about-logo.png \
    "$pkgdir/usr/share/icons/hicolor/192x192/apps/$pkgname.png"
  install -Dvm644 browser/branding/$theme/content/about-logo@2x.png \
    "$pkgdir/usr/share/icons/hicolor/384x384/apps/$pkgname.png"
  install -Dvm644 browser/branding/$theme/content/about-logo.svg \
    "$pkgdir/usr/share/icons/hicolor/scalable/apps/$pkgname.svg"
  install -Dvm644 ${srcdir}/identity-icons-brand.svg \
    "$pkgdir/usr/share/icons/hicolor/symbolic/apps/$pkgname-symbolic.svg"

  install -Dvm644 ${srcdir}/$pkgname.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  # Install a wrapper to avoid confusion about binary path
  install -Dvm755 /dev/stdin "$pkgdir/usr/bin/$pkgname" <<END
#!/bin/sh
exec /usr/lib/$pkgname/firefox "\$@"
END

  # Replace duplicate binary with wrapper
  # https://bugzilla.mozilla.org/show_bug.cgi?id=658850
  ln -srfv "$pkgdir/usr/bin/$pkgname" "$pkgdir/usr/lib/$pkgname/firefox-bin"

  # Use system certificates
  local nssckbi="$pkgdir/usr/lib/$pkgname/libnssckbi.so"
  if [[ -e $nssckbi ]]; then
    ln -srfv "$pkgdir/usr/lib/libnssckbi.so" "$nssckbi"
  fi

  #if [[ -f "$startdir/.crash-stats-api.token" ]]; then
  #  find . -name '*crashreporter-symbols-full.zip' -exec \
  #    "$startdir/upload-symbol-archive" "$startdir/.crash-stats-api.token" {} +
  #else
  #  find . -name '*crashreporter-symbols-full.zip' -exec \
  #    cp -fvt "$startdir" {} +
  #fi
}

# vim:set sw=2 sts=-1 et:
