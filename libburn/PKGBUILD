# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=libburn
pkgver=1.5.6
pkgrel=1.1
pkgdesc="Library for reading, mastering and writing optical discs"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://dev.lovelyhq.com/libburnia"
license=('GPL')
depends=('glibc')
# https://dev.lovelyhq.com/libburnia/web/wikis/Releases
source=(https://files.libburnia-project.org/releases/${pkgname}-${pkgver}.tar.gz{,.sig})
options=('!emptydirs')
sha256sums=('7295491b4be5eeac5e7a3fb2067e236e2955ffdc6bbd45f546466edee321644b'
            'SKIP')
validpgpkeys=('44BC9FD0D688EB007C4DD029E9CBDFC0ABC0A854') # Thomas Schmitt <scdbackup@gmx.net>

build() {
  cd "${pkgname}"-${pkgver/.pl01/}
  ./configure --prefix=/usr --disable-static --build=${CHOST}
  make
}

package() {
  cd "${pkgname}"-${pkgver/.pl01/}
  make DESTDIR="${pkgdir}" install
}
