# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: AndyRTR <andyrtr@archlinux.org>

# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: royrocks <royrocks13@gmail.com>

pkgname=libpaper
pkgver=2.0.4
pkgrel=1
pkgdesc='Library for handling paper characteristics'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://github.com/rrthomas/libpaper'
license=('GPL')
depends=('sh' 'run-parts')
provides=('paper')
replaces=('paper')
backup=('etc/papersize')
source=("https://github.com/rrthomas/libpaper/releases/download/v${pkgver}/libpaper-${pkgver}.tar.gz")
sha256sums=('eec7b774f610dcba5fe84bea35868d2242058af216d97ca00367ac10e34eeada')

prepare() {
  cd "$pkgname-$pkgver"
  autoreconf -vfi
}

build() {
  cd "$pkgname-$pkgver"
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  
  # add systemwide default papersize read by many office applications
  install -dm 755 "$pkgdir"/etc
  echo '# Simply write the paper name. See papersize(5) for possible values' > "$pkgdir"/etc/papersize

  # add libpaper.d directory other packages can use to store files
  install -dm 755 "$pkgdir"/etc/libpaper.d
}
