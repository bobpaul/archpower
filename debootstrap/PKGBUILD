# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=debootstrap
pkgver=1.0.133
pkgrel=1.1
pkgdesc="Bootstrap a basic Debian system"
arch=(any)
license=('GPL')
url="https://tracker.debian.org/pkg/debootstrap"
depends=('perl' 'wget' 'binutils')
source=(https://salsa.debian.org/installer-team/debootstrap/-/archive/$pkgver/debootstrap-$pkgver.tar.bz2
        arch-detect.patch)
optdepends=('gnupg: check release signatures'
            'debian-archive-keyring: check release signatures for Debian'
            'debian-ports-archive-keyring: check release signatures for Debian Ports'
            'ubuntu-keyring: check release signatures for Ubuntu')
sha512sums=('ba9bd6960f134e5b77a4357a3788637453cfdbab9dca521a43807fad59870cd49d5343f41899d94d926f9416a85109cac471e557371a99197faf86c01d7f8b7d'
            '247132de66fed4219799af07dfbdc0d1371ca53f429207cd7e0cd24c205e050f73864381a18c578b47762395a4d7bf5adc7970437fef24764417cbb0afd31a04')

prepare() {
  cd debootstrap-$pkgver

  sed -i 's/sbin/bin/g' Makefile

  # Debian defaults
  sed -i 's|export PATH|export PATH="/usr/local/sbin:/usr/local/bin:/usr/sbin:/usr/bin:/sbin:/bin"|' debootstrap

  # Detect Architecture
  patch -p1 -i ../arch-detect.patch
}

package() {
  cd debootstrap-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dm644 debootstrap.8 "$pkgdir"/usr/share/man/man8/debootstrap.8
}
