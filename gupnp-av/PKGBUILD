# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Thijs Vermeir <thijsvermeir@gmail.com>
# Contributor: Denis Zawada <deno@rootnode.net>

pkgname=gupnp-av
pkgver=0.14.1
pkgrel=2.1
pkgdesc="Library to ease handling and implementation of UPnP A/V profiles"
url="https://wiki.gnome.org/Projects/GUPnP"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(LGPL)
depends=(glib2 libxml2)
makedepends=(glib2-devel gobject-introspection gtk-doc vala meson git)
options=(debug)
_commit=86f148ebb920a8113190fd3e6cae636c9fdd3ca7  # tags/gupnp-av-0.14.1^0
source=("git+https://gitlab.gnome.org/GNOME/gupnp-av.git#commit=$_commit"
        build-fix.patch)
sha256sums=('e920c5ce2c634b665c7cb0406a1599758ce82d084b18ff156625aca8f65dc28a'
            'fdea56304a37c2ba9c34216a84bacd5302b2a94c7fb762c333cf2b0ef174ee23')

pkgver() {
  cd gupnp-av
  git describe --tags | sed 's/^gupnp-av-//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd gupnp-av
  patch -Np1 -i ${srcdir}/build-fix.patch
}

build() {
  arch-meson gupnp-av build -D gtk_doc=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  depends+=(libg{lib,object}-2.0.so)
  provides+=(libgupnp-av-1.0.so)

  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
