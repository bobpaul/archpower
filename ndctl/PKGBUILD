# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ndctl
pkgver=71.1
pkgrel=1
pkgdesc='Utility library for managing the libnvdimm (non-volatile memory device) sub-system in the Linux kernel'
arch=( x86_64 powerpc64le powerpc )
url="https://github.com/pmem/ndctl"
license=('LGPL')
depends=('json-c' 'keyutils' 'kmod' 'systemd-libs' 'util-linux-libs')
makedepends=('asciidoctor' 'bash-completion' 'systemd' 'xmlto')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pmem/ndctl/archive/v$pkgver.tar.gz"
	0001-daxctl-link-against-libndctl-in-case-its-use-doesn-t.patch)
sha512sums=('92fb06ca64455775ce0b89ba0846f70235e02fe3502e2a8a55a858f76f87cb90ae98f5bf01620e5884f667ea78ee2bee2cd9e7a85ffa8f8d9c456e9282ca9ebc'
            'd61383a5255023fc0441e54c682b5f127900bc9402106e3e4d3b0357b63b8607b8345cbf0d7aecc36ade08b491984189bbc53bd9d23bff3ff1eed0482a8141d4')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 -i ${srcdir}/0001-daxctl-link-against-libndctl-in-case-its-use-doesn-t.patch

  # fix location of modprobe.d conf
  sed -i 's|^modprobedir =.*|modprobedir = /usr/lib/modprobe.d/|' Makefile.am
}

build() {
  cd "$srcdir"/$pkgname-$pkgver
  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
