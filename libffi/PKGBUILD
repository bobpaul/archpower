# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Stéphane Gaudreault <stephane@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=libffi
pkgver=3.4.2
pkgrel=3
pkgdesc='Portable foreign function interface library'
arch=(x86_64 powerpc64le powerpc)
url='https://sourceware.org/libffi/'
license=('MIT')
depends=('glibc')
checkdepends=('dejagnu')
provides=('libffi.so')
source=(https://github.com/libffi/libffi/releases/download/v$pkgver/libffi-$pkgver.tar.gz
        https://github.com/libffi/libffi/releases/download/v3.3/libffi-3.3.tar.gz
        powerpc-fix.patch
        powerpc-fix-2.patch
        powerpc64-fix.patch
        powerpc64-fix-2.patch)
sha256sums=('540fb721619a6aba3bdeef7d940d8e9e0e6d2c193595bc243241b77ff9e93620'
            '72fba7922703ddfa7a028d513ac15a85c8d54c8d67f55fa5a4802885dc652056'
            '031647283323f81e2e3690e5237c433f89f30e031905eb4a6db11dfa83c8ad83'
            '41031984367ddf06f5f1a3701f9263b25d4fe1baa02ca727bc1747afbb5c5798'
            '359d8239c4864dc8fe5e429974052d58be5fe046e5fc273b5f1d89bb4e7880d6'
            '01cdfd9c03cf2c1c8dbaa26c97d032c6ba28006927eaf9a64e05217dd9b03af9')
b2sums=('a8137bc895b819f949fd7705e405be627219c6d1fdef280253330f7407d4a548bb057d7bb0e9225d1767d42f9bf5f0ab3c455db1c3470d7cc876bb7b7d55d308'
        'cddc40729a30a9bd34d675809f51f8d1b4ccaffa54bc6dd6f7e965f4e260edd34754719f9f6247c8957aeb7cf154d56ce1fe16a54c3f1ad39afbebdf41d23caa'
        '410418750dd12d747776eb158e86a738ad9f9495bd219a8586f02141297e1422dee51adf82d3d197a76fbbdca11da68cb5cad9bf3a2256596422b8cef8ef0d49'
        '08df602aec8bc42b326bd13e03a7518c072e15c32c96c066329221a9fbfb5c3fc696d0bbbd8bcb69653bf4784808d10614e1d31b52908bd4c1dbdec62324786d'
        '280e600407d03f056a9a29f9e41ef0a5735a512f78881e1e54d60e9ca7b490d781f935e65cfca349509dc5fc69566e8ce69ef7d9df345631c35f91ab98ada520'
        'a6b3c226e5b9fb3fa3350ef2d73c70e6ee8ca9bded2ec0911027a2c49b49e488f668219494adb5deefee67a4d61637f9c505924d8144650c1dd66655a96b3bf6')

prepare() {
  cd $pkgname-3.3
  patch -Np1 -i ${srcdir}/powerpc-fix.patch
  patch -Np1 -i ${srcdir}/powerpc-fix-2.patch
  patch -Np1 -i ${srcdir}/powerpc64-fix.patch
  patch -Np1 -i ${srcdir}/powerpc64-fix-2.patch
}

build() {
  # TODO: remove this block once the libffi 3.4 rebuilds are bootstrapped
  pushd $pkgname-3.3
  ./configure \
    --prefix=/usr \
    --disable-static \
    --enable-pax_emutramp
  make
  popd

  cd $pkgname-$pkgver
  # remove --disable-exec-static-tramp once ghc and gobject-introspection
  # work fine with it enabled (https://github.com/libffi/libffi/pull/647)
  ./configure \
    --prefix=/usr \
    --disable-static \
    --disable-multi-os-directory \
    --disable-exec-static-tramp \
    --enable-pax_emutramp
  make
}

check() {
  make -C $pkgname-$pkgver check
}

package() {
  # TODO: remove this block once the libffi 3.4 rebuilds are bootstrapped
  pushd $pkgname-3.3
  make DESTDIR="$srcdir"/fakeinstall install
  install -Dt "$pkgdir"/usr/lib "$srcdir"/fakeinstall/usr/lib/libffi.so.7
  popd

  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
  install -Dm 644 README.md -t "$pkgdir"/usr/share/doc/$pkgname
}

# vim: ts=2 sw=2 et:
