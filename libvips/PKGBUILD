# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=libvips
pkgver=8.10.3
pkgrel=1
pkgdesc="A fast image processing library with low memory needs"
arch=(x86_64 powerpc64le powerpc)
license=('LGPL')
url="https://libvips.github.io/libvips/"
depends=('cfitsio' 'fftw' 'imagemagick' 'libexif' 'libgsf' 'libheif' 'libimagequant' 'librsvg'
         'libwebp' 'libxml2' 'openexr' 'orc' 'pango' 'poppler-glib')
source=("https://github.com/libvips/libvips/releases/download/v$pkgver/vips-$pkgver.tar.gz"
        libvips-glib.patch)
sha512sums=('f337717e30dcd5bf8efe089ab5579af0938e0172c9aa87ab03120ea838378e3cf5ca6477d4f32f5eeeed56fe8e6e0ab8091c7bb5fe2be07a8bfa545aa15271c7'
            'bad2d0791269e3ed7cbc3d49c648361e739dc1ad2cdef24999e4954e80635047ccae5f19dfe2f95cf34ee44a974e4137c3c8a811e1d0c4b6880a9c1c034cf1ae')

prepare() {
  cd vips-$pkgver
  patch -Np1 -i $srcdir/libvips-glib.patch
}

build() {
  cd vips-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd vips-$pkgver
  make DESTDIR="$pkgdir" install
}
