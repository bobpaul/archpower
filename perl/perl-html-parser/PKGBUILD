# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: kevin <kevin@archlinux.org>
# Contributor: Manolis Tzanidakis
# Contributor: Firmicus <francois.archlinux.org>

pkgname=perl-html-parser
pkgver=3.80
pkgrel=1
pkgdesc="Perl HTML parser class"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=('PerlArtistic')
url="https://search.cpan.org/dist/HTML-Parser/"
depends=('perl-html-tagset' 'perl-http-message' 'perl-uri')
options=('!emptydirs' '!distcc')
source=(https://www.cpan.org/authors/id/O/OA/OALDERS/HTML-Parser-$pkgver.tar.gz)
sha512sums=('0fa299791243cf15f07bc995109e4995ba3f851c3192345b64493e401fc2b4407d0196bfc79c58460791b041a3f5faf26625ec5da94660d071d1c06c954df415')

build() {
  cd HTML-Parser-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd HTML-Parser-$pkgver
  make test
}

package() {
  cd HTML-Parser-$pkgver
  make install DESTDIR="${pkgdir}"
}
