# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_pkgname=yarl
pkgname=python-yarl
pkgver=1.12.0
pkgrel=1
pkgdesc='Yet another URL library'
url='https://github.com/aio-libs/yarl/'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=('Apache-2.0')
depends=(
  'glibc'
  'python'
  'python-multidict'
  'python-idna'
)
makedepends=(
  'cython'
  'python-setuptools'
  'python-build'
  'python-installer'
  'python-wheel'
  'python-expandvars'
)
checkdepends=(
  'python-pytest'
  'python-pytest-xdist'
)
source=(https://github.com/aio-libs/yarl/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('d2028e44741bc1b47669ec9dfa2264a190d5f16d223d132fe8247011e54f41b3640e53387ae93256f82f186c2abd61718f81a45ae180e96eabedb050e83bda9e')
b2sums=('3814505c8ca9176f737b204bab92df24c63adf054ed7625ec7bacb83697fa5d3098ad16681b3729c45fd3205e04238ed5663e18dc1f25a24e31ba30cfb5c1962')

prepare() {
  cd ${_pkgname}-${pkgver}
  sed 's| .install-cython ||g' -i Makefile
}

build() {
  cd ${_pkgname}-${pkgver}
  make cythonize
  export LANG=en_US.UTF-8
  python -m build --wheel --no-isolation
}

check() {
  cd ${_pkgname}-${pkgver}
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  cd tests
  ../test-env/bin/python -m pytest -v --override-ini="addopts="
}

package() {
  cd ${_pkgname}-${pkgver}
  export LANG=en_US.UTF-8
  python -m installer --destdir="$pkgdir" dist/*.whl
}

# vim: ts=2 sw=2 et:
