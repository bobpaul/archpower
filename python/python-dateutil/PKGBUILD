# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jelle van der Waa <jelle@vdwaa.nl>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: lilydjwg <lilydjwg@gmail.com>

pkgname=python-dateutil
pkgver=2.8.2
pkgrel=4
pkgdesc="Provides powerful extensions to the standard datetime module"
arch=('any')
license=('BSD' 'Apache')
url="https://github.com/dateutil/dateutil"
depends=('python-six')
makedepends=('python-setuptools-scm' 'python-six' 'git')
checkdepends=('python-pytest' 'python-freezegun' 'python-hypothesis')
source=(git+https://github.com/dateutil/dateutil.git#commit=6b035517571e63b6a63a493740c5506ec1e5da44)
sha256sums=('SKIP')

build() {
  cd "$srcdir"/dateutil
  python setup.py build
}

check() {
  cd dateutil
  pytest -W ignore::pytest.PytestUnknownMarkWarning # https://github.com/pytest-dev/pytest/issues/5678
}

package() {
  cd "$srcdir"/dateutil
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
