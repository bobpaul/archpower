# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: SÃ©bastien Luttringer <seblu@aur.archlinux.org>

pkgname=python-sqlalchemy
pkgver=1.4.11
pkgrel=1
arch=(x86_64 powerpc64le powerpc)
url="https://www.sqlalchemy.org/"
license=('MIT')
pkgdesc='Python SQL toolkit and Object Relational Mapper'
depends=('python')
optdepends=('python-psycopg2: connect to PostgreSQL database')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-pytest-xdist' 'python-mock')
source=("https://pypi.io/packages/source/S/SQLAlchemy/SQLAlchemy-$pkgver.tar.gz"{,.asc})
validpgpkeys=('83AF7ACE251C13E6BB7DEFBD330239C1C4DAFEE1')
sha512sums=('28d5da109d754183e7f1887c652c47cd7162afec285a44ed6bc51566dc40d43cdb8cd1794f93f31b2e085488025f04243613d500936c220aaab3aedb75fb6dcd'
            'SKIP')

prepare() {
  sed -i '/warnings.filterwarnings("error", category=DeprecationWarning)/a \    warnings.filterwarnings("ignore", category=DeprecationWarning, message="Creating a LegacyVersion has been deprecated and will be removed in the next major release")' \
      SQLAlchemy-$pkgver/lib/sqlalchemy/testing/warnings.py
}

build() {
  cd "$srcdir"/SQLAlchemy-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/SQLAlchemy-$pkgver
  python setup.py pytest
}

package() {
  cd SQLAlchemy-$pkgver
  python setup.py install --root="${pkgdir}"
  install -D -m644 LICENSE \
	  "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 ft=sh et:
