# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer : Felix Yan <felixonmars@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgbase=pyopenssl
pkgname=('python-pyopenssl')
pkgver=23.1.1
pkgrel=1.2
arch=('any')
pkgdesc="Python3 wrapper module around the OpenSSL library"
url='https://github.com/pyca/pyopenssl'
license=('Apache')
depends=('openssl' 'python' 'python-six' 'python-cryptography')
replaces=('pyopenssl')
provides=('pyopenssl')
conflicts=('pyopenssl')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-pretend' 'python-flaky')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/pyca/pyopenssl/archive/${pkgver}.tar.gz)
sha512sums=('38350f2db0b93f2f501cf1c185bebdeb5cf6313eede492ed4dcd8750f22218c8b669b398728de8d62481c2f87055b4138949cfd3f7f6cf4002d1ba5860b1330d')

prepare() {
  export LC_CTYPE=en_US.UTF-8
}

build() {
  cd "$srcdir"/pyopenssl-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/pyopenssl-$pkgver
  python setup.py pytest || warning "Tests failed due to missing SSLv3_METHOD"
}

package_python-pyopenssl() {
  cd pyopenssl-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

# vim: ts=2 sw=2 et:
