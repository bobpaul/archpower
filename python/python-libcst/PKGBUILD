# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=LibCST
pkgname=python-libcst
pkgver=0.3.17
pkgrel=1
pkgdesc="A concrete syntax tree parser and serializer library for Python that preserves many aspects of Python's abstract syntax tree"
arch=('any')
url='https://github.com/Instagram/LibCST'
license=('MIT')
depends=('python-typing_extensions' 'python-typing_inspect' 'python-yaml' 'python-isort')
makedepends=('python-setuptools')
checkdepends=('python-hypothesis' 'python-hypothesmith' 'python-isort' 'python-black')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('57fc4b174e4943871f509b21135a452df36d3662b4d0a32ecb0059ef7431a990f402bc4259a5068f5d4f1c6cc462a69305490cc4c46936a138e35cbe90bd6936')

prepare() {
  cd $_pkgname-$pkgver

  sed -i 's/import AbstractBaseMatcherNodeMeta/import Optional, AbstractBaseMatcherNodeMeta/' libcst/codegen/gen_matcher_classes.py
  python -m libcst.codegen.generate matchers
}

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  python -m unittest
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
