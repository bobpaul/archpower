# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=libcst
pkgname=python-libcst
pkgver=1.3.1
pkgrel=1
pkgdesc="A concrete syntax tree parser and serializer library for Python that preserves many aspects of Python's abstract syntax tree"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://github.com/Instagram/LibCST'
license=('MIT')
depends=(
  'glibc'
  'gcc-libs'
  'python'
  'python-typing_extensions'
  'python-typing_inspect'
  'python-pyyaml'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools-rust'
  'python-setuptools-scm'
  'python-wheel'
  'ufmt'
)
checkdepends=(
  'python-hypothesis'
  'python-hypothesmith'
  'python-pytest'
)
source=("https://pypi.io/packages/source/l/libcst/libcst-$pkgver.tar.gz"
        libcst-cargo-updates.patch)
sha512sums=('c5ac467bab0ff2808db4510620444cb7705d1af88f0e3f41c43c9ab493206d58561ca2ffb9ee98f14286ee182b4271103d9ca3c3935fef5a8f8dcf60dfae5b50'
            'e7a327d16a15e7646a831d24e812b301e93c525f8cac71ec719e6b05a93679c8da0c5ac5290e971a9191d4fc75e41bf0c0cd6c02f9a336f7bc1cd44ee1a24474')

prepare() {
  cd $_pkgname-$pkgver
  # primarily for the upgrade to pyo3 >0.20.2 which implements
  # portable Atomic for 32bit systems
  patch -Np0 -i ${srcdir}/libcst-cargo-updates.patch
}

build() {
  cd $_pkgname-$pkgver

  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    --ignore libcst/tests/test_fuzz.py
    --deselect libcst/metadata/tests/test_type_inference_provider.py::TypeInferenceProviderTest::test_gen_cache_0
    --deselect libcst/metadata/tests/test_type_inference_provider.py::TypeInferenceProviderTest::test_simple_class_types_0
    --deselect libcst/metadata/tests/test_type_inference_provider.py::TypeInferenceProviderTest::test_with_empty_cache
  )
  cd $_pkgname-$pkgver

  pytest -vv "${pytest_options[@]}"
}

package() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_pkgname-$pkgver

  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  # remove tests
  rm -frv "$pkgdir/$site_packages/$_pkgname/"{tests,metadata/tests}
}

# vim:set ts=2 sw=2 et:
