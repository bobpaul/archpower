# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=sphinx-theme-builder
pkgname=python-$_pkgname
pkgver=0.2.0a13
pkgrel=1
pkgdesc='Python build backend for Sphinx themes'
arch=('any')
url='https://github.com/pradyunsg/sphinx-theme-builder'
license=('BSD')
depends=('python' 'python-packaging' 'python-rich' 'python-tomli' 'python-nodeenv' 'python-setuptools' 'python-pep621')
optdepends=('python-build: CLI'
            'python-click: CLI'
            'python-sphinx-autobuild: CLI')
makedepends=('python-setuptools' 'python-dephell')
checkdepends=('python-pytest' 'python-build' 'python-click' 'python-sphinx-autobuild')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('32d88f7f29e4f5496ae55387b4dd5ff203707c804bbefd8f8ef5019492c0028132d053a9d1d8d12ff19d2af0505b439a612354b371b86aba8f5f560d13f3df9f')

prepare() {
  cd $_pkgname-$pkgver
  #sudo pip install async-timeout==3.0.1 ptyprocess

  dephell deps convert --from pyproject.toml --to setup.py
}

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  PYTHONPATH=src pytest
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
