# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-lazy-object-proxy
pkgver=1.9.0
pkgrel=1
pkgdesc='A fast and thorough lazy object proxy'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://github.com/ionelmc/python-lazy-object-proxy"
license=('BSD')
depends=("python")
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest-benchmark')
source=("https://github.com/ionelmc/python-lazy-object-proxy/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('a737d73b109acd327e21b1c923b01d99090ba3100d669c9ac90c387d46602f09b334ad027215cb3eebf694ba8c7d8c70bb8ce6fb0a7e625002e4435a03e2687f')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd python-lazy-object-proxy-$pkgver
  python setup.py build
}

check() {
  cd python-lazy-object-proxy-$pkgver
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-cpython-310" pytest
}

package() {
  cd python-lazy-object-proxy-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
