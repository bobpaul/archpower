# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-packaging
pkgver=22.0
pkgrel=1
pkgdesc="Core utilities for Python packages"
arch=(any)
url="https://github.com/pypa/packaging"
license=('Apache')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-flit-core')
checkdepends=('python-pytest' 'python-pretend')
source=("https://github.com/pypa/packaging/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('0e01101779e0ddeedec3f260251bd823853bc5158c8d20e08b9b2b5763f9f78d801a96f656929840535431659344bcc7dd91073fd32fe52a7b441fd2cf29ddb1')

build() {
  cd packaging-$pkgver
  python -m build -nw
}

check() {
  cd packaging-$pkgver
  python -m pytest
}

package() {
  cd packaging-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
