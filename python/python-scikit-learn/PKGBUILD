# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>

pkgname=python-scikit-learn
pkgver=0.24.1
pkgrel=1
pkgdesc="A set of python modules for machine learning and data mining"
arch=(x86_64 powerpc64le powerpc)
url="http://scikit-learn.sourceforge.net/"
license=('BSD')
depends=('python-scipy' 'python-joblib' 'python-threadpoolctl')
optdepends=('python-matplotlib: plotting capabilities')
makedepends=('python-setuptools' 'cython')
makedepends_powerpc64le=('openmp')
makedepends_x86_64=('openmp')
options=(!emptydirs)
source=("scikit-learn-${pkgver}.tar.gz::https://github.com/scikit-learn/scikit-learn/archive/${pkgver}.tar.gz")
sha256sums=('a663dbe0feb04bd9949da315a3abc9ddf5ce84d4e07d16c5b96e0947ee56a4eb')

build() {
  cd "$srcdir"/scikit-learn-$pkgver
  python setup.py build
}

package() {
  cd "$srcdir"/scikit-learn-$pkgver

  python setup.py install --root="$pkgdir"/ --optimize=1

  install -D COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  # See FS#49651
  install -d "$pkgdir"/usr/share/doc/$pkgname
  cp -r doc/tutorial "$pkgdir"/usr/share/doc/$pkgname/tutorial
}
