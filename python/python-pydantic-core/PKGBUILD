# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pydantic-core
pkgname=python-pydantic-core
# WARNING: this package is pinned down to the patch-level version in python-pydantic and should only be updated in lock-step with it
pkgver=2.15.0
pkgrel=1
epoch=1
pkgdesc="Core validation logic for pydantic written in rust "
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://github.com/pydantic/pydantic-core"
license=(MIT)
depends=(
  gcc-libs
  glibc
  python
  python-typing-extensions
)
makedepends=(
  python-build
  python-installer
  python-maturin
)
checkdepends=(
  python-dirty-equals
  python-hypothesis
  python-pytest
  python-pytest-benchmark
  python-pytest-examples
  python-pytest-mock
  python-pytest-timeout
)
options=(!lto)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz
       pyo3-0.20.2.patch)
sha512sums=('24b57c9e5f03f4b0b338495a525dba53e13f8a25ada22b9dfa021dc8f2cab20aa91fa16138a291f762008a12da77016b445a718158396c85e51694e935f907ff'
            '19c420bdad332294bd1424ce8d5d2745b0c24d390be24fa8f50e5d983f37a0072f7e6f86a2c69d1beb600ca430db957154718895b239ae021e5742eaac39ce60')
b2sums=('305c6e9228769f4953080de11fb62ceda3ab20a8cc49f6e12e8dca7dbffa593c65e66eea13e33ee43b39e8a8d530f3667cadcb737bc5b2b09fdc8fa717144630'
        'ceef44371997eaaf11b23480d500303ea6f427cddf5359d40a394816bf5a9cb011c7043f71eb944775aafb2702c44a7b7b164f2ba312fdafba01e7d0d82619f8')

prepare() {
  # we don't support version pinning
  sed -e 's/,!=4.7.0//g' -i $_name-$pkgver/pyproject.toml

  cd $_name-$pkgver
  patch -Np1 -i ${srcdir}/pyo3-0.20.2.patch
}

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
  )
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  # install to temporary location, as importlib is used
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="$PWD/test_dir/$site_packages:$PYTHONPATH"
  HYPOTHESIS_PROFILE=slow pytest "${pytest_options[@]}"
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
