# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-oslo-context
pkgver=4.0.0
pkgrel=1
pkgdesc="OpenStack Context library"
arch=('any')
url="https://pypi.python.org/pypi/oslo.context/$pkgver"
license=('Apache')
depends=('python-pbr' 'python-debtcollector')
makedepends=('python-setuptools')
checkdepends=('python-mock' 'python-oslotest')
source=("https://github.com/openstack/oslo.context/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('e9d6a545ae8bbde1af722cf0eee4d0c22bd40f940ac669b7c80a9be29fee6a748fb281da55df83200b9f633e242dfc6ae43075b4dc5760f0253bf2ff18ce6fcd')

export PBR_VERSION=$pkgver

build() {
  cd oslo.context-$pkgver
  python setup.py build
}

check() {
  cd oslo.context-$pkgver
  stestr run
}

package() {
  cd oslo.context-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
