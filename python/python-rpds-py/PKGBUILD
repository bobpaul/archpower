# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-rpds-py
pkgver=0.17.1
pkgrel=1
pkgdesc='Python bindings to the Rust rpds crate for persistent data structures'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://github.com/crate-py/rpds'
license=('MIT')
depends=(
  'glibc'
  'gcc-libs'
  'python'
)
makedepends=(
  'git'
  'python-build'
  'python-maturin'
  'python-installer'
)
_commit='9c07d534116bc9823121959f035393ce58fd447e'
source=("$pkgname::git+$url#commit=$_commit"
        pyo3-0.21.2.patch)
b2sums=('3ba3967d132d4de5150766d6e8221886c956fa7182be9b7a1b46c34ee99c20a19bebf35c24419f1d14319a49616f8f3db1e783c0a529c3c257dc4244862cabba'
        '03051c37fa916ff9a8b9f73d35b25cc3a223f4841b7730513bfd619ac0da3a0ba915af82c3d792d9cfa75a7ec74a7fa9abe6f1a51457071cca19f90c66976ee5')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

prepare() {
  cd "$pkgname"

  patch -Np1 -i ${srcdir}/pyo3-0.21.2.patch

  MYARCH=${CARCH}
  case "${CARCH}" in
    riscv64) MYARCH=riscv64gc ;;
  esac

  # download dependencies
  cargo fetch --locked --target "$MYARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "$site_packages/rpds_py-$pkgver.dist-info/license_files/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
