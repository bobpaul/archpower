# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Max Mehl <aur at mehl dot mx>
# SPDX-FileCopyrightText: 2019 Max Mehl
# SPDX-License-Identifier: CC0-1.0

pkgname=python-license-expression
pkgver=30.3.0
pkgrel=2
pkgdesc='Utility to parse, normalize and compare license expressions'
arch=(any)
url='https://github.com/nexB/license-expression'
license=('Apache-2.0')
depends=(
  'python'
  'python-boolean.py'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools-scm'
  'python-wheel'
)
checkdepends=(
  'python-pytest'
  'python-pytest-xdist'
)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('110698509a2aeeb0e119e6d8613d1d9b544b28bd91ee3e8c3d09301f227eae338e7cb21b041e37ac5b83ac97f9d33f826c66cb0251e50de99c3f3608654469b5')
b2sums=('12a1ae9a4bf0fa652be124966fc5286709b6f84f316ba87858198e5d6c8060f34dbcfc1e3483e6acb3e3aae0b121e6275e5e4001c0beddc39a2d9cf2bd6691b3')

prepare() {
  cd "$pkgname"

  # Fix file to comply with PEP-440
  sed \
    -i pyproject.toml \
    -e "s/^fallback_version =.*/fallback_version = \"$pkgver\"/"
}

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl
}
