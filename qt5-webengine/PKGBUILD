# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=qt5-webengine
_qtver=5.15.2
pkgver=${_qtver/-/}
pkgrel=6
arch=(x86_64 powerpc64le)
url='https://www.qt.io'
license=('LGPL3' 'LGPL2.1' 'BSD')
pkgdesc='Provides support for web applications using the Chromium browser project'
depends=('qt5-webchannel' 'qt5-location' 'libxcomposite' 'libxrandr' 'pciutils' 'libxss' 
         'libevent' 'snappy' 'nss' 'libxslt' 'minizip' 'ffmpeg' 're2' 'libvpx' 'krb5' 'ttf-font'
         'libxtst')
makedepends=('python2' 'gperf' 'jsoncpp' 'ninja' 'qt5-tools' 'poppler' 'libpipewire02')
optdepends=('libpipewire02: WebRTC desktop sharing under Wayland')
groups=('qt' 'qt5')
_pkgfqn="${pkgname/5-/}-everywhere-src-${_qtver}"
source=("https://download.qt.io/official_releases/qt/${pkgver%.*}/${_qtver}/submodules/${_pkgfqn}.tar.xz"
         0001-Use-_fpstate-instead-of-_libc_fpstate-on-linux.patch
         0018-sandbox-membarrier.patch
         0020-msgvec-push_back.patch
         0021-sandbox-sched_getparam.patch
         0089-enable-ppc64.patch
         0090-qtwebengine-ppc64.patch
         0999-size_t-fix.patch
         chromium-icu68.patch
         qt5-webengine-glibc-2.33.patch
         v8-call-new-ListFormatter-createInstance.patch)
sha256sums=('c8afca0e43d84f7bd595436fbe4d13a5bbdb81ec5104d605085d07545b6f91e0'
            'db912bb3af0e75299a050bb6a00fa9f1483805f2642181f5a6c95f10e998c897'
            'da58d2214d1c50a7b784e18a97473c9086657d5f43e8640675e98451859307d1'
            'd13e6ad37d36834867d57e23c339b73053983dafa76c7377999ff2a5b17332c3'
            '8ec43b866f6a5e4ac0f3739a59f2e6c54ae7d2a2719a5b9102d7d2ec7053bc7f'
            '83dec2057fc6e8d2924be5d9dcf31f3f75c829a7fdc9251e126417236e674312'
            '4071048c1fcf5c57d1fa610d91368552d96959b94eb699d6c96f9266b81c330e'
            '581ee65a1e68532a78a32d1609b63cf7f8b5a89aa08fe2fe6063e0c58d17614a'
            '70ea4c7e02e65dd10d36126518deacab50e60045a91eefd1ac933801c934c8a0'
	    '5600cfa40254fa3fa2cb541d3b55cc8f7a9231de8d2830c25a7651aa392de16f'
            '44ebcff050a1c849819d66399c14bd711801d0eb64f518d292d3d6efedce3b3a')

prepare() {
  mkdir -p build
  # Disable jumbo build https://bugreports.qt.io/browse/QTBUG-88657
  sed -i 's|use_jumbo_build=true|use_jumbo_build=false|' -i ${_pkgfqn}/src/buildtools/config/common.pri

  cd ${_pkgfqn}
  patch -p1 -d src/3rdparty/chromium/v8 -i "$srcdir"/v8-call-new-ListFormatter-createInstance.patch # Fix build with ICU 69

  # thanks Voidlinux!
  patch -Np1 -i ${srcdir}/0001-Use-_fpstate-instead-of-_libc_fpstate-on-linux.patch
  patch -Np1 -i ${srcdir}/0018-sandbox-membarrier.patch
  patch -Np1 -i ${srcdir}/0020-msgvec-push_back.patch
  patch -Np1 -i ${srcdir}/0021-sandbox-sched_getparam.patch
  patch -Np1 -i ${srcdir}/0089-enable-ppc64.patch
  patch -Np1 -i ${srcdir}/0090-qtwebengine-ppc64.patch


  patch -Np1 -i ${srcdir}/qt5-webengine-glibc-2.33.patch
  patch -Np2 -i ${srcdir}/0999-size_t-fix.patch

  patch -Np0 -i ${srcdir}/chromium-icu68.patch
}

build() {
  cd build
  qmake ../${_pkgfqn} -- \
    -proprietary-codecs \
    -system-ffmpeg \
    -webp \
    -spellchecker \
    -webengine-icu \
    -webengine-kerberos \
    -webengine-webrtc-pipewire
  make
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "$pkgdir/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  install -Dm644 "$srcdir"/${_pkgfqn}/src/3rdparty/chromium/LICENSE "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE.chromium
}
