# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=qt5-webengine
_qtver=5.15.5
pkgver=${_qtver/-/}
pkgrel=2
# Upstream won't tag releases, because potatoes https://lists.qt-project.org/pipermail/interest/2021-March/036386.html
arch=(x86_64 powerpc64le)
url='https://www.qt.io'
license=('LGPL3' 'LGPL2.1' 'BSD')
pkgdesc='Provides support for web applications using the Chromium browser project'
depends=('qt5-webchannel' 'qt5-location' 'libxcomposite' 'libxrandr' 'pciutils' 'libxss' 
         'libevent' 'snappy' 'nss' 'libxslt' 'minizip' 'ffmpeg' 're2' 'libvpx' 'libxtst'
	 'ttf-font' 'libxkbfile')
 makedepends=('git' 'python2' 'python' 'gperf' 'jsoncpp' 'ninja' 'qt5-tools' 'poppler' 'libpipewire02' 'nodejs-lts-dubnium' 'clang' 'gn')
optdepends=('libpipewire02: WebRTC desktop sharing under Wayland')
groups=('qt' 'qt5')
_pkgfqn=qtwebengine
source=(git+https://code.qt.io/qt/qtwebengine.git#tag=v$pkgver-lts
        git+https://code.qt.io/qt/qtwebengine-chromium.git#branch=87-based
        0018-sandbox-membarrier.patch
        0020-msgvec-push_back.patch
        0021-sandbox-sched_getparam.patch
        0089-enable-ppc64.patch
        0090-qtwebengine-ppc64.patch
        qt5-webengine-glibc-2.33.patch
        xxx-ppc64le-libvpx.patch)
sha256sums=('SKIP'
            'SKIP'
            'da58d2214d1c50a7b784e18a97473c9086657d5f43e8640675e98451859307d1'
            'd13e6ad37d36834867d57e23c339b73053983dafa76c7377999ff2a5b17332c3'
            '7fd108dfbf67f6fbda59a74bdc0d63b443e1beb98238f5401d01bd0233122944'
            '6a859d37d2971bee39792f6a0e69ed1fed986400070c1f795c147d89c7ad948e'
            '76d99f3ed901581c756bb8b5c74513ab944840e680ac310e5463116d3cf4551e'
            '2294e5390c869963fc58f7bf1ee0a254a3f7fce3ed00c04e34a5f03e2b31b624'
            '9af5aad8554c966542550f7467bb86d3a02d101aded4407f0a14ad289e47ee62')

prepare() {
  mkdir -p build
  
  # Disable jumbo build https://bugreports.qt.io/browse/QTBUG-88657
  #sed -i 's|use_jumbo_build=true|use_jumbo_build=false|' -i ${_pkgfqn}/src/buildtools/config/common.pri

  cd ${_pkgfqn}
  git submodule init
  git submodule set-url src/3rdparty "$srcdir"/qtwebengine-chromium
  git submodule set-branch --branch 87-based src/3rdparty
  git submodule update

  patch -p1 -i "$srcdir"/qt5-webengine-glibc-2.33.patch # Fix text rendering when building with glibc 2.33

  # thanks Voidlinux!
  patch -Np1 -i ${srcdir}/0018-sandbox-membarrier.patch
  patch -Np1 -i ${srcdir}/0020-msgvec-push_back.patch
  patch -Np1 -i ${srcdir}/0089-enable-ppc64.patch
  patch -Np1 -i ${srcdir}/0090-qtwebengine-ppc64.patch

  # thanks Gentoo!
  patch -Np0 -d src/3rdparty/chromium -i ${srcdir}/xxx-ppc64le-libvpx.patch
}

build() {
  pushd ${srcdir}/qtwebengine/src/3rdparty/chromium/third_party/libvpx
  mkdir -p source/config/linux/ppc64
  mkdir -p source/libvpx/test
  touch source/libvpx/test/test.mk
  ./generate_gni.sh
  popd

  cd build
  qmake ../${_pkgfqn} -- \
    -proprietary-codecs \
    -system-ffmpeg \
    -webp \
    -spellchecker \
    -webengine-icu \
    -webengine-kerberos \
    -webengine-webrtc-pipewire \
    -webengine-pepper-plugins \
    -webengine-printing-and-pdf \
    -webengine-proprietary-codecs \
    -webengine-pulseaudio \
    -webengine-spellchecker \
    -webengine-webrtc \
    -webengine-geolocation \
    -webengine-kerberos \
    -no-webengine-embedded-build
  make
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "$pkgdir/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  install -Dm644 "$srcdir"/${_pkgfqn}/src/3rdparty/chromium/LICENSE "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE.chromium

  # Fix cmake dependency versions
  sed -e "s|$pkgver\ |5.15.2 |" -i "$pkgdir"/usr/lib/cmake/*/*Config.cmake
}
