# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
pkgname=smpeg
pkgver=0.4.5
pkgrel=5
pkgdesc="SDL MPEG Player Library"
arch=(x86_64 powerpc64le powerpc riscv64)
url="https://icculus.org/smpeg/"
license=(LGPL)
depends=(sdl)
makedepends=(subversion)
optdepends=('glu: to use glmovie')
source=("$pkgname-$pkgver::svn://svn.icculus.org/smpeg/tags/release_${pkgver//./_}")
sha256sums=('SKIP')

prepare() {
  cd $pkgname-$pkgver
  ./autogen.sh
}

build() {
  cd $pkgname-$pkgver
  CFLAGS+=" -Wno-error=narrowing" LDFLAGS+=" -lstdc++" \
  ./configure --build=$CHOST --prefix=/usr --mandir=/usr/share/man --disable-static \
              --disable-gtk-player --disable-opengl-player
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
