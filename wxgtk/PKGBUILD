# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgbase=wxgtk
pkgname=(wxgtk2 wxgtk3 wxgtk-common)
pkgver=3.1.5
pkgrel=2
arch=(x86_64 powerpc64le powerpc riscv64)
url='https://wxwidgets.org'
license=(custom:wxWindows)
makedepends=(gst-plugins-base glu libnotify gtk2 gtk3)
makedepends_powerpc=(webkit2gtk)
makedepends_powerpc64le=(webkit2gtk)
makedepends_x86_64=(webkit2gtk)
options=(!emptydirs)
source=(https://github.com/wxWidgets/wxWidgets/releases/download/v$pkgver/wxWidgets-$pkgver.tar.bz2
        wxgtk-abicheck.patch)
sha256sums=('d7b3666de33aa5c10ea41bb9405c40326e1aeb74ee725bb88f90f1d50270a224'
            '53501db871290b71967af08b60aedb738c920a307ef9bd32dd19c30498732cf8')

prepare() {
  # C++ ABI check is too strict
  patch -d wxWidgets-$pkgver -p1 -i ../wxgtk-abicheck.patch

  cp -r wxWidgets-$pkgver wxWidgets-$pkgver-gtk3
}

build() {
  cd wxWidgets-$pkgver
  ./configure --prefix=/usr --libdir=/usr/lib --with-gtk=2 --with-opengl --enable-unicode \
    --enable-graphics_ctx --enable-mediactrl --with-regex=builtin \
    --with-libpng=sys --with-libxpm=sys --with-libjpeg=sys --with-libtiff=sys \
    --disable-precomp-headers
  make
  make -C locale allmo

  [ ! "${CARCH}" == 'riscv' ] && flags="--enable-webview"
  cd ../wxWidgets-$pkgver-gtk3
  ./configure --prefix=/usr --libdir=/usr/lib --with-gtk=3 --with-opengl --enable-unicode \
    --enable-graphics_ctx --enable-mediactrl $flags --with-regex=builtin \
    --with-libpng=sys --with-libxpm=sys --with-libjpeg=sys --with-libtiff=sys \
    --disable-precomp-headers
  make
}

package_wxgtk-common() {
  pkgdesc='Common libraries and headers for wxgtk2 and wxgtk3'
  depends=(zlib gcc-libs expat)

  cd wxWidgets-$pkgver
  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir"/usr/{bin/wx-config,lib/{wx,libwx_gtk*}}

  install -D -m644 docs/licence.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_wxgtk2() {
  pkgdesc='GTK+2 implementation of wxWidgets API for GUI'
  depends=(gtk2 libgl gst-plugins-base-libs libsm libxxf86vm wxgtk-common libnotify)

  cd wxWidgets-$pkgver
  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir"/usr/{include,share,lib/libwx_base*,bin/wxrc*}
  
  install -D -m644 docs/licence.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_wxgtk3() {
  pkgdesc='GTK+3 implementation of wxWidgets API for GUI'
  depends=(gtk3 gst-plugins-base-libs libsm libxxf86vm wxgtk-common libnotify)
  optdepends=('webkit2gtk: for webview support')
  conflicts=('wxgtk<3.0.3.1-2')

  cd wxWidgets-$pkgver-gtk3
  make DESTDIR="$pkgdir" install  
  rm -r "$pkgdir"/usr/{include,share,lib/libwx_base*,bin/wxrc*}
  mv "$pkgdir"/usr/bin/wx-config{,-gtk3}
   
  install -D -m644 docs/licence.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
