# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: SaultDon <sault.don gmail>
pkgname=pdal
pkgver=2.5.0
pkgrel=1
pkgdesc="A C++ library for translating and manipulating point cloud data"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="http://www.pdal.io"
license=('BSD')
depends=('gdal' 'libgeotiff' 'jsoncpp' 'libunwind')
makedepends=('cmake' 'python' 'python-numpy' 'ninja' 'postgresql-libs')
optdepends=('python-numpy: for the Python plugin'
            'sqlite: for the sqlite plugin'
            'postgresql-libs: for the postgresql plugin')
provides=('pdal')
source=("https://github.com/PDAL/PDAL/releases/download/${pkgver}/PDAL-${pkgver}-src.tar.gz"
        pdal-2.5.0-fix-opt-abseil.patch)
sha512sums=('10545ad44efbb7a654d596d4a861a81177d61a602ff8425965def5dd99e1595a7873c741f99eefcd7ae5dc7f438b321b51399db3b33fb8eddf46bec8394472b4'
            'c45040f70a57d3be0f3c739a580d4a72c74d9f219393b17d34608b4318abc72a30cfabd66b518a1dea40b382b9c018816c86f59df2c754a985d99b55cd185787')

prepare() {
  cd "PDAL-$pkgver-src/"
  patch -Np1 -i ${srcdir}/pdal-2.5.0-fix-opt-abseil.patch
}

build() {
  cd "PDAL-$pkgver-src/"

  case "${CARCH}" in
    powerpc64)
      CFLAGS="${CFLAGS/-mabi=altivec -maltivec/}"
      CXXFLAGS="${CXXFLAGS/-mabi=altivec -maltivec/}"
    ;;
  esac

  cmake \
      -Bbuild \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DBUILD_PLUGIN_PGPOINTCLOUD=ON \
      -DWITH_COMPLETION=ON

  ninja -C build
}

package() {
  cd "PDAL-$pkgver-src"

  DESTDIR="$pkgdir/" ninja -C build install

  install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}
