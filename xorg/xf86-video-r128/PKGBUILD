# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=xf86-video-r128
pkgver=6.12.1
pkgrel=1
pkgdesc="X.org ati Rage128 video driver"
arch=(i686 x86_64 powerpc64 powerpc)
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('glibc')
makedepends=('xorg-server-devel' 'X-ABI-VIDEODRV_VERSION=25.2')
optdepends=('r128-dri: DRI1 support from mesa-7.11')
groups=('xorg-drivers')
source=(https://gitlab.freedesktop.org/xorg/driver/$pkgname/-/archive/$pkgname-$pkgver/$pkgname-$pkgname-$pkgver.tar.bz2
        fix-non-x86.patch)
sha256sums=('9c2c92f111fa07d0d261d1a2650b73627e66ad7c8840c4b3a12a9c4754955407'
            '02e54c11bca8716206b0b627d06ed2b1801193045c370ba3f1bdc79632f95a45')
#validpgpkeys=('6EA3F3F3B9082632A9CBE931D53A0445B47A0DAB') # Connor Behan <connor.behan@gmail.com>

prepare() {
  cd ${pkgname}-${pkgname}-${pkgver}
  patch -Np1 -i ${srcdir}/fix-non-x86.patch
}

build() {
  cd ${pkgname}-${pkgname}-${pkgver}
  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
