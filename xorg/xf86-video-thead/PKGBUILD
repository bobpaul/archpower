# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>

pkgname=xf86-video-thead
pkgver=1.0.2
pkgrel=1
epoch=1
pkgdesc="X.org T-Head 1520 video driver"
arch=(riscv64)
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('systemd-libs' 'mesa')
makedepends=('xorg-server-devel' 'systemd' 'X-ABI-VIDEODRV_VERSION=25.2' 'git')
conflicts=('xorg-server<21.1.1' 'X-ABI-VIDEODRV_VERSION<25' 'X-ABI-VIDEODRV_VERSION>=26')
groups=('xorg-drivers')
_commit=e3b21e56ec7272f7216ab1f723e48c597571cb51
source=("${pkgname}::git+https://github.com/revyos/xf86-video-thead#commit=$_commit")
sha512sums=('84fc166fc31ddc67d8e81f018be67106aec63dd4692b0a0b3e0946ac3b2d78ebd775ab7899f772667925331981246071e4fd182b4bbdf9c943b09c28364922a6')

prepare() {
  cd ${pkgname}

  git submodule init
  git submodule update

  autoreconf -fiv
}

build() {
  cd ${pkgname}

  # Since pacman 5.0.2-2, hardened flags are now enabled in makepkg.conf
  # With them, module fail to load with undefined symbol.
  # See https://bugs.archlinux.org/task/55102 / https://bugs.archlinux.org/task/54845
  export CFLAGS=${CFLAGS/-fno-plt}
  export CXXFLAGS=${CXXFLAGS/-fno-plt}
  export LDFLAGS=${LDFLAGS/,-z,now}

  ./configure --prefix=/usr --build=${CHOST}
  make
}

package() {
  cd ${pkgname}

  make "DESTDIR=${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
