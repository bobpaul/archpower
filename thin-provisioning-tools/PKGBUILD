# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Eric BÃ©langer <eric@archlinux.org>
# Contributor: Jason Hall <cakersq@gmail.com>

pkgname=thin-provisioning-tools
pkgver=1.0.9
pkgrel=2
pkgdesc='Suite of tools for manipulating the metadata of the dm-thin device-mapper target'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://github.com/jthornber/thin-provisioning-tools'
license=('GPL3')
depends=('gcc-libs')
makedepends=('cargo')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/jthornber/${pkgname}/archive/v${pkgver}.tar.gz"
        hotfix-no-atomic64.diff)
sha256sums=('830a6f86f96bfaadd779ca36b15a02b21fd1082ea0e72102b269d999d0b20429'
            '6eb1ecd180a3184374ac930b5a69414dc664cde14ba7945b008d5c3357e5784d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  _target=${CHOST}
  case "${CARCH}" in
    powerpc)
      # T2 is also awesome
      patch -Np1 -i ${srcdir}/hotfix-no-atomic64.diff
    ;;
    riscv64)
      _target=riscv64gc-unknown-linux-gnu
    ;;
  esac

  cargo build --release --target=${_target}
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make \
    DESTDIR="${pkgdir}"/usr \
    BINDIR="${pkgdir}"/usr/bin \
    DATADIR="${pkgdir}"/usr/share \
    install
}

