# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kwallet
pkgver=5.102.0
pkgrel=1
pkgdesc='Secure and unified container for user passwords'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(knotifications kiconthemes kservice gpgme qca-qt5)
makedepends=(extra-cmake-modules kdoctools boost doxygen qt5-tools qt5-doc)
optdepends=('kwalletmanager: Configuration GUI')
provides=(org.freedesktop.secrets)
replaces=(kwallet-secrets)
groups=(kf5)
source=(https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz{,.sig}
        kwallet-gpgme++-rewind-instead.patch)
sha256sums=('a311f080a5e9a3d0c13ae115935a57c2fcaca0fa0ef8773c09b3f5e5f1640983'
            'SKIP'
            '32491f0150ac5fb1a644c8bbe32971181237655c9f23fb7c20b1cf20da606b17')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  cd ${pkgname}-${pkgver}
  # off_t isn't great and we just rewind anyway
  patch -Np1 -i ${srcdir}/kwallet-gpgme++-rewind-instead.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON \
    -D_OFFT_IS_64BIT=TRUE
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
