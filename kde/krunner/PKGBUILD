# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=krunner
pkgver=5.84.0
pkgrel=1
pkgdesc='Framework for providing different actions given a string query'
arch=(x86_64 powerpc64le powerpc)
url='https://community.kde.org/Frameworks'
license=(LGPL)
groups=(kf5)
depends=(plasma-framework threadweaver)
makedepends=(extra-cmake-modules kdoctools doxygen qt5-tools qt5-doc)
source=(https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('37a8049e2161cfaa2659160fef0734c5a918a522ec40888fdca5c2b2530f5195'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
