# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Zuf <kontakt.zuf@gmail.com>
# Contributor: Darwin Bautista <djclue917@gmail.com>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=kdenlive
pkgver=21.04.3
pkgrel=1
pkgdesc='A non-linear video editor for Linux using the MLT video framework'
arch=(x86_64 powerpc64le powerpc)
url='https://apps.kde.org/kdenlive/'
license=(GPL)
groups=(kde-applications kde-multimedia)
depends=(qt5-networkauth knewstuff knotifyconfig kfilemetadata purpose mlt6 rttr breeze-icons frei0r-plugins)
makedepends=(extra-cmake-modules kdoctools v4l-utils)
optdepends=('ffmpeg: for FFmpeg plugin'
            'cdrtools: for creation of DVD ISO images'
            'dvdauthor: for creation of DVD'
            'dvgrab: for firewire capture'
            'recordmydesktop: for screen capture'
            'xine-ui: for DVD preview'
            'vlc: for DVD preview'
            'opencv: for motion tracking'
            'plasma-desktop: theme configuration'
            'opentimelineio: timeline export/import')
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        kdenlive-mlt6.patch)
sha256sums=('754fae49644afbc8ecc0f5a4d579f09738f189a7626c99f862e5e4cdb6426df1'
            'SKIP'
            'db0819cea73fc7c2926c39a3de66fba86e94a6ca2e1f97e7bad1166ac6aec5bb')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 < kdenlive-mlt6.patch # Use melt6 executable name
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DCRASH_AUTO_TEST=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
