# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kcalutils
pkgver=22.04.0
pkgrel=2
pkgdesc='The KDE calendar utility library'
arch=(x86_64 powerpc64le powerpc riscv64)
url='https://kontact.kde.org'
license=(LGPL)
depends=(kcalendarcore kidentitymanagement grantlee)
makedepends=(extra-cmake-modules doxygen qt5-tools)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/pim/kcalutils/-/commit/494331a2.patch)
sha256sums=('478237de88282aa074ff6d83d7af7b8e0e9f24e6bad2c1709215455f09abc972'
            'SKIP'
            'c40249f3f7f8da26cee9ff731b399656259da3c5d73778e6636b26ea1cd30bef')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

prepare() {
  patch -d $pkgname-$pkgver -p1 < 494331a2.patch # Fix displaying calendar invites in kmail
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
