# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kget
pkgver=21.08.2
pkgrel=1
pkgdesc='Download Manager'
url='https://apps.kde.org/kget/'
arch=(x86_64 powerpc64le powerpc)
license=(GPL LGPL FDL)
groups=(kde-applications kde-network)
depends=(qca-qt5 qgpgme plasma-workspace kdelibs4support)
makedepends=(extra-cmake-modules boost libktorrent libmms kdoctools kdesignerplugin kinit)
optdepends=('libktorrent: torrent support' 'libmms: MMS support')
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('d0c40b968d184f1a45e83dc26ac7e4a2a9761196bc7c3e8cc85d9539ed380086'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
