# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org> 
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=umbrello
pkgver=21.08.2
pkgrel=1
pkgdesc='UML modeller'
arch=(x86_64 powerpc64le powerpc)
url='https://apps.kde.org/umbrello/'
license=(GPL)
depends=(kdelibs4support kdevelop-php qt5-webkit)
depends_powerpc64le=(qt5-webengine)
depends_x64_64=(qt5-webengine)
makedepends=(extra-cmake-modules kdoctools kdesignerplugin kdevelop-pg-qt llvm kinit doxygen)
groups=(kde-applications kdesdk)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('d917cc43706e9064651e9e4a1870047535b6b27dcdd2f37a365abf7b0d003ff6'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() { 
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_UNITTESTS=OFF \
    -DBUILD_KF5=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
