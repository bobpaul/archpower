# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux,org>
# Contributor: Elvis Angelaccio <elvis.angelaccio@kdemail.net>
# Contributor: Gustavo alvarez <sl1pkn07@gmail.com>

pkgname=kwave
pkgver=21.08.1
pkgrel=1
pkgdesc='A sound editor'
arch=(x86_64 powerpc64le powerpc)
url='https://apps.kde.org/kwave/'
license=(GPL)
depends=(kio audiofile libmad libsamplerate hicolor-icon-theme id3lib fftw)
makedepends=(extra-cmake-modules kdoctools doxygen librsvg opus)
optdepends=('opus: OGG plugin')
groups=(kde-applications kde-multimedia)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('9463383c6e0f6a25d7bef2ae39842a490c72de4c28614b7b1a3e5d788c9d87b6'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
