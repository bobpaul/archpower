# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kcontacts
epoch=1
pkgver=5.80.0
pkgrel=1
pkgdesc="Address book API for KDE"
arch=(x86_64 powerpc64le)
url="https://community.kde.org/Frameworks"
license=(LGPL)
depends=(kcoreaddons kconfig ki18n kcodecs iso-codes)
makedepends=(extra-cmake-modules doxygen qt5-tools qt5-doc)
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('d387b909e2e52b35efef6201502cb71705a76fdcd4fda8e7e535e5c66cbf7b9c'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
