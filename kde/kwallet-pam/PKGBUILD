# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer:Antonio Rojas <arojas@archlinux.org>

pkgname=kwallet-pam
pkgver=5.22.3
pkgrel=1
pkgdesc='KWallet PAM integration'
arch=(x86_64 powerpc64le powerpc)
url='https://kde.org/plasma-desktop/'
license=(LGPL)
depends=(socat kwallet)
makedepends=(extra-cmake-modules)
groups=(plasma)
source=(https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('af217af41ea10eb65e9ed171d84328b04f4e3ab6a791b69af61064fc72e14d74'
            'SKIP')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
