# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=akonadi-search
pkgver=20.12.3
pkgrel=1
pkgdesc="Libraries and daemons to implement searching in Akonadi"
url='https://kontact.kde.org'
arch=(x86_64 powerpc64le)
license=(GPL LGPL FDL)
depends=(xapian-core akonadi-mime kcontacts kcalendarcore krunner)
makedepends=(extra-cmake-modules boost)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('f85cac2a99e94772d780ea8cbaaf069f86fd7c0b3e7a5c6ea1dbad5a2c104c2d'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
