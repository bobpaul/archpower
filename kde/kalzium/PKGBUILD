# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=kalzium
pkgver=20.12.3
pkgrel=1
pkgdesc="Periodic Table of Elements"
url="https://kde.org/applications/education/kalzium/"
arch=(x86_64 powerpc64le)
license=(GPL LGPL FDL)
depends=(kparts knewstuff kplotting kunitconversion qt5-script avogadrolibs openbabel vtk hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools eigen ocaml facile python boost)
groups=(kde-applications kde-education)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
        kalzium-openbabel3.patch::"https://invent.kde.org/education/kalzium/commit/2b9ebd85.patch")
sha256sums=('8233969b13f104e8388a771df2561f49fa65a6913184a0d92c7d4ec4ecdcffe4'
            'SKIP'
            '742734255681bcd57569f16d58deb3f2bb38b353352fa77d6e65772846334a9c')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 -i ../kalzium-openbabel3.patch # Fix build with openbabel 3
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

