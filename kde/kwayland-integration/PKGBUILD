# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer:Antonio Rojas <arojas@archlinux.org>

pkgname=kwayland-integration
pkgver=5.22.5
pkgrel=1
pkgdesc='Provides integration plugins for various KDE frameworks for the wayland windowing system'
arch=(x86_64 powerpc64le powerpc)
url='https://kde.org/plasma-desktop/'
license=(LGPL)
depends=(kwindowsystem kidletime kwayland kguiaddons)
makedepends=(extra-cmake-modules)
groups=(plasma)
source=(https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('4cbc301227a507fb2ac42e08130c79ff58a9905ebd444ddd64c78bb1e834fb4d'
            'SKIP')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
