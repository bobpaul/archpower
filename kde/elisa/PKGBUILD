# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=elisa
pkgver=24.01.95
pkgrel=1
pkgdesc='A simple music player aiming to provide a nice experience for its users'
url='https://apps.kde.org/elisa/'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(LGPL-2.0-or-later3)
depends=(gcc-libs
         glibc
         kcolorscheme
         kconfig
         kconfigwidgets
         kcoreaddons
         kcrash
         kdbusaddons
         kfilemetadata
         ki18n
         kiconthemes
         kio
         kirigami
         kitemviews
         kxmlgui
         qqc2-desktop-style
         qt6-base
         qt6-declarative
         qt6-multimedia
         vlc)
makedepends=(extra-cmake-modules
             kdoctools)
groups=(kde-applications
        kde-multimedia)
source=(https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('938bf7be4bce4efb9c7b8f9ba1865ed9d0dd08de8ea4eee45a2cda18c3522c4b'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF 
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
