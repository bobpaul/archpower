# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=akonadi-calendar
pkgver=23.08.4
pkgrel=1
pkgdesc='Akonadi calendar integration'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://kontact.kde.org'
license=(LGPL)
depends=(kmailtransport kcalutils akonadi-contacts akonadi-mime)
depends_x86_64=(messagelib)
makedepends=(extra-cmake-modules qt5-tools doxygen)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        fuck-webengine.patch)
sha256sums=('b8a6b260c3230d8336382d8398e3106e308148bf0d3e9dc1dd613e9533ce5e75'
            'SKIP'
            '4f7ab7a4eb2e434bca20456c5788b8de88ada751ee83da4a583c7ca2e9439cf0')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
  cd $pkgname-$pkgver
#FUCKTHIS: Yes, screw this! Remote iTIP Email encryption support, it is not worth breaking most if not
#FUCKTHIS: all non-mainstream ISAs.
  patch -Np1 -i ${srcdir}/fuck-webengine.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
