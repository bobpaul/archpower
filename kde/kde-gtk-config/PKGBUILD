# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kde-gtk-config
pkgver=6.0.0
_dirver=$(echo $pkgver | cut -d. -f1-3)
pkgrel=3
pkgdesc='Syncs KDE settings to GTK applications'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://kde.org/plasma-desktop/'
license=(LGPL-2.0-or-later)
depends=(gcc-libs
         glib2
         glibc
         kcolorscheme
         kconfig
         kcoreaddons
         kdbusaddons
         kdecoration
         kguiaddons
         kwindowsystem
         qt6-base
         qt6-svg
         sh
         xsettingsd)
makedepends=(extra-cmake-modules
             gtk3
             sassc)
optdepends=('gtk3: GTK3 apps support')
groups=(plasma)
source=(https://download.kde.org/stable/plasma/$_dirver/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/plasma/kde-gtk-config/-/commit/541004a3.patch)
sha256sums=('60071ffcb5427818a50caa95b2272489da10805c32b975d382026c078f8d8453'
            'SKIP'
            '19d028af2a6d060cfbac881876a2541cc3d101434fb292c13bebe2663aeeefe2')
validpgpkeys=('E0A3EB202F8E57528E13E72FD7574483BB57B18D'  # Jonathan Esk-Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  # https://invent.kde.org/plasma/kde-gtk-config/-/merge_requests/107
  patch -d $pkgname-$pkgver -p1 < 541004a3.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
