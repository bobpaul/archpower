# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kdepim-addons
pkgver=21.08.0
pkgrel=2
pkgdesc='Addons for KDE PIM applications'
arch=(x86_64 powerpc64le)
url='https://kontact.kde.org'
license=(GPL LGPL FDL)
depends=(incidenceeditor ktnef libksieve akonadi-import-wizard poppler-qt5 kitinerary kontactinterface discount)
makedepends=(extra-cmake-modules kdoctools boost kaddressbook)
optdepends=('grammalecte: French grammar plugin')
groups=(kde-applications kdepim)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/pim/kdepim-addons/-/commit/3fb106d7.patch)
sha256sums=('c224dce2b422e10ea7d908070549dcf7ce956f546ed440d333ee1bdb7f086e39'
            'SKIP'
            'dbd41796ee4a5f13e43ca93c8e0787100d05b3deaa96d5bbfb20876b22169868')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 < 3fb106d7.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
