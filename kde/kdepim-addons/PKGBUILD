# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kdepim-addons
pkgver=20.12.3
pkgrel=1
pkgdesc='Addons for KDE PIM applications'
arch=(x86_64 powerpc64le)
url='https://kontact.kde.org'
license=(GPL LGPL FDL)
depends=(incidenceeditor ktnef libksieve akonadi-import-wizard poppler-qt5 kitinerary kontactinterface discount)
makedepends=(extra-cmake-modules kdoctools boost kaddressbook)
optdepends=('grammalecte: French grammar plugin')
groups=(kde-applications kdepim)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('b76135782d910b787eef321a18acb81cf637fcfadee954cf482f7be19722a671'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
