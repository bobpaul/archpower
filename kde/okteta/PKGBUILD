# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=okteta
pkgver=0.26.6
pkgrel=1
epoch=1
pkgdesc='KDE hex editor for viewing and editing the raw data of files'
url='https://kde.org/applications/utilities/okteta/'
arch=(x86_64 powerpc64le powerpc)
license=(GPL LGPL FDL)
depends=(kcmutils kparts knewstuff qca qt5-script hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools qt5-tools)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/KDE/okteta/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('a47e9f517647c74ed7a62b7fa1df68653d8273a45e84a7c07cd8d55d50f6d8af')
validpgpkeys=(E191FD5BE6F46870F09E82B2024E7FB43D015474  # Friedrich W. H. Kossebau <kossebau@kde.org>
              0A48BC961075B4BA8523E3790A345FB086E797D9) # https://share.kde.org/s/8dKaJTw8pR8iiwJ

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
