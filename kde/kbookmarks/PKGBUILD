# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kbookmarks
pkgver=5.92.0
pkgrel=1
pkgdesc='Support for bookmarks and the XBEL format'
arch=(x86_64 powerpc64le powerpc riscv64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kxmlgui)
makedepends=(extra-cmake-modules qt5-tools qt5-doc doxygen)
groups=(kf5)
source=(https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('99504e761621b56e07a561e1caea196fe4623c9334b889f081e3da8e4a19d543'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>
options=(debug)

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
