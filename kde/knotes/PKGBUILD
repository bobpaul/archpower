# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=knotes
pkgver=22.04.0
pkgrel=1
pkgdesc='Popup notes'
arch=(x86_64 powerpc64le powerpc riscv64)
url='https://apps.kde.org/knotes/'
license=(GPL2)
depends=(kdepim-runtime kdnssd kontactinterface)
makedepends=(extra-cmake-modules kdoctools)
groups=(kde-applications kde-pim)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('555357eb9163e056777252221de174c5d8dea366c22070d3bed30bbf59d87908'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
