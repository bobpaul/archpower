# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=plasma-framework
pkgver=5.93.0
pkgrel=4
pkgdesc='Plasma library and runtime components based upon KF5 and Qt5'
arch=(x86_64 powerpc64le powerpc riscv64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kactivities kdeclarative kwayland kirigami2)
makedepends=(extra-cmake-modules qt5-tools qt5-doc kdoctools doxygen)
groups=(kf5)
source=(https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/frameworks/plasma-framework/-/commit/b62d83be.patch
        https://invent.kde.org/frameworks/plasma-framework/-/commit/aec00b32.patch
        https://invent.kde.org/frameworks/plasma-framework/-/commit/734b956c.patch)
sha256sums=('d4a2c687ea2ea8dfe7c256eb58441fc458102ed4b445dc24685513dd96b5c322'
            'SKIP'
            '441555e3bda01c0f6d13985df8b033df7661b1410817b85a18e595250369b68e'
            'edd14e81c42f5a8bac2d2e686d8aa6a02a03e67020b85dd30f6864ceb591aeaa'
            'fcf0227e0794678b1da9497d109bafdf148fee0f7dc88420ea25dc3ad5c0bb3a')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>
options=(debug)

prepare() {
  patch -d $pkgname-$pkgver -p1 < b62d83be.patch # Fix plasmoid sizes
  patch -d $pkgname-$pkgver -p1 < aec00b32.patch # Fix OSD position
  patch -d $pkgname-$pkgver -p1 < 734b956c.patch # Fix highlight in some applets
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
