# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdepim-runtime
pkgver=21.12.3
pkgrel=1
pkgdesc='Extends the functionality of kdepim'
arch=(x86_64 powerpc64le powerpc riscv64)
url='https://kontact.kde.org'
license=(GPL LGPL FDL)
depends=(libkolabxml kdav akonadi-calendar knotifyconfig kalarmcal kmbox pimcommon akonadi-notes akonadi qca-qt5 qt5-networkauth)
makedepends=(extra-cmake-modules kdoctools boost libetebase)
optdepends=('libetebase: EteSync resource')
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        kdepim-runtime-no-webengine.patch)
sha256sums=('f5b00a947cd74a39ee18d61dbfbc59b9b8b5137b52674bb42905d89d98b519aa'
            'SKIP'
            '6b78360218716084ec795f686307e99ec186256d4bf84b44c7dc6ef74188a639')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

prepare() {
  cd ${pkgname}-${pkgver}
  case "${CARCH}" in
    powerpc|riscv64) patch -Np1 -i ${srcdir}/kdepim-runtime-no-webengine.patch ;;
  esac
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
