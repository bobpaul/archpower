# Maintainer: Antonio Rojas <arojas@archlinux.org> 

pkgname=discover
pkgver=5.23.1
pkgrel=1
pkgdesc='KDE and Plasma resources management GUI'
arch=(x86_64 powerpc64le powerpc)
url='https://kde.org/plasma-desktop/'
license=(LGPL)
depends=(knewstuff kitemmodels kcmutils kidletime qt5-graphicaleffects appstream-qt archlinux-appstream-data
         hicolor-icon-theme kirigami2 discount kuserfeedback)
makedepends=(extra-cmake-modules plasma-framework packagekit-qt5 flatpak)
makedepends_x86_64=(fwupd)
optdepends=('packagekit-qt5: to manage packages from Arch Linux repositories' 'flatpak: Flatpak packages support')
optdepends_x86_64=('fwupd: firmware update support')
optdepends=('packagekit-qt5: to manage packages from Arch Linux repositories' 'flatpak: Flatpak packages support'
            'fwupd: firmware update support')
groups=(plasma)
source=(https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('44255c9b1a8df70a4c710396ccbdaf1725f93694a040d4197e4bf45285e6ee71'
            'SKIP')
validpgpkeys=('E0A3EB202F8E57528E13E72FD7574483BB57B18D'  # Jonathan Esk-Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF \
    -DWITH_KCM=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
