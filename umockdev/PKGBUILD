# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Marcin (CTRL) Wieczorek <marcin@marcin.co>
# Contributor: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=umockdev
pkgver=0.17.9
pkgrel=1
pkgdesc="Record/mock hardware devices for bug reports and regression tests"
arch=(x86_64 powerpc64le powerpc riscv64)
url="https://github.com/martinpitt/umockdev/"
license=('LGPL')
depends=('systemd' 'libgudev')
makedepends=('meson' 'gobject-introspection' 'gtk-doc' 'vala')
source=($pkgname-$pkgver.tar.gz::https://github.com/martinpitt/$pkgname/archive/$pkgver.tar.gz)
sha256sums=('bad57f40f495702638ee49179c39655b71b99308c5d37ee988effae95b099e2d')

build() {
  cd $pkgname-$pkgver

  arch-meson -Dgtk_doc=true ../build
  ninja -v -C ../build
}

check() {
  cd $pkgname-$pkgver

  meson test -C ../build --print-errorlogs
}

package() {
  cd $pkgname-$pkgver

  DESTDIR="${pkgdir}" meson install -C ../build
}
