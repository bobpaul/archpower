# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Marcin (CTRL) Wieczorek <marcin@marcin.co>
# Contributor: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=umockdev
pkgver=0.15.4
pkgrel=1
pkgdesc="Record/mock hardware devices for bug reports and regression tests"
arch=(x86_64 powerpc64le powerpc)
url="https://github.com/martinpitt/umockdev/"
license=('LGPL')
depends=('systemd' 'libgudev')
makedepends=('meson' 'gobject-introspection' 'gtk-doc' 'vala')
source=($pkgname-$pkgver.tar.gz::https://github.com/martinpitt/$pkgname/archive/$pkgver.tar.gz
	$pkgname-$pkgver-glib-assertion.patch::https://github.com/martinpitt/umockdev/commit/a236f0b55fbb6ff50a6429da9d404703d6637d94.patch)
sha256sums=('4121c048976d6b51b44a362835478fa8aaa16048060f3398952be828144a015e'
            '037669cfbe44e5f690ebdf94b60a477034df70cf91be0ae35c150d41d24fe085')

build() {
  cd $pkgname-$pkgver

  patch -Np1 -i $srcdir/$pkgname-$pkgver-glib-assertion.patch

  arch-meson -Dgtk_doc=true ../build
  ninja -v -C ../build
}

check() {
  cd $pkgname-$pkgver

  meson test -C ../build --print-errorlogs
}

package() {
  cd $pkgname-$pkgver

  DESTDIR="${pkgdir}" meson install -C ../build
}
