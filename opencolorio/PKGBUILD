# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Dumitru Ursu <dima@ceata.org>

pkgname=opencolorio
pkgver=2.3.0
pkgrel=1
pkgdesc="A color management framework for visual effects and animation"
arch=(x86_64 powerpc64le)
url="https://opencolorio.org"
license=('BSD')
depends=('freeglut' 'glew' 'lcms2' 'tinyxml' 'yaml-cpp' 'imath' 'pystring' 'minizip-ng' 'openexr')
makedepends=('cmake' 'python' 'pybind11' 'ninja')
checkdepends=('openimageio')
optdepends=('python: python bindings')
source=($pkgname-$pkgver.tar.gz::https://github.com/AcademySoftwareFoundation/OpenColorIO/archive/v$pkgver.tar.gz
yaml-cpp-0.8.patch)
sha512sums=('cdf20ca6c85b1d70244d8532d010344f1cf85be6a116d831e83ea04d7c47a961049691e6a241e9a7f632d864df57a4bccb68084e27187ec8a05f1fdf344824d7'
            '9e7ec2cf6fce73ef593ad1c96cba0ae523f5850a5827356f311559ef3c9c5576741b48c8c9568dad7dd1009da7dcb9ff3578ee5cdac8828dbc2f64e2dbf2dee5')

prepare() {
  patch -d OpenColorIO-$pkgver -p1 < yaml-cpp-0.8.patch # Fix build witth yaml-cpp 0.8
}

build() {
  cd OpenColorIO-$pkgver

  CXXFLAGS+=" -ffat-lto-objects"
  cmake \
    -GNinja \
    -Bbuild \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_SKIP_INSTALL_RPATH=YES \
    -Wno-dev
  ninja -C build
}

check() {
  cd OpenColorIO-$pkgver/build

  ctest -E test_gpu
}

package() {
  cd OpenColorIO-$pkgver

  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
