# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Dan McGee <dan@archlinux.org>
# Contributor: Florian Zeitz <florob at babelmonkeys dot de>

pkgname=numactl
pkgver=2.0.14
pkgrel=2
pkgdesc="Simple NUMA policy support"
arch=(x86_64 powerpc64le powerpc riscv64)
url="http://oss.sgi.com/projects/libnuma/"
license=(LGPL2.1 GPL2)
depends=(glibc)
provides=(libnuma.so)
# NOTE: debug pkg currently unavailable as it contains /build dir
source=("https://github.com/numactl/numactl/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
	numactl-2.0.14-latomic.patch
        numactl-2.0.14-numademo-cflags.patch)
sha512sums=('28b95985d6b2f26c5f6f15fe235224c998c86f534adf5fdaa355a292cf2fd65515c91ba2a76c899d552d439b18ea1209a1712bd6755f8ee3a442f3935993b2e6'
            'c699b6de2b66dc9e37bc88406e802183d600fc177cc36ff7699df5c0cc465bb3e262a87d7ad37ee35344b3f581da576dca7900640f57d65b2ad5429c422acbc3'
            '0d58314ee1454add2d4d35114c5dca0d4785ecd09700f041ad312e7aa0b2d23aa1c3bc0ae01a33db901252081509de45abce690acb3c6ae3f0d9393903739386')
b2sums=('5f2abe25061ac29ecad8a6b24fe800d72c3538d9a155358f4b329ed1140053c7dbd93f01891904f76db94ed01113a34b4ebcbbf40e4060caf747958785ac9590'
        'fbaec11307df3fd619254f5f1f253c856169390ec75e1e83b9c3c3b8c76a4c5c48c099496b2aea6dacedeb9c1a9f2d8991621d9f5e1c32a0f1f533968e905c45'
        '23eb5a93f07edcd67d3b964bd3b48469ec4316e64a9b1641b3dc92e299f03b4f5ec4a31154236fb811fc613da5e8bdd9b401238f6da883f1818f67ab1800ebec')

prepare(){
  cd "${pkgname}-${pkgver}"
  patch -Np1 -i ${srcdir}/numactl-2.0.14-latomic.patch
  patch -Np1 -i ${srcdir}/numactl-2.0.14-numademo-cflags.patch
  autoreconf -fiv
}

build() {
  cd "${pkgname}-${pkgver}"

  ./configure --prefix=/usr
  make
}

check() {
  make -k test -C $pkgname-$pkgver || echo "Tests known to fail depending on system load."
}

package() {
  make DESTDIR="$pkgdir" install -C $pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgver/README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  # move_pages(2) is provided by man-pages and is more up-to-date there anyway
  rm -rf "$pkgdir/usr/share/man/man2"
}
