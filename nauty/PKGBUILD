# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=nauty
epoch=1
pkgver=2.8.9
pkgrel=2
pkgdesc='A program for computing automorphism groups of graphs and digraphs'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='http://pallini.di.uniroma1.it/'
license=(Apache-2.0)
depends=(glibc)
source=(https://debian.stanford.edu/debian/pool/main/n/nauty/nauty_${pkgver}+ds.orig.tar.xz)
sha256sums=('17426fd0f8536ef393580f1c78b2b6fafafae1ee6b69ed9b61cec9b42e59b887')

prepare() {
  cd nauty${pkgver//./_}
  aclocal
  libtoolize -f
  autoreconf -fi
}
 
build() {
  cd nauty${pkgver//./_}

# Workaround build system discarding LDFLAGS
  CFLAGS="$CFLAGS $LDFLAGS" \
  ./configure \
    --prefix=/usr \
    --includedir=/usr/include/nauty \
    --enable-generic
  make
}

package() {
  cd nauty${pkgver//./_}

  make DESTDIR="$pkgdir" install
  mkdir -p "$pkgdir"/usr/share/licenses/nauty
  install COPYRIGHT "$pkgdir"/usr/share/licenses/nauty/LICENSE
}
