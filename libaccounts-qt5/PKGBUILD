# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>

pkgname=libaccounts-qt5
pkgdesc='Qt 5.x based client library for accessing the online accounts database'
pkgver=1.16
pkgrel=3
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://gitlab.com/accounts-sso/libaccounts-qt'
license=(LGPL)
depends=(libaccounts-glib qt5-base)
makedepends=(doxygen)
source=(https://gitlab.com/accounts-sso/libaccounts-qt/-/archive/VERSION_$pkgver/libaccounts-qt-VERSION_$pkgver.tar.gz)
sha256sums=('3e527b151ee87e851b0f027214bda1353d8e48e10a5b54109f1e4b7d422a7de3')

prepare() {
  cd libaccounts-qt-VERSION_$pkgver
  sed -i 's|SUBDIRS  += Accounts tests|SUBDIRS += Accounts|' accounts-qt.pro
}

build() {
  cd libaccounts-qt-VERSION_$pkgver
  qmake PREFIX=/usr LIBDIR=/usr/lib
  make
}

package() {
  cd libaccounts-qt-VERSION_$pkgver
  make INSTALL_ROOT="$pkgdir" install

# Remove docs
  rm -r "$pkgdir"/usr/share
}
