# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=tree-sitter
pkgver=0.20.10
pkgrel=1
pkgdesc='Rust bindings to the Tree-sitter parsing library'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://github.com/tree-sitter/tree-sitter'
license=('MIT')
provides=('libtree-sitter.so')
depends=('gcc-libs')
makedepends=('git' 'rust')
options=('!lto')
source=("git+$url.git#commit=c51896d32dcc11a38e41f36e3deb1a6a9c4f4b14")
b2sums=('SKIP')

prepare() {
  cd $pkgname/lib

  case "${CARCH}" in
    riscv64) MYARCH=riscv64gc ;;
    *)       MYARCH=${CARCH} ;;
  esac
  cargo fetch --locked --target "$MYARCH-unknown-linux-gnu"
}

build() {
  cd $pkgname

  make
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" PREFIX=/usr install

  install -Dm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}

