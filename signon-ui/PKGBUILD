# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=signon-ui
pkgver=0.17+20171022
pkgrel=3.1
_commit=4368bb77d9d1abc2978af514225ba4a42c29a646
pkgdesc='UI component responsible for handling the user interactions which can happen during the login process of an online account'
arch=(x86_64 powerpc64le powerpc riscv64)
url='https://launchpad.net/online-accounts-signon-ui'
license=(GPL)
depends=(libaccounts-qt signond libnotify)
depends_powerpc=(qt5-webkit)
depends_riscv64=(qt5-webkit)
depends_powerpc64le=(qt5-webengine)
depends_x86_64=(qt5-webengine)
makedepends=(git)

# when no qt5-webengine is available revert to before it was mandated
case "${CARCH}" in
  powerpc|riscv64) _commit=79d6d672564012af31cf20f3515ffd64edc787a2 ;;
esac

source=("git+https://gitlab.com/accounts-sso/signon-ui.git#commit=${_commit}"
        fake-user-agent.patch)
sha256sums=('SKIP'
            '5eb7782c6472e51a8107a25324d1d30052bac5d8e9050907cd957c89568fa577')
options=(debug)

prepare() {
  cd $pkgname

  # Do not install tests
  sed -e 's|src \\|src|' -e '/tests/d' -i signon-ui.pro

  case "${CARCH}" in
    powerpc64le|x86_64)
      # Fake user ID to bypass Google blacklist
      patch -p1 -i ../fake-user-agent.patch
      ;;
  esac
}

build() {
  cd $pkgname

  qmake-qt5 PREFIX=/usr LIBDIR=/usr/lib
  make
}

package() {
  cd $pkgname

  make INSTALL_ROOT="$pkgdir" install
}
