# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: schuay <jakob.gruber@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: stonecrest <stonecrest[at]gmail[dot]com>
# Contributor: Chocobo1 <chocobo1 AT archlinux DOT net>

pkgname=mac
pkgver=10.87
_pkgver="${pkgver//./}"
pkgrel=1
pkgdesc='APE codec and decompressor'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://www.monkeysaudio.com/index.html'
license=('BSD-3-Clause')
depends=('glibc' 'gcc-libs')
makedepends=('cmake' 'python-html2text')
source=(
  "$pkgname-$pkgver.zip::https://monkeysaudio.com/files/MAC_${_pkgver}_SDK.zip"
  "$pkgname-$pkgver-license.html::https://www.monkeysaudio.com/license.html"
)
sha512sums=('2e0ef696302d8a066e57a47df9578a922fa07f0b8a916d0bb041b4f558e4824e0cc4d2c3fa5bda2ecf22c5380780301f03af1cb4b01ba5b525b6c2616f9a0197'
            '440ead15e56d37fd0a6985319ceb871166566e7fea7d07f3fc2df49f5bee01627b31647ce35595ae52c5119c22d9465d19cf41c51cc22b2929a7ea3e0ee941d1')
b2sums=('4a4d1946bcb33f742b75f1ad1d21170309ff82016eaa82104035d544c6916b5a0fb8ceff827bafb782aaea07d77c83b94c080426e5ed458e3e9d53f46a584354'
        'f67cb22ad165f06bced85b414f194b44f24b1193535d39bad1f74a1804e73b8fe4b2ecd97c90227070ba7458e9e580d66480f9c49dc3042a91e6106cfad3c8a5')

build() {
  cmake \
    -S . \
    -B build \
    -D CMAKE_BUILD_TYPE='None' \
    -D CMAKE_INSTALL_PREFIX='/usr'

  cmake --build build

  # generate license
  html2text --ignore-links "$pkgname-$pkgver-license.html" \
    | sed -n '/^## License$/,$p' \
    > LICENSE.md
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.md
}
