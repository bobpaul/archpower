# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Roman Kyrylych <roman@gmail.com>

pkgname=ftjam
pkgver=2.5.3rc2
pkgrel=6.1
pkgdesc="FT Jam - an enhanced version of the Jam build tool"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://freetype.org/jam/"
license=('custom')
depends=('glibc')
provides=('jam')
source=("https://ftp.lysator.liu.se/pub/void-ppc-sources/${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.bz2"
        'license.txt'
        ftjam-2.5.3-clang16.patch
        ftjam-2.5.3-false-flags.patch
        ftjam-2.5.3-i-hate-yacc.patch
        ftjam-2.5.3-nostrip.patch
        ftjam-2.5.3rc2-c99.patch)
md5sums=('911cc4191b3e7213905d107962b4529b'
         'ef8ff7da77fe60e25d1e3e740600f1f0'
         '005367b7218451a63f2932c90a922c16'
         '5793839463732989d0103360e53ed728'
         '0eec86fc75aceaba0835e30b75622a56'
         'd340410311008dfcb1111e2dd4c1feb0'
         'd168a8a56ef5d8e169e93a85d157c898')
sha256sums=('a9f8a274049893a4d1f1ad82f7b04f3ebc2554635dd606185e20ba97cb4d64ec'
            '78a6448f2e349e807741fa0abec0724bd35fd63cde0a4f6a46fee4b3ce4568fa'
            'c477e1b6b48d9830a873cde7cc41470320ca112c74bff6f3faf5144d4950d48e'
            'ef95761ea85b103e8665b044b7a99c18e8d1c83ab3bf35e30e2d8f454ed946e8'
            '8d8710e4699b3c910ed8f1ea50f9b1c1a9beaae8aee0c6fc815905666a5ffb81'
            '66af8842f5853d22717dc1613c001aae61eed3c7f5997f16007298e0e7a97f6d'
            'bd3fc16b24a6cfed8a533a5c25728f44cece2665f8a2bbf238eb50b695cce65c')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 -i ${srcdir}/ftjam-2.5.3-clang16.patch
  patch -Np1 -i ${srcdir}/ftjam-2.5.3-false-flags.patch
  patch -Np1 -i ${srcdir}/ftjam-2.5.3-i-hate-yacc.patch
  patch -Np1 -i ${srcdir}/ftjam-2.5.3-nostrip.patch
  patch -Np1 -i ${srcdir}/ftjam-2.5.3rc2-c99.patch
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
	      --build=${CHOST}
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 ../license.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
