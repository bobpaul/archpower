# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Thomas Arnhold <thomas at arnhold dot org>

pkgname=liborcus
pkgver=0.17.2
pkgrel=5
pkgdesc="File import filter library for spreadsheet documents."
arch=(x86_64 powerpc64le powerpc)
url="https://gitlab.com/orcus/orcus/blob/master/README.md"
license=('MPL')
depends=('libixion')
makedepends=('boost' 'mdds')
source=(https://kohei.us/files/orcus/src/${pkgname}-${pkgver}.tar.xz
        liborcus-0.17.2-gcc-13.patch)
# https://gitlab.com/orcus/orcus/-/releases
sha256sums=('30509d414e3cf439e0ae1e0c31b4b0ef5b2bb8e2b2404e3cb7d44a72162640ab'
            '719f60b940cf670c9fb94674f91615cec93cf91b0bd086fed5c0d8c1014a0729')

prepare() {
    cd "${pkgname}"-${pkgver}
    patch -Np1 -i ${srcdir}/liborcus-0.17.2-gcc-13.patch
}

build() {
    cd "${pkgname}"-${pkgver}
    ./configure --prefix=/usr
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
    make
}

check() {
    cd "${pkgname}"-${pkgver}
    make check
}

package() {
    cd "${pkgname}"-${pkgver}
    make DESTDIR="$pkgdir" install
}
