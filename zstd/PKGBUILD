# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Johan Förberg <johan@forberg.se>

pkgname=zstd
pkgver=1.5.1
pkgrel=2.1
pkgdesc='Zstandard - Fast real-time compression algorithm'
url='https://facebook.github.io/zstd/'
arch=(x86_64 powerpc64le powerpc riscv64)
license=(BSD GPL2)
depends=(glibc gcc-libs zlib xz lz4)
makedepends=(gtest)
provides=(libzstd.so)
source=(https://github.com/facebook/zstd/releases/download/v${pkgver}/zstd-${pkgver}.tar.zst{,.sig}
        zstd-pr2857.patch::https://patch-diff.githubusercontent.com/raw/facebook/zstd/pull/2857.patch)
sha256sums=('444f68cf74d2ee6e1d3e53aa99463570bcabf37734f26f86b8aa3fec99446f96'
            'SKIP'
            '5c94d6f67b707b1f9084db0414d39db11a84c5941f5f9dfa0d9be2407f2959bf')
b2sums=('2ab2764ee7b4104916c77bc80dc2ac05e52fdf4909cd730e2f0aa4e2a2c55de098d2527910e2ed9109d514bfc5a3b7cf1237c72e9178d55f9781ec4cd2987876'
        'SKIP'
        'fc74cc9e9e69a42bae2c79b21be214ee3470797353da9733ed85e6c71b0e45feba89061a938d75aaec612cc954fcb93ade20bcb209b5501c4ca23f73dc1ff4cd')
validpgpkeys=(4EF4AC63455FC9F4545D9B7DEF8FE99528B52FFD)

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 -i ${srcdir}/zstd-pr2857.patch
}

build() {
  cd ${pkgname}-${pkgver}
  make
  make -C contrib/pzstd
}

check() {
  cd ${pkgname}-${pkgver}
  make check
  make -C contrib/pzstd test
}

package() {
  cd ${pkgname}-${pkgver}
  make PREFIX=/usr DESTDIR="${pkgdir}" install
  ln -sf /usr/bin/zstd "${pkgdir}/usr/bin/zstdmt"
  install -Dm 755 contrib/pzstd/pzstd -t "${pkgdir}/usr/bin"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
