# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Johan Förberg <johan@forberg.se>

pkgname=zstd
pkgver=1.5.2
pkgrel=3.1
pkgdesc='Zstandard - Fast real-time compression algorithm'
url='https://facebook.github.io/zstd/'
arch=(x86_64 powerpc64le powerpc riscv64)
license=(BSD GPL2)
depends=(glibc gcc-libs zlib xz lz4)
makedepends=(gtest)
provides=(libzstd.so)
options=(debug)
source=(https://github.com/facebook/zstd/releases/download/v${pkgver}/zstd-${pkgver}.tar.zst{,.sig}
        "zstd-3165.patch::https://patch-diff.githubusercontent.com/raw/facebook/zstd/pull/3165.patch"
        "zstd-3167.patch::https://patch-diff.githubusercontent.com/raw/facebook/zstd/pull/3167.patch")
sha256sums=('3ea06164971edec7caa2045a1932d757c1815858e4c2b68c7ef812647535c23f'
            'SKIP'
            'a8043a84f3d409b7c52c75016c55d941e7a9d3d5034660a5e4ca320d0eccff7b'
            '07db9fe01b7164fc7180a52c43d79c1ae4d15abb5669cbf91a5c9c7f357d5f7e')
b2sums=('513e4526a92bcb59416b3457d186a30e554f9e0cf21d7114eb3e9fbcbd9d662c8d95cf0b06237f6fe3f756862c63de0aa146d6a23cb4111c16e6459608d115f1'
        'SKIP'
        'da89701a726478daafc991b118c7c5f9324849d50dfef5abec031e487ced8c2274accd9afa48eccccc35448707eb1ccf00c11168ec430e0fdc6b406bec324216'
        '26e25d98485bacc8295a0add027b86ae8e386a9164c13ffd5497796c8e8da5122f0e9724c0e727e59712ab142ac62cf6fcbb4ae08e35c696874b863eead05ed4')
validpgpkeys=(4EF4AC63455FC9F4545D9B7DEF8FE99528B52FFD)

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 -i ${srcdir}/zstd-3165.patch
  patch -Np1 -i ${srcdir}/zstd-3167.patch
}

build() {
  cd ${pkgname}-${pkgver}
  # Set PREFIX, as libzstd.pc is otherwise generated with -L/usr/local
  make PREFIX=/usr
  make -C contrib/pzstd
}

check() {
  cd ${pkgname}-${pkgver}
  make check
  make -C contrib/pzstd test
}

package() {
  cd ${pkgname}-${pkgver}
  make PREFIX=/usr DESTDIR="${pkgdir}" install
  ln -sf /usr/bin/zstd "${pkgdir}/usr/bin/zstdmt"
  install -Dm 755 contrib/pzstd/pzstd -t "${pkgdir}/usr/bin"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
