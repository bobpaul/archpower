# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: Lucy <lucy@luz.lu>
# Contributor: Bartlomiej Piotrowski <nospam@bpiotrowski.pl>

pkgname=pkgconf
pkgver=1.8.0
pkgrel=1.1
pkgdesc="Package compiler and linker metadata toolkit"
url="https://github.com/pkgconf/pkgconf"
license=(custom:ISC)
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
depends=(glibc sh)
makedepends=(git meson)
provides=(pkg-config pkgconfig libpkgconf.so)
conflicts=(pkg-config)
replaces=(pkg-config)
groups=(base-devel)
_commit=cef30268e1a3f79efd607c26abcf556aa314c9c4  # tags/pkgconf-1.8.0
source=("git+$url#commit=$_commit"
        i686-pc-linux-gnu.personality
        powerpc64{,le}-unknown-linux-gnu.personality
        powerpc-unknown-linux-gnu.personality
        riscv64-unknown-linux-gnu.personality
        x86_64-pc-linux-gnu.personality)
sha256sums=('SKIP'
            '6697c6db7deaae269ea75624a70e80949241f2cf59a537f31ecfcac726d90bc1'
            '45d8b75fd66705de2bb5e87a29a61b684fbf8fd5e60e22fbb7ab1b2864881005'
            '481c1d61538943759db67fe80e34d03f681a36bba0eceaa9b0e40c4b6ef36f76'
            'be1a66bf5a6728fe22a37940f78cdcfc5db6323d821a1d1f3468edd68179b4be'
            'bdcb5b5071233817290b2a3857ab4ab0a1be7b6cf9da74dfc2291756f3216d41'
            'c8297817ba0b57d003878db247ff34b4c47a7594c9f67dcfe8ff8d6567956cd5')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^pkgconf-//;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build -D tests=false
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  case "${CARCH}" in
    x86_64)
      install -Dt "$pkgdir/usr/share/pkgconfig/personality.d" -m644 \
        i686-pc-linux-gnu.personality \
        x86_64-pc-linux-gnu.personality
    ;;
    *)
      install -Dt "$pkgdir/usr/share/pkgconfig/personality.d" -m644 \
        ${CHOST}.personality
    ;;
  esac

  ln -s pkgconf "$pkgdir/usr/bin/i686-pc-linux-gnu-pkg-config"
  ln -s pkgconf "$pkgdir/usr/bin/powerpc64le-unknown-linux-gnu-pkg-config"
  ln -s pkgconf "$pkgdir/usr/bin/powerpc64-unknown-linux-gnu-pkg-config"
  ln -s pkgconf "$pkgdir/usr/bin/powerpc-unknown-linux-gnu-pkg-config"
  ln -s pkgconf "$pkgdir/usr/bin/riscv64-unknown-linux-gnu-pkg-config"
  ln -s pkgconf "$pkgdir/usr/bin/x86_64-pc-linux-gnu-pkg-config"
  ln -s pkgconf "$pkgdir/usr/bin/pkg-config"

  ln -s pkgconf.1 "$pkgdir/usr/share/man/man1/pkg-config.1"

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 $pkgname/COPYING
}

# vim:set sw=2 et:
