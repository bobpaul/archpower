# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=libphonenumber
pkgver=8.12.47
pkgrel=1.1
epoch=1
pkgdesc="Google's common library for parsing, formatting, and validating international phone numbers"
url="https://github.com/googlei18n/libphonenumber"
arch=(x86_64 powerpc64le powerpc riscv64)
license=(Apache)
depends=(icu protobuf boost-libs abseil-cpp)
makedepends=(boost cmake gtest git java-environment)
options=(debug)
_commit=e43c69d31a1d55391f16a31823482293d7f94703  # tags/v8.12.47^0
source=("git+$url#commit=$_commit" absl.diff)
sha256sums=('SKIP'
            '670d40d4d24eca5a32379bfaf696af1879615f0cc7bdc5801bbeb5cb8b961916')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^libphonenumber-\|^v//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname

  # Use our abseil-cpp
  git apply -3 ../absl.diff
}

build() {
  CXXFLAGS+=" -Wno-error=deprecated-declarations"  # readdir_r deprecation
  CXXFLAGS+=" -Wno-error=unused-variable"          # this is nuts
  cmake -S $pkgname/cpp -B build \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  cmake --build build
}

check() {
  cmake --build build --target tests
}

package() {
  depends+=(libicu{uc,i18n}.so libprotobuf.so libboost_thread.so)
  provides+=(libgeocoding.so libphonenumber.so)

  DESTDIR="$pkgdir" cmake --install build
}
