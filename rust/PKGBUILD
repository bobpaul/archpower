# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Alexander F Rødseth <xyproto@archlinux.org>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: userwithuid <userwithuid@gmail.com>

pkgname=('rust' 'rust-docs')
epoch=1
pkgver=1.58.0
pkgrel=1

_llvm_ver=13.0.1

pkgdesc='Systems programming language focused on safety, speed and concurrency'
url='https://www.rust-lang.org/'
arch=(powerpc64le powerpc)
license=('MIT' 'Apache')

makedepends=("llvm=$_llvm_ver" 'libffi' 'perl' 'python'
             'curl' 'cmake'  'ninja' 'rust')
makedepends_powerpc64le=('rust')
makedepends_x86_64=('rust')
checkdepends=('procps-ng' 'gdb')

options=('!emptydirs' '!strip')

source=(
  "https://static.rust-lang.org/dist/rustc-$pkgver-src.tar.gz"{,.asc}
  "https://github.com/llvm/llvm-project/releases/download/llvmorg-$_llvm_ver/compiler-rt-$_llvm_ver.src.tar.xz"{,.sig}
  0001-bootstrap-Change-libexec-dir.patch
  0001-cargo-Change-libexec-dir.patch
  0002-compiler-Change-LLVM-targets.patch
  0003-compiler-Use-wasm-ld-for-wasm-targets.patch
)
sha256sums=('a7c8eeaee85bfcef84c96b02b3171d1e6540d15179ff83dddd9eafba185f85f9'
            'SKIP'
            '7b33955031f9a9c5d63077dedb0f99d77e4e7c996266952c1cec55626dca5dfc'
            'SKIP'
            'a2a8a7c6a5732f79cc92f81d3c8380589c4e2b9a996fb565bbefe42f66b62597'
            '0f8cfbe0387dd62909fe9cb9523223ae1c2319494e2caf5fa4d0592198631b3e'
            '977da945985dce4484ace8794cbc91241b1f3c27311c1a8b201231db6e1d19e5'
            'fcf3c4442ba6a9e59415652ce8514428b2e37b80c5fe7d07fb44259ad37abafb')
validpgpkeys=('108F66205EAEB0AAA8DD5E1C85AB96E6FA1BE5FE'  # Rust Language (Tag and Release Signing Key) <rust-key@rust-lang.org>
              '474E22316ABF4785A88C6E8EA2C794A986419D8A'  # Tom Stellard <tstellar@redhat.com>
              'B6C8F98282B944E3B0D5C2530FC3042E345AD05D') # Hans Wennborg <hans@chromium.org>

prepare() {
  cd "rustc-$pkgver-src"

  # Patch bootstrap and cargo so credential helpers
  # are in /usr/lib instead of /usr/libexec
  patch -d src/tools/cargo -Np1 < ../0001-cargo-Change-libexec-dir.patch
  patch -Np1 -i ../0001-bootstrap-Change-libexec-dir.patch

  # Use our *-pc-linux-gnu targets, making LTO with clang simpler
  patch -Np1 -i ../0002-compiler-Change-LLVM-targets.patch

  # Use our wasm-ld
  patch -Np1 -i ../0003-compiler-Use-wasm-ld-for-wasm-targets.patch

  BOOTSTRAP_PATH="/usr/bin"
  case "${CARCH}" in
    powerpc) 
      BUILD_TOOLS='"cargo", "clippy", "rustfmt", "analysis", "src", "rust-demangler"'
    ;;
    *)
      BUILD_TOOLS='"cargo", "rls", "clippy", "rustfmt", "analysis", "src", "rust-demangler"'
    ;;
  esac

  cat >config.toml <<END
changelog-seen = 2
profile = "user"

[llvm]
link-shared = true

[build]
target = ["${CHOST}"]
cargo = "${BOOTSTRAP_PATH}/cargo"
rustc = "${BOOTSTRAP_PATH}/rustc"
rustfmt = "${BOOTSTRAP_PATH}/rustfmt"
vendor = true
tools = [${BUILD_TOOLS}]
sanitizers = true
profiler = true

[install]
prefix = "/usr"

[rust]
# LLVM crashes when passing an object through ThinLTO twice.  This is triggered
# when using rust code in cross-language LTO if libstd was built using ThinLTO.
# http://blog.llvm.org/2019/09/closing-gap-cross-language-lto-between.html
# https://github.com/rust-lang/rust/issues/54872
codegen-units-std = 1

debuginfo-level-std = 2
channel = "stable"
rpath = false

[target.${CHOST}]
llvm-config = "/usr/bin/llvm-config"
END
}

build() {
  cd "rustc-$pkgver-src"

  export RUST_BACKTRACE=1
  export RUST_COMPILER_RT_ROOT="$srcdir/compiler-rt-$_llvm_ver.src"
  [[ -d $RUST_COMPILER_RT_ROOT ]]

  DESTDIR="$PWD"/dest-rust python ./x.py install -j "$(nproc)"

  # Remove analysis data for libs that weren't installed
  # TODO: Find out where these come from
  local file lib
  while read -rd '' file; do
    lib="${file%.json}.rlib"
    lib="${lib/\/analysis\///lib/}"
    if [[ ! -e $lib ]]; then
      echo "missing '$lib'"
      rm -v "$file"
    fi
  done < <(find "dest-rust/usr/lib/rustlib"  -path '*/analysis/*.json' -print0)

  # move docs and cross targets out of the way for splitting
  mv dest-rust/usr/share/doc dest-doc
}

package_rust() {
  depends=('gcc-libs' 'llvm-libs' 'curl' 'libssh2')
  optdepends=('lldb: rust-lldb script'
              'gdb: rust-gdb script')
  provides=('cargo' 'rustfmt')
  conflicts=('cargo' 'rustfmt')
  replaces=('cargo' 'rustfmt' 'cargo-tree')

  cd "rustc-$pkgver-src"

  cp -a dest-rust/* "$pkgdir"
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE*

  # delete unnecesary files, e.g. components and manifest files only used for the uninstall script
  cd "$pkgdir"/usr/lib/rustlib
  rm components install.log manifest-* rust-installer-version uninstall.sh

  # rustbuild always installs copies of the shared libraries to /usr/lib,
  # overwrite them with symlinks to the per-architecture versions
  ln -srft "$pkgdir"/usr/lib x86_64-unknown-linux-gnu/lib/*.so

  install -d "$pkgdir"/usr/share/bash-completion/
  mv "$pkgdir"/etc/bash_completion.d "$pkgdir"/usr/share/bash-completion/completions
}

package_rust-docs() {
  pkgdesc='Documentation for the Rust programming language'

  cd "rustc-$pkgver-src"
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE*

  install -d "$pkgdir"/usr/share/
  cp -a dest-doc "$pkgdir"/usr/share/doc
}

# vim:set ts=2 sw=2 et:
sha256sums=('0ad531a32f3c2e996b9322c6b7262a9cfe557e49ff3363adea07b575106cd770'
            'SKIP'
            '7b33955031f9a9c5d63077dedb0f99d77e4e7c996266952c1cec55626dca5dfc'
            'SKIP'
            'a2a8a7c6a5732f79cc92f81d3c8380589c4e2b9a996fb565bbefe42f66b62597'
            '0f8cfbe0387dd62909fe9cb9523223ae1c2319494e2caf5fa4d0592198631b3e'
            '977da945985dce4484ace8794cbc91241b1f3c27311c1a8b201231db6e1d19e5'
            'fcf3c4442ba6a9e59415652ce8514428b2e37b80c5fe7d07fb44259ad37abafb')
