# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Contributor: Alexander F Rødseth <xyproto@archlinux.org>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: userwithuid <userwithuid@gmail.com>

pkgname=('rust' 'rust-docs')
epoch=1
pkgver=1.48.0
pkgrel=3

pkgdesc='Systems programming language focused on safety, speed and concurrency'
url='https://www.rust-lang.org/'
arch=(powerpc)
license=('MIT' 'Apache')

makedepends=('rust' 'libffi' 'perl' 'python' 'curl' 'cmake' 'llvm11-libs')
checkdepends=('procps-ng' 'gdb')

options=('!emptydirs' '!strip')

source=("https://static.rust-lang.org/dist/rustc-$pkgver-src.tar.gz"{,.asc}
        0001-Fix-LLVM-build.patch
        0006-test-use-extern-for-plugins-Don-t-assume-multilib.patch
        0007-test-sysroot-crates-are-unstable-Fix-test-when-rpath.patch
        0009-Link-stage2-tools-dynamically-to-libstd.patch
        0016-do-not-copy-libunwind.patch
        force-u32-atomics-cos-we-dont-care-and-tools-does-not-allow-us-to-disable-anything.patch)
sha256sums=('0e763e6db47d5d6f91583284d2f989eacc49b84794d1443355b85c58d67ae43b'
            'SKIP'
            '08616f9acea0967af8fda6eed66854fb47fb618969dea139a5fc52017da43da0'
            '37dd40c028f4f3f79f8ecc7357023f2cf555ad6cfbd9a55c9f1ef61589144acd'
            '95ca814677947e87344b0890fd0d323f8dba567fa390219b92debdca71f14ff2'
            '0883036789d8f8523de8896f9eed5c50d714c67ccbefddb46de0a80be76f3c39'
            'c2394b6ee55374fce587ec03c3ffc5bd684aa67194b1bed4c34a162245b535f4'
            'SKIP')
validpgpkeys=('108F66205EAEB0AAA8DD5E1C85AB96E6FA1BE5FE'  # Rust Language (Tag and Release Signing Key) <rust-key@rust-lang.org>
              '474E22316ABF4785A88C6E8EA2C794A986419D8A'  # Tom Stellard <tstellar@redhat.com>
              'B6C8F98282B944E3B0D5C2530FC3042E345AD05D') # Hans Wennborg <hans@chromium.org>

prepare() {
  cd "rustc-$pkgver-src"
  
  patch -Np1 -i ${srcdir}/0001-Fix-LLVM-build.patch
  patch -Np1 -i ${srcdir}/0006-test-use-extern-for-plugins-Don-t-assume-multilib.patch
  patch -Np1 -i ${srcdir}/0007-test-sysroot-crates-are-unstable-Fix-test-when-rpath.patch
  patch -Np1 -i ${srcdir}/0009-Link-stage2-tools-dynamically-to-libstd.patch
  patch -Np1 -i ${srcdir}/0016-do-not-copy-libunwind.patch
 
  [ "${CARCH}" == 'powerpc' ] &&  \
    patch -Np1 -i ${srcdir}/force-u32-atomics-cos-we-dont-care-and-tools-does-not-allow-us-to-disable-anything.patch
 
  cat >config.toml <<END
[llvm]
link-shared = true

[build]
target = ["${CHOST}"]
tools = ["analysis", "cargo", "clippy", "miri", "rls", "rustfmt", "src"]
cargo = "/usr/bin/cargo"
rustc = "/usr/bin/rustc"
python = "/usr/bin/python"
sanitizers = false
profiler = true
#vendor = true
#full-bootstrap = true
extended = true

[install]
prefix = "/usr"

[rust]
codegen-units = 1

# LLVM crashes when passing an object through ThinLTO twice.  This is triggered when using rust
# code in cross-language LTO if libstd was built using ThinLTO.
# http://blog.llvm.org/2019/09/closing-gap-cross-language-lto-between.html
# https://github.com/rust-lang/rust/issues/54872
codegen-units-std = 1

debuginfo-level-std = 2
debug-assertions = false
debuginfo-level-rustc = 0
debuginfo-level-tests = 0
backtrace = true
parallel-compiler = false
jemalloc = false
llvm-libunwind = false 
codegen-tests = false

channel = "stable"

rpath = false

#[target.${CHOST}]
#llvm-config = "/usr/bin/llvm-config"
END
}

build() {
  cd "rustc-$pkgver-src"

  export RUST_BACKTRACE=1
  #export RUST_COMPILER_RT_ROOT="$srcdir/compiler-rt-$_llvm_ver.src"lsa

  python ./x.py dist ${MAKEFLAGS} 
  DESTDIR="$PWD"/dest-rust python ./x.py install -j "$(nproc)"

  # Remove analysis data for libs that weren't installed
  # TODO: Find out where these come from
  local file lib
  while read -rd '' file; do
    lib="${file%.json}.rlib"
    lib="${lib/\/analysis\///lib/}"
    if [[ ! -e $lib ]]; then
      echo "missing '$lib'"
      rm -v "$file"
    fi
  done < <(find "dest-rust/usr/lib/rustlib"  -path '*/analysis/*.json' -print0)

  # move docs out of the way for splitting
  mv dest-rust/usr/share/doc dest-doc
}

package_rust() {
  depends=('gcc-libs' 'llvm-libs' 'curl' 'libssh2')
  optdepends=('lldb: rust-lldb script'
              'gdb: rust-gdb script')
  provides=('cargo' 'rustfmt')
  conflicts=('cargo' 'rustfmt')
  replaces=('cargo' 'rustfmt' 'cargo-tree')

  cd "rustc-$pkgver-src"

  cp -a dest-rust/* "$pkgdir"
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE*

  # delete unnecesary files, e.g. components and manifest files only used for the uninstall script
  cd "$pkgdir"/usr/lib/rustlib
  rm components install.log manifest-* rust-installer-version uninstall.sh

  # rustbuild always installs copies of the shared libraries to /usr/lib,
  # overwrite them with symlinks to the per-architecture versions
  ln -srft "$pkgdir"/usr/lib ${CHOST}/lib/*.so

  #install -d "$pkgdir"/usr/share/bash-completion
  #mv "$pkgdir"/etc/bash_completion.d/ "$pkgdir"/usr/share/bash-completion/completions/
}

package_rust-docs() {
  description=('Documentation for the Rust programming language')

  cd "rustc-$pkgver-src"
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE*

  install -d "$pkgdir"/usr/share/
  cp -a dest-doc "$pkgdir"/usr/share/doc
}

# vim:set ts=2 sw=2 et:
