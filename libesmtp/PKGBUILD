# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=libesmtp
pkgver=1.1.0
pkgrel=2.1
pkgdesc='A library to manage posting mail to a preconfigured MTA.'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://libesmtp.github.io/'
license=('LGPL')
depends=('openssl')
makedepends=('meson')
source=("https://github.com/libesmtp/libESMTP/archive/v$pkgver/$pkgname-$pkgver.tar.gz"
        libesmtp-1.1.0-fix-build-with-clang16.patch)
sha256sums=('32bc3614ca12d21c7d933f32d43410e8744b6f91fdca7732da9877a385e4e6c3'
            '5b08e3bfb46f2fca4cc5d18a13e6391d2c0d630893066fb7c1b7f96050717ee8')

prepare() {
  cd libESMTP-${pkgver}
  patch -Np1 -i ${srcdir}/libesmtp-1.1.0-fix-build-with-clang16.patch
}

build() {
  arch-meson build libESMTP-${pkgver}
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
}
