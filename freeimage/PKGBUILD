# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Mihai Militaru <mihai.militaru@gmx.com>
# Contributor: scippio <scippio@berounet.cz>

pkgname=freeimage
pkgver=3.18.0
pkgrel=23
pkgdesc="Library project for developers who would like to support popular graphics image formats"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=('GPL' 'custom:FIPL')
url="http://freeimage.sourceforge.net/"
depends=('libjpeg-turbo' 'openexr' 'openjpeg2' 'libwebp' 'libraw' 'libtiff' 'jxrlib')
makedepends=('mesa' 'glu')
source=("https://downloads.sourceforge.net/project/freeimage/Source%20Distribution/${pkgver}/FreeImage${pkgver//./}.zip"
        freeimage-unbundle.patch
        freeimage-libraw-0.20.patch
        freeimage-libraw-0.21.patch
        Fix-big-endian.patch
        Fix-compatibility-with-system-libpng.patch
        Fix-encoding-of-fi-header.patch
        CVE-2019-12211-13.patch
        r1877-improved-DDS-plugin-against-malicious-images.patch
        r1848-improved-PFM-plugin-against-malicious-images.patch
        r1836-improved-BMP-plugin-when-working-with-malicious-images.patch
        r1832-improved-BMP-plugin-when-working-with-malicious-images.patch
        r1830-minor-refactoring.patch)
sha512sums=('9d9cc7e2d57552c3115e277aeb036e0455204d389026b17a3f513da5be1fd595421655488bb1ec2f76faebed66049119ca55e26e2a6d37024b3fb7ef36ad4818'
            'bd96331900e3f13c8830ef59377c840fa36b3c3ef97d1effd6f644acfefb7d618812b268f00f8f051e9d4c978b8dac0c0ab0e6ec7db5aaebf0fa0328913cfaac'
            '5709e4c5baac3505bf2f2498082fbf6b8614e631fec69fc629036c8d033fbe21434a198e9ae24d577dd65928fd31a0d95c584ea4349d74134f7859d4e57b8397'
            'f922af1e000ee83afc87871bbce04076372220174c74a83a6fc3812277f6a0203b631d7f49f250dd78f94015d1a3fb2b72a6b5c4d0a32d4071b197b417d65b8a'
            'aeb5910c59fb26130d8020eb6cc325cfa29fdeb962bd6ff969aa4839551f62d0e06dabc966df6f61000dcbd90367b0c6a46e435ef2745a37f90da9b3a5c16af8'
            '233a9c2a0f812c6dbd036488492150be292efa10733533a7adb2cee2bbe5dee34b3247e0c1b70c707dd7ee0be252766d574647b062f45afdc9def9488b45c21f'
            '243ec85df823a51c160b4ff4a9d141c81c2ceb5de19eb11dd02ea9994a8f3e60d0c960149dc924312b81a61280769dfe4d7747afef55704b8a77561a230e96da'
            '1b94f20684cb2864d509a5b75c6b3f13eef3ca9484f165b977be21dad2a2b875863d1e6de8a8cd97f5984dbba422bf8060449e854803a9857de6980310214af6'
            'a7ca04dee58d2de7c0a808e9b4c5a4c223a031a23e61e79577f46b404c1394d6b935a682938b00f3f69896bce79488b013871aff077784fb0efedf76131310f3'
            '11275fa68c08f697cf7f2a91dc68d10704ae50478480b91c2ab146414a80577bf0b0d7e80026b9c258f52fb88845c24f062d8b37aa86819b684258bf63ceab1d'
            '2e131f21a4e543b7129eee85870c75aa0f4583a1f6f81128d16f08861d2af8966a7f3f05db982663ebf5456b2929dc1d3f9b94c2d4a1bb3045d61ec9072e8413'
            'ad0943ce2b3cb84f3948a140a01c1fcf913fb71381e1443dd36e44add7bc5f71e3d67cbd2e635b255343c15309dc171d49238c0dc3831deea1d5ed7f6435b04f'
            '134ed3a0a39386cbfe5d5e6a11a43797a511a436d23789b9c16cc5e4685eb5fae00a7807ecc695b4b1a5271bdc9a7908c1403f3260ef8ca9eca1a1a367fe7c7b')

prepare() {
  cd FreeImage

  # TODO: This will only compile against libjpeg-turbo 2 but we will wait for that :D
  patch -p1 -i ../freeimage-unbundle.patch # Unbundle libraries (Fedora)
  rm -r Source/Lib* Source/ZLib Source/OpenEXR
  # can't be built due to private headers
  > Source/FreeImage/PluginG3.cpp
  > Source/FreeImageToolkit/JPEGTransform.cpp

  patch -p1 -i ../freeimage-libraw-0.20.patch # Fix build with libraw 0.20
  patch -p1 -i ../freeimage-libraw-0.21.patch # Fix build with libraw 0.21

  patch -p1 -i ../Fix-big-endian.patch
  patch -p1 -i ../Fix-compatibility-with-system-libpng.patch
  patch -p1 -i ../Fix-encoding-of-fi-header.patch
  patch -p1 -i ../CVE-2019-12211-13.patch
  patch -p1 -i ../r1877-improved-DDS-plugin-against-malicious-images.patch
  patch -p1 -i ../r1848-improved-PFM-plugin-against-malicious-images.patch
  patch -p1 -i ../r1836-improved-BMP-plugin-when-working-with-malicious-images.patch
  patch -p1 -i ../r1832-improved-BMP-plugin-when-working-with-malicious-images.patch
  patch -p1 -i ../r1830-minor-refactoring.patch
}

build() {
  case "${CARCH}" in
    powerpc|powerpc64)
      export CFLAGS="-O2 -pipe"
      export CXXFLAGS="-O2 -pipe"
    ;;
  esac

  cd FreeImage
  sh gensrclist.sh
  sh genfipsrclist.sh
  make -f Makefile.gnu
  make -f Makefile.fip
}

package() {
  cd FreeImage
  make -f Makefile.gnu DESTDIR="${pkgdir}" install
  make -f Makefile.fip DESTDIR="${pkgdir}" install

  install -D -m644 "${srcdir}"/FreeImage/license-fi.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
