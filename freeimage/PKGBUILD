# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Mihai Militaru <mihai.militaru@gmx.com>
# Contributor: scippio <scippio@berounet.cz>

pkgname=freeimage
pkgver=3.18.0
pkgrel=21
pkgdesc="Library project for developers who would like to support popular graphics image formats"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=('GPL' 'custom:FIPL')
url="http://freeimage.sourceforge.net/"
depends=('libjpeg-turbo' 'openexr' 'openjpeg2' 'libwebp' 'libraw' 'libtiff' 'jxrlib')
makedepends=('mesa' 'glu' 'dos2unix')
source=("https://downloads.sourceforge.net/project/freeimage/Source%20Distribution/${pkgver}/FreeImage${pkgver//./}.zip"
        freeimage-unbundle.patch
        freeimage-libraw-0.20.patch
        freeimage-libraw-0.21.patch
        freeimage-3.18.0-libjpeg9.patch
        freeimage-3.18.0-null-to-false.patch
        bigendian.patch)
sha512sums=('9d9cc7e2d57552c3115e277aeb036e0455204d389026b17a3f513da5be1fd595421655488bb1ec2f76faebed66049119ca55e26e2a6d37024b3fb7ef36ad4818'
            'bd96331900e3f13c8830ef59377c840fa36b3c3ef97d1effd6f644acfefb7d618812b268f00f8f051e9d4c978b8dac0c0ab0e6ec7db5aaebf0fa0328913cfaac'
            '5709e4c5baac3505bf2f2498082fbf6b8614e631fec69fc629036c8d033fbe21434a198e9ae24d577dd65928fd31a0d95c584ea4349d74134f7859d4e57b8397'
            'f922af1e000ee83afc87871bbce04076372220174c74a83a6fc3812277f6a0203b631d7f49f250dd78f94015d1a3fb2b72a6b5c4d0a32d4071b197b417d65b8a'
            '75fc7c4d257d4bd67202c5c91af23f3e990342490c4661ab2219b89c40626703d9ceb5f05dc8d2a912153fbe5b9e07f24566570597f84cec15fbe03cb7d4b69e'
            '0c7ba4969dd718678b87d4a6ad3358b797d2a793831a7e1a29d006e81ea5f7cb4316f456eba05edced65fcb846509a36d5f2e8c487528e1303be346e0564157b'
            '8dfa902c0a80321bd84dae619a3cb5d7f94897c52c230d1550246b7775e2a0f286f6a777ca6b1bf80a28dae4a8689873ad96049f3677b263b8a32e19eb05ea0d')

prepare() {
  cd FreeImage

  # TODO: This will only compile against libjpeg-turbo 2 but we will wait for that :D
  patch -p1 -i ../freeimage-unbundle.patch # Unbundle libraries (Fedora)
  rm -r Source/Lib* Source/ZLib Source/OpenEXR
  # can't be built due to private headers
  > Source/FreeImage/PluginG3.cpp
  > Source/FreeImageToolkit/JPEGTransform.cpp

  patch -p1 -i ../freeimage-libraw-0.20.patch # Fix build with libraw 0.20
  patch -p1 -i ../freeimage-libraw-0.21.patch # Fix build with libraw 0.21
  patch -p1 -i ../freeimage-3.18.0-libjpeg9.patch
  patch -p1 -i ../freeimage-3.18.0-null-to-false.patch

  dos2unix Source/FreeImage/PluginBMP.cpp
  dos2unix Source/FreeImage/PluginDDS.cpp
  patch -p1 -i ../bigendian.patch
}

build() {
  cd FreeImage
  sh gensrclist.sh
  sh genfipsrclist.sh
  make -f Makefile.gnu
  make -f Makefile.fip
}

package() {
  cd FreeImage
  make -f Makefile.gnu DESTDIR="${pkgdir}" install
  make -f Makefile.fip DESTDIR="${pkgdir}" install

  install -D -m644 "${srcdir}"/FreeImage/license-fi.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
