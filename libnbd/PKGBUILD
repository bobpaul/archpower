# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Thomas Wei√üschuh <thomas t-8ch de>
pkgname=libnbd
pkgver=1.21.6
pkgrel=1
pkgdesc="NBD client library in userspace"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://gitlab.com/nbdkit/libnbd"
license=('LGPL-2.1-or-later')
depends=('glibc' 'gnutls' 'libxml2')
makedepends=('perl' 'ocamlbuild' 'ocaml-findlib' 'rust' 'fuse3' 'python' 'go')
optdepends=(
  'fuse3: For nbdfuse support'
  'ocaml: For OCaml bindings'
  'python>=3: For Python bindings'
)
source=(
  "https://download.libguestfs.org/libnbd/${pkgver%.*}-development/libnbd-${pkgver}.tar.gz"{,.sig}
)
sha256sums=('7f104ab113fd71677660aabda556edc1b712584e6f7beb1780994dc50abc7472'
            'SKIP')
validpgpkeys=('F7774FB1AD074A7E8C8767EA91738F73E1B768A0')  # Richard W.M. Jones <rjones@redhat.com>

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -fiv
}

build() {
  cd "${pkgname}-${pkgver}"
  export CFLAGS+=" -ffat-lto-objects"

  case "${CARCH}" in
    powerpc|powerpc64)
      _configure_flags=(--disable-golang)
    ;;
  esac

  ./configure --prefix=/usr --sbindir=/usr/bin ${_configure_flags[@]}
  make
}

check() {
  cd "${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install
}
