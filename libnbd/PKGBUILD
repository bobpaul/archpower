# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Thomas Wei√üschuh <thomas t-8ch de>
pkgname=libnbd
pkgver=1.21.3
pkgrel=1
pkgdesc="NBD client library in userspace"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://gitlab.com/nbdkit/libnbd"
license=('LGPL-2.1-or-later')
depends=('glibc' 'gnutls' 'libxml2')
makedepends=('perl' 'ocamlbuild' 'ocaml-findlib' 'rust' 'fuse3' 'python' 'git' 'go')
optdepends=(
  'fuse3: For nbdfuse support'
  'ocaml: For OCaml bindings'
  'python>=3: For Python bindings'
)
source=(
  "git+https://gitlab.com/nbdkit/libnbd.git#tag=v${pkgver}"
)
sha256sums=('7a18e913de790c15a4a7bda4a3512df3b6a1285c6803387b54fb35eb7d566f21')
validpgpkeys=('F7774FB1AD074A7E8C8767EA91738F73E1B768A0')  # Richard W.M. Jones <rjones@redhat.com>

prepare() {
  cd "${pkgname}"
  autoreconf -fiv
}

build() {
  cd "${pkgname}"
  export CFLAGS+=" -ffat-lto-objects"

  case "${CARCH}" in
    powerpc|powerpc64)
      _configure_flags=(--disable-golang)
    ;;
  esac

  ./configure --prefix=/usr --sbindir=/usr/bin ${_configure_flags[@]}
  make
}

check() {
  cd "${pkgname}"
  make check
}

package() {
  cd "${pkgname}"
  make DESTDIR="$pkgdir" install
}
