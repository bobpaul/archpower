# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>
# Contributor: K. Piche <kpiche@rogers.com>

pkgname=r
pkgver=4.2.2
pkgrel=2
pkgdesc='Language and environment for statistical computing and graphics'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(GPL)
url='https://www.r-project.org/'
depends=(lapack libtiff pcre2 perl libxmu pango zip unzip curl icu which)
makedepends=(gcc-fortran tk texlive-core autoconf2.13)
makedepends_powerpc=(jdk-openjdk)
makedepends_powerpc64le=(jdk-openjdk)
makedepends_riscv64=(jdk-openjdk)
makedepends_x86_64=(jdk-openjdk)
optdepends=('tk: tcl/tk interface'
            'texlive-bin: latex sty files'
            'gcc-fortran: needed to compile some CRAN packages'
            'openblas: faster linear algebra')
backup=('etc/R/Makeconf' 'etc/R/Renviron' 'etc/R/ldpaths' 'etc/R/repositories' 'etc/R/javaconf')
options=(!emptydirs)
source=(https://cran.r-project.org/src/base/R-${pkgver%%.*}/R-$pkgver.tar.gz
	r.desktop
	r.png
	R.conf
        R-3.4.1-parallel.patch
        R-3.4.1-rmath-shared.patch
        R-3.6.2-no-gzip-doc.patch
        R-3.6.2-no-LDFLAGS-in-libR-pkg-config.patch
        R-4.2.2-browser-pdf-prefs.patch
        R-4.2.2-isspace-without-readline.patch
        R-4.2.2-parallel-rmath-h-install.patch)
sha256sums=('0ff62b42ec51afa5713caee7c4fde7a0c45940ba39bef8c5c9487fef0c953df5'
            '25b01ea93fa704884b65ba002d44d4e99725bd826997e8c73b6467df9f23c798'
            '1580d06a737951f4f3c903cbd514247d9071fc6868eb9c2de94bb999cc195cb1'
            'b7833166041b06f716b6a79095d27d4abd83549816dc53193213827139eae6ef'
            '7d08b75592f9452b1f92bfe148f9d24fd19ba9163cd16ee2dac753f2c1a891be'
            '76a2d04b3a77884baa3cf84b25920f875e026b73c217c59b06aba2fae634f858'
            '052d4fa4905dc80c1409bade86b2f177788b0d45ed421f45f032f8ac187eec56'
            '5611b1c168ff338a4164302fc265be150700ebbca49a33b55973810fe5519b6e'
            '98ba658e6be56bafc5f831627894c4706406e2e8feb39f6a18e8b54b0ec62f2c'
            '9e2a9d728abfd0a4acc83ac8a287592435472336fd55bc5083fc0b1ac5f75448'
            'f406c35ec38d37ccce109492afc18e02ed8f5adb781af68628801fa40c3f1121')

prepare() {
  cd R-$pkgver

  patch -Np1 -i ${srcdir}/R-3.4.1-parallel.patch
  patch -Np1 -i ${srcdir}/R-3.4.1-rmath-shared.patch
  patch -Np1 -i ${srcdir}/R-3.6.2-no-gzip-doc.patch
  patch -Np1 -i ${srcdir}/R-3.6.2-no-LDFLAGS-in-libR-pkg-config.patch
  patch -Np1 -i ${srcdir}/R-4.2.2-browser-pdf-prefs.patch
  patch -Np1 -i ${srcdir}/R-4.2.2-isspace-without-readline.patch
  patch -Np1 -i ${srcdir}/R-4.2.2-parallel-rmath-h-install.patch

  # set texmf dir correctly in makefile
  sed -i 's|$(rsharedir)/texmf|${datarootdir}/texmf|' share/Makefile.in

  #AT_M4DIR=m4 autoreconf -fi
}

build() {
  cd R-$pkgver
# -ffat-lto-objects is needed for third-party packages shipping static libraries
  CFLAGS+=" -ffat-lto-objects" \
  CXXFLAGS+=" -ffat-lto-objects" \
  ./configure  --prefix=/usr \
               --libdir=/usr/lib \
               --sysconfdir=/etc/R \
               --datarootdir=/usr/share \
               rsharedir=/usr/share/R/ \
               rincludedir=/usr/include/R/ \
               rdocdir=/usr/share/doc/R/ \
               --with-x \
               --enable-R-shlib \
               --enable-memory-profiling \
               --with-lapack \
               --with-blas \
               --disable-R-framework \
               --disable-R-static-lib \
               F77=gfortran \
               LIBnn=lib
  make
  make pdf info

  make -C src/nmath/standalone shared
}

package() {
  cd R-$pkgver
  make DESTDIR="$pkgdir" install install-pdf install-info

# install libRmath.so
  make DESTDIR="$pkgdir" -C src/nmath/standalone install
  #make DESTDIR="$pkgdir" install

# Fixup R wrapper scripts.
  sed -i "s|$pkgdir ||" "${pkgdir}/usr/bin/R"
  rm "$pkgdir"/usr/lib/R/bin/R
  cd "$pkgdir"/usr/lib/R/bin
  ln -s ../../../bin/R

# install some freedesktop.org compatibility
  install -Dm644 "$srcdir"/r.desktop -t "$pkgdir"/usr/share/applications
  install -Dm644 "$srcdir"/r.png -t "$pkgdir"/usr/share/pixmaps

# move the config directory to /etc and create symlinks
  install -d "$pkgdir"/etc/R
  cd "$pkgdir"/usr/lib/R/etc
  for _i in *; do
    mv -f $_i "$pkgdir"/etc/R
    ln -s /etc/R/$_i $_i
  done

# Install ld.so.conf.d file to ensure other applications access the shared lib
  install -Dm644 "$srcdir"/R.conf -t "$pkgdir"/etc/ld.so.conf.d

# Add provides for bundled packages
  for _f in "$pkgdir"/usr/lib/R/library/*/DESCRIPTION; do
    _pkg=$(grep Package: $_f | cut -d' ' -f2 | tr '[:upper:]' '[:lower:]')
    _ver=$(grep Version $_f | cut -d' ' -f2)
    _prov="r-$_pkg=${_ver/-/.}"
    provides+=($_prov)
  done
}
