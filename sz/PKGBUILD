# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=sz
pkgver=2.1.12
pkgrel=3.1
pkgdesc="Modular parametrizable lossy compressor framework for scientific data"
arch=(x86_64 powerpc64le powerpc riscv64)
url="https://szcompressor.org/"
license=(BSD)
depends=(glibc gcc-libs)
makedepends=(cmake gcc-fortran hdf5 netcdf python python-numpy swig)
makedepends_powerpc64le=(openmp)
makedepends_x86_64=(openmp)
source=(https://github.com/szcompressor/SZ/releases/download/v${pkgver}/sz-${pkgver}.tar.gz)
sha256sums=('3712b2cd7170d1511569e48a208f02dfb72ecd7ad053c321e2880b9083e150de')

build() {
  openmp=ON
  case "${CARCH}" in
    powerpc|riscv64) openmp=OFF ;;
  esac

  cmake -B build -S SZ-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -DBUILD_FORTRAN=ON \
    -DBUILD_HDF5_FILTER=ON \
    -DBUILD_NETCDF_READER=ON \
    -DBUILD_OPENMP=${openmp} \
    -DBUILD_PYTHON_WRAPPER=ON
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  install -Dm644 SZ-${pkgver}/copyright-and-BSD-license.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}
  rm "${pkgdir}"/usr/bin/test*
}
