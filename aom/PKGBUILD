# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Chocobo1 <chocobo1 AT archlinux DOT net>

pkgbase=aom
pkgname=(aom aom-docs)
pkgver=3.1.3
pkgrel=1
pkgdesc="Alliance for Open Media video codec"
url="https://aomedia.org/"
arch=(x86_64 powerpc64le powerpc)
license=(BSD custom:PATENTS)
depends=(gcc-libs)
makedepends=(git cmake ninja doxygen)
makedepends_x86_64=(yasm)
_commit=ce9a40ce01ade9d6fea1721c82645804a2f39b00  # tags/v3.1.3^0
source=("git+https://aomedia.googlesource.com/aom#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd $pkgbase
  git describe --tags | sed 's/^v//;s/-errata/.errata/;s/-/+/g'
}

prepare() {
  cd $pkgbase

  # https://bugs.archlinux.org/task/71800
  git cherry-pick -n 31257f59a1df72cbbd1399efb780d13a0e433b16
}

build() {
  cmake -S $pkgbase -B build -G Ninja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=1 \
    -DENABLE_TESTS=0
  cmake --build build
}

package_aom() {
  provides=(libaom.so)

  DESTDIR="$pkgdir" cmake --install build

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 $pkgbase/{LICENSE,PATENTS}
}

package_aom-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mkdir -p "$pkgdir/usr/share/doc"
  cp -r build/docs/html "$pkgdir/usr/share/doc/$pkgbase"

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 $pkgbase/{LICENSE,PATENTS}
}

# vim:set et sw=2:
