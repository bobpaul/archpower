# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Dave Reisner <dreisner@archlinux.org>
# Contributor: Thomas BÃ¤chler <thomas@archlinux.org>

pkgname=mkinitcpio
pkgver=32
pkgrel=2
pkgdesc="Modular initramfs image creation utility"
arch=(any)
url='https://github.com/archlinux/mkinitcpio'
license=('GPL')
depends=('awk' 'mkinitcpio-busybox>=1.19.4-2' 'kmod' 'util-linux>=2.23' 'libarchive' 'coreutils'
         'bash' 'binutils' 'diffutils' 'findutils' 'grep' 'filesystem>=2011.10-1' 'zstd' 'systemd')
makedepends=('asciidoc')
optdepends=('gzip: Use gzip compression for the initramfs image'
            'xz: Use lzma or xz compression for the initramfs image'
            'bzip2: Use bzip2 compression for the initramfs image'
            'lzop: Use lzo compression for the initramfs image'
            'lz4: Use lz4 compression for the initramfs image'
            'mkinitcpio-nfs-utils: Support for root filesystem on NFS')
provides=('initramfs')
backup=('etc/mkinitcpio.conf')
source=("https://sources.archlinux.org/other/$pkgname/$pkgname-$pkgver.tar.gz"{,.sig}
        "fix-strip.patch::https://github.com/archlinux/mkinitcpio/commit/dece596d569ca90af902bf2403227b478b629c03.patch"
        "mkinitcpio-powerpc-support.patch")
install=mkinitcpio.install
sha512sums=('c180bef8af774305e8c9779b3405daaa5be33588bf89c6625a28eac1cde424f447782737d85d810fed4971d38b31086a072293d42aad08398c090f9538b56a4e'
            'SKIP'
            '33bebb544b632066838a13a5d2d5adb09a74be37cee96e44e0f9e71b2ba5f4097d3642f98f299d936141692347504cc95a9cf35fdf444e27d6620871051d7ddc'
            'd822b8a0171e927a053241ac29fb36040caa4c944389d4b8a4f67af1351cad631daffa1cf1a00561c2b2efb491d30ec7e9379cb2cfef34dbc68422c74e782967')
b2sums=('0c29659278e662eb58bc776f99e9579b5f0cf3268175382ecf2d635d54a6a8261ae173e387503a0c20a7492656c703a6699b52e1a6693c85c3211225d3e7abf8'
        'SKIP'
        'b1779f2d2fc0ce51b2ea347058b55845c00eb964d87013310528dcc0de661af9f0a2ef801531d66d17bf807ffe44cbdf7fbc34eb91734bfa973fc9b2fb08399b'
        '56f784c5f4bc0d6d6503b9e158a724999c9c97dd1359f417b45fc17daa8fd38b4aebaa8b8eab930ad9bb720641a1e7ad5c0a37786e9494ddbd771065f15d0a4e')
validpgpkeys=('ECCAC84C1BA08A6CC8E63FBBF22FB1D78A77AEAB'    # Giancarlo Razzolini
              'C100346676634E80C940FB9E9C02FF419FECBE16')   # Morten Linderud

export PATH=$PATH:/usr/bin/core_perl

prepare() {
    cd "${pkgname}-${pkgver}"
    patch -Np1 < "$srcdir/fix-strip.patch"
    patch -Np1 < "$srcdir/mkinitcpio-powerpc-support.patch"
}

check() {
  make -C "$pkgname-$pkgver" check
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}
