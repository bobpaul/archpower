# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=libgexiv2
pkgver=0.14.1
pkgrel=1
pkgdesc='GObject-based wrapper around the Exiv2 library'
url='https://wiki.gnome.org/Projects/gexiv2'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(GPL2)
depends=(exiv2 glib2)
makedepends=(gobject-introspection meson python-gobject vala)
source=("https://gitlab.gnome.org/GNOME/gexiv2/-/archive/gexiv2-$pkgver/gexiv2-gexiv2-$pkgver.tar.bz2"
         https://gitlab.gnome.org/GNOME/gexiv2/-/commit/06adc8fb.patch
         https://gitlab.gnome.org/GNOME/gexiv2/-/commit/b3116376.patch
         https://gitlab.gnome.org/GNOME/gexiv2/-/commit/45b0763a.patch)
b2sums=('c2985185d07ebb3388c6bd735f492efab6d2f023a11a3cb2034b83997862b9fe995a56de6ba3d00b3126bb4fd153afaf68d1090f97247b572b30895b36cde114'
        '631deb48a9844be4dbdbb66a8ed8c866c8d712b581f8483ef24999f179fbf7ecf12b9196efcb5de0197bf586d86ea6f69ab5a5117bbf2de07061360637b5713d'
        'f40be9d6d98ad5ad81d1fd8bf40459bd29521e2a65fb1ed6b696f377fc1aee0c12624ea57369f41ea115d98b634007b3e43e5b618942eb25b1bae9f564405e1b'
        '43fffda741f9c7b160ba6bf01628fc4a94ee71075430c3e91da58833c3fdb942ffaf53fba88aa39fffbbc48bfcd139649fb903c749144c3816e81a64db32667d')

prepare() {
# Fix build with exiv2 0.28
  patch -d gexiv2-gexiv2-$pkgver -p1 < 06adc8fb.patch
  patch -d gexiv2-gexiv2-$pkgver -p1 < 45b0763a.patch
  patch -d gexiv2-gexiv2-$pkgver -p1 < b3116376.patch
}

build() {
  arch-meson gexiv2-gexiv2-$pkgver build -D gtk_doc=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  depends+=(libg{lib,object,io}-2.0.so libexiv2.so)
  provides+=(libgexiv2.so)
  meson install -C build --destdir "$pkgdir"
}
