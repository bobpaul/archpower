# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
pkgname=terraform-provider-helm
pkgver=2.3.0
pkgrel=1
pkgdesc="Terraform provider to utilize Helm"
url="https://github.com/mrparkers/terraform-provider-helm"
arch=(x86_64 powerpc64le powerpc64)
license=("MIT")
makedepends=("go" "git")
makedepends_powerpc64le=("go-pie")
makedepends_x86_64=("go-pie")
source=(
  "$pkgname-$pkgver.tar.gz::https://github.com/hashicorp/terraform-provider-helm/archive/v$pkgver.tar.gz"
)
sha512sums=('8a8482f50ec972750251b05beb4d55ac192d9b4c7219379aae7583959283dfd43c34927f262ac4ed0a722b31d7400be649e57f59c5c0d486ddfbb4d35140ff9a')

build() {
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  if [ "${CARCH}" != "powerpc64" ]; then
	 GOFLAGS="-buildmode=pie"
  fi
  export GOFLAGS="${GOFLAGS} -trimpath -mod=readonly -modcacherw"

  cd $pkgname-$pkgver
  go build .
}

package() {
  cd $pkgname-$pkgver
  install -Dm755 $pkgname "$pkgdir/usr/bin/$pkgname"
}
