# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias [tobias at archlinux.org]

pkgname=mousepad
pkgver=0.5.3
pkgrel=1
pkgdesc="Simple text editor for Xfce"
arch=(x86_64 powerpc64le powerpc)
url="https://www.xfce.org/"
license=('GPL2')
groups=('xfce4-goodies')
depends=('xfconf' 'gtksourceview3' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('intltool' 'perl')
source=(https://archive.xfce.org/src/apps/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('e7208bb9e96a1525d3358a64f9cdd165005078e84e81984a953a2a03491fcaa8')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/xfce4 \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
