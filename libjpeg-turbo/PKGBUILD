# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Simone Sclavi 'Ito' <darkhado@gmail.com>

_name=libjpeg-turbo
pkgname=$_name
pkgver=2.1.5.1
pkgrel=1
pkgdesc="JPEG image codec with accelerated baseline compression and decompression"
url="https://libjpeg-turbo.org/"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(BSD)
depends=(glibc)
makedepends=(cmake ninja 'java-environment=19')
makedepends_x86_64=(nasm)
optdepends=('java-runtime>=19: for TurboJPEG Java wrapper')
provides=(libjpeg libjpeg.so libturbojpeg.so)
source=(https://sourceforge.net/projects/$_name/files/$pkgver/$_name-$pkgver.tar.gz{,.sig}
        libjpeg-turbo-2.1.5-jni-not-required.patch)
sha512sums=('86a7248d064043b26b09755633ef4872a2a6133c9e677a9fe4be6645b2e0fde102cf01e09119967b3b6b85f4cb93f3f7c49ec4973944d5eff99b5b90ce8b0be6'
            'SKIP'
            '41e3d1bfa3ab154627afd20582e5ca6a95df5aa5b581d3a931a31ce921b0b895c2c3a1b0e02c6699180dc2219a60aa2abcf0ddaedccb50980790ee0bf17b22e0')
b2sums=('9583a0a91e45347d651de6bf5192a23bfe4e64cef67cdbb18f5b578b424fc46fc114e5649e93a467906cf047948e43a11b6d17b48c47f879c51cff1a4ef356ff'
        'SKIP'
        '610a9b4fded44c55bc57aad645cc2118b125974f06f933d832a9b598cf340d24c20d76db67d5eeb375a90234efbae8dd46a075dff2ff022dba69ad9f399694b8')
validpgpkeys=('0338C8D8D9FDA62CF9C421BD7EC2DBB6F4DBF434') # The libjpeg-turbo Project (Signing key for official binaries) <information@libjpeg-turbo.org>

prepare() {
  cd $_name-$pkgver

  patch -Np1 -i ../libjpeg-turbo-2.1.5-jni-not-required.patch
}

build() {
  local cmake_options=(
    -B build
    -DCMAKE_INSTALL_PREFIX=/usr
    -DCMAKE_INSTALL_LIBDIR=/usr/lib
    -DCMAKE_BUILD_TYPE=None
    -DENABLE_STATIC=OFF
    -DWITH_JAVA=ON
    -DJAVA_AWT_INCLUDE_PATH=/usr/lib/jvm/java-19-openjdk
    -DWITH_JPEG8=ON
    -G Ninja
    -S $_name-$pkgver
    -W no-dev
  )

  CFLAGS+=" -I/usr/lib/jvm/default/include -I/usr/lib/jvm/default/include/linux" \
  JAVA_HOME=/usr/lib/jvm/default/ \
  cmake "${cmake_options[@]}"
  cmake --build build -v
}

check() {
  ctest --test-dir build --output-on-failure --stop-on-failure -j$(nproc)
}

package() {
  DESTDIR="$pkgdir" cmake --install build -v

  # header required by some dependents
  # https://bugs.archlinux.org/task/24787
  install -vDm 644 $_name-$pkgver/jpegint.h "$pkgdir/usr/include"

  install -vDm 644 $_name-$pkgver/LICENSE.md -t "$pkgdir/usr/share/licenses/$pkgname"
}
