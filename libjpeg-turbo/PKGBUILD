# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Simone Sclavi 'Ito' <darkhado@gmail.com>

pkgname=libjpeg-turbo
pkgver=3.0.2
pkgrel=2
pkgdesc="JPEG image codec with accelerated baseline compression and decompression"
url="https://libjpeg-turbo.org/"
_url="https://github.com/libjpeg-turbo/libjpeg-turbo/"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(
  BSD-3-Clause
  IJG
)
depends=(glibc)
makedepends=(
  cmake
  ninja
)
makedepends_powerpc64le=(
  'java-environment>=19'
)
makedepends_riscv64=(
  'java-environment>=19'
)
makedepends_x86_64=(
  'java-environment>=19'
  nasm
)
optdepends_powerpc64le=('java-runtime>=19: for TurboJPEG Java wrapper')
optdepends_riscv64=('java-runtime>=19: for TurboJPEG Java wrapper')
optdepends_x86_64=('java-runtime>=19: for TurboJPEG Java wrapper')
provides=(
  libjpeg
  libjpeg.so
  libturbojpeg.so
)
source=(
  $_url/releases/download/$pkgver/$pkgname-$pkgver.tar.gz{,.sig}
)
sha512sums=('f5eadda0712feb810a8c3bb2621fda24a4c30574998ce30f423b3ffa25225c7a87cb14b696232bc0270485f422a2853a5c32eafb65bc5eeab1b41d8aeb32ad29'
            'SKIP')
b2sums=('b6eff81657707852ee9ea81099179c6e20914f513fd32b12b1bfbf5233f850f54a1062a98150044aa34476f0c54e42a67c88633a59834a764d3f8bb394f374f0'
        'SKIP')
validpgpkeys=('0338C8D8D9FDA62CF9C421BD7EC2DBB6F4DBF434') # The libjpeg-turbo Project (Signing key for official binaries) <information@libjpeg-turbo.org>

build() {
  local cmake_options=(
    -B build
    -DCMAKE_INSTALL_PREFIX=/usr
    -DCMAKE_INSTALL_LIBDIR=/usr/lib
    -DCMAKE_BUILD_TYPE=None
    -DENABLE_STATIC=OFF
    -DWITH_JPEG8=ON
    -G Ninja
    -S $pkgname-$pkgver
    -W no-dev
  )

  case "${CARCH}" in
    powerpc)
      cmake_options+=(
        -DWITH_SIMD=0
        -DWITH_JAVA=OFF
      )
    ;;
    powerpc64|riscv64)
      cmake_options+=(
        -DWITH_SIMD=1
        -DWITH_JAVA=OFF
      )
    ;;
    *)
      cmake_options+=(
        -DWITH_SIMD=1
        -DWITH_JAVA=ON
      )
    ;;
  esac

  CFLAGS+=" -I/usr/lib/jvm/default/include -I/usr/lib/jvm/default/include/linux" \
  JAVA_HOME=/usr/lib/jvm/default/ \
  cmake "${cmake_options[@]}"
  cmake --build build -v
}

check() {
  ctest --test-dir build --output-on-failure --stop-on-failure -j$(nproc)
}

package() {
  DESTDIR="$pkgdir" cmake --install build -v

  # header required by some dependents
  # https://bugs.archlinux.org/task/24787
  install -vDm 644 $pkgname-$pkgver/jpegint.h "$pkgdir/usr/include"
   
  install -vDm 644 $pkgname-$pkgver/LICENSE.md -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $pkgname-$pkgver/README.ijg -t "$pkgdir/usr/share/licenses/$pkgname/"
}
