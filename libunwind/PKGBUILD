# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: SÃ©bastien Luttringer
# Contributor: Lawrence Lee <valheru@facticius.net>
# Contributor: Phillip Marvin <phillip.marvin@gmail.com>
# Contributor: keystone <phillip.marvin@gmail.com>

pkgname=libunwind
pkgver=1.7.2
pkgrel=1
pkgdesc="Determine and manipulate the call-chain of a program"
url="https://www.nongnu.org/libunwind/"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(GPL)
depends=(
  glibc
  xz
  zlib
)
makedepends=(texlive-binextra)
provides=(
  libunwind-{coredump,ptrace,setjmp,x86_64}.so
  libunwind.so
)
options=(debug)
source=(
  https://github.com/libunwind/libunwind/releases/download/v$pkgver/libunwind-$pkgver.tar.gz{,.asc}
  "${pkgname}-ppc32-ucontext.patch::https://patch-diff.githubusercontent.com/raw/libunwind/libunwind/pull/521.patch"
)
b2sums=('519570a02d06ce4a174ca226941e493499054112de1c92938434e9fb56fabc8446f699a886ea8beee672ac5e28acd03d16169257a43e2ee1bab084fb331ef4cf'
        'SKIP'
        '85d25c52b151de07f5cd8f5e18aec0929fba24c51b2a0b2fa97aec2eb23590b4da6c687e14edac088d8e02c734afea8127ea92c9dc993556889d77461762bdb8')
validpgpkeys=(
  F86EB09F72717426F20D36470A0FF845B7DB3427  # Stephen M. Webb <stephen.webb@bregmasoft.ca>
)

prepare() {
  cd libunwind-$pkgver
  patch -Np1 -i ${srcdir}/${pkgname}-ppc32-ucontext.patch
}

build() {
  local configure_options=(
    --prefix=/usr
    --sysconfdir=/etc
    --localstatedir=/var
    --build=${CHOST}
  )

  cd libunwind-$pkgver
  ./configure "${configure_options[@]}"
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd libunwind-$pkgver
  make check
}

package() {
  cd libunwind-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set sw=2 sts=-1 et:
