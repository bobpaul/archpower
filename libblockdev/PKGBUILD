# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=libblockdev
pkgver=2.26
pkgrel=3
pkgdesc="A library for manipulating block devices"
arch=(x86_64 powerpc64le powerpc riscv64)
url="https://github.com/rhinstaller/libblockdev"
license=('LGPL')
depends=('btrfs-progs' 'dosfstools' 'dmraid' 'gptfdisk' 'libbytesize' 'lvm2' 'mdadm'
         'parted' 'volume_key' 'xfsprogs' 'kmod' 'libyaml')
depends_powerpc64le=('ndctl')
depends_riscv64=('ndctl')
depends_x86_64=('ndctl')
makedepends=('autoconf-archive' 'gobject-introspection' 'systemd' 'python')
source=("$pkgname-$pkgver.tar.gz::https://github.com/rhinstaller/libblockdev/archive/$pkgver-1.tar.gz")
sha512sums=('662f9b6a03933a7192079dd2826f87e8465a5a2d792c8768a4cb241cc8adecf04d7558a082bec7cd83ac9cddb3121362ce9cd5eecc73a91ac3ea517e5911a7ed')

build() {
  cd "$srcdir"/$pkgname-$pkgver-1
  ./autogen.sh

  case "${CARCH}" in
    powerpc) CONFIGURE_FLAGS="--without-nvdimm" ;;
  esac


  ./configure --prefix=/usr --sysconfdir=/etc --without-lvm_dbus ${CONFIGURE_FLAGS}
  make
}

check() {
  cd "$srcdir"/$pkgname-$pkgver-1
  make check
}

package() {
  cd "$srcdir"/$pkgname-$pkgver-1
  make DESTDIR="$pkgdir" install
}
