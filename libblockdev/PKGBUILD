# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=libblockdev
pkgver=2.25
pkgrel=2
pkgdesc="A library for manipulating block devices"
arch=(x86_64 powerpc64le powerpc)
url="https://github.com/rhinstaller/libblockdev"
license=('LGPL')
depends=('btrfs-progs' 'dosfstools' 'dmraid' 'gptfdisk' 'libbytesize' 'lvm2' 'mdadm' 'ndctl'
         'parted' 'volume_key' 'xfsprogs' 'kmod' 'libyaml')
makedepends=('autoconf-archive' 'gobject-introspection' 'systemd' 'python')
source=("$pkgname-$pkgver.tar.gz::https://github.com/rhinstaller/libblockdev/archive/$pkgver-1.tar.gz"
        "$pkgname-$pkgver-pr-623.patch::https://patch-diff.githubusercontent.com/raw/storaged-project/libblockdev/pull/623.patch")
sha512sums=('d78a5a506b2d1c12dc218987ccf422e25ea06b2f71cc4ea39b86d1e23842b29b49832f1dfe6da318a14de94f7e66c0070f3e34443c4feb89e4e8f04c1971aa9e'
            '3f4c545bce2c504d4eddacd25ef281d85c9378fc849fe94b54a3d01233f9fe587a160cb94bd738600b69f656c71ba71ed1c368f24576206c56ccffdbecb0991b')

build() {
  cd "$srcdir"/$pkgname-$pkgver-1

  patch -Np1 -i ${srcdir}/${pkgname}-${pkgver}-pr-623.patch

  ./autogen.sh

  [ "${CARCH}" == 'powerpc' ] && EXTRA_CONFIGURE_FLAGS="--without-nvdimm"

  ./configure --prefix=/usr --sysconfdir=/etc --without-lvm_dbus ${EXTRA_CONFIGURE_FLAGS}
  make
}

check() {
  cd "$srcdir"/$pkgname-$pkgver-1
  make check
}

package() {
  cd "$srcdir"/$pkgname-$pkgver-1
  make DESTDIR="$pkgdir" install
}
