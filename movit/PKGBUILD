# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Evert Vorster <evorster@gmail.com>
# Contributor: Martin Sandsmark <martin.sandsmark@kde.org>

pkgname=movit
pkgver=1.6.3
pkgrel=2
pkgdesc="The modern video toolkit"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://movit.sesse.net/"
license=('GPL')
depends=('fftw' 'libepoxy')
makedepends=('eigen' 'gtest' 'sdl2')
source=(https://movit.sesse.net/$pkgname-$pkgver.tar.gz
       movit-1.6.3-gcc12.patch
       workaround-glsizeiptr-abi-change.patch)
sha256sums=('eb19f109ec99d6050de5267d059c7b351d3d5e39c77d43ca348a95f474a99498'
            'f1d0fc3c4f9693e4fca73fe9d6c2c29d74a3a7e06eda4de3f11fb888bb3f9855'
            '48844a2ee65ae80c14f463a979a0adabf0e4c5f1e84889f6994d718bc136585c')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i $srcdir/movit-1.6.3-gcc12.patch
  patch -Np1 -i $srcdir/workaround-glsizeiptr-abi-change.patch
}

build() {
  cd $pkgname-$pkgver

  case "${CARCH}" in
    powerpc64)
      export CFLAGS='-O2 -pipe'
      export CXXFLAGS='-O2 -pipe'
    ;;
  esac

  CXXFLAGS+=' -std=c++14' \
  ./configure --prefix=/usr
  make GTEST_DIR=/usr/src/googletest
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
}
