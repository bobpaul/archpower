# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
pkgname=arcticfox
pkgver=45.1
pkgrel=1
pkgdesc='A fork of Palemoon with TenFourFox enhancements'
arch=(powerpc powerpc64)
license=(MPL2)
url='https://github.com/rmottola/Arctic-Fox'
depends=(gtk2 libxcb libxt alsa-lib libpulse gconf icu)
makedepends=(zip unzip autoconf2.13 python2 xorgproto python2-setuptools git
             pulseaudio)
provides=('browser')
options=(!debug !lto)
source=("git+https://github.com/rmottola/Arctic-Fox.git#tag=v${pkgver}"
        mozconfig-powerpc{,64}
        powerpc-disable-malloc-static-sizes.patch)
sha256sums=('8cad5365588ad7ad6ca5087a788f59487a6259c687aa69419c55827270892f30'
            '5f4c756aecd1bcca97ff4eb908db16b256585b55163500456155695265f6c360'
            '138b88d5c6c092eed2e7b5b50db498c8f385a9f70f1653b67e737c3afbc89262'
            '040b262c985df9b7d53c7e739124e604b17975ca3c2bee3a99cb14ba39991e24')

prepare() {
  cd Arctic-Fox

  cp ${srcdir}/mozconfig-${CARCH} .mozconfig
  [ ! -z "${MAKEFLAGS}" ] &&
    echo "mk_add_options MOZ_MAKE_FLAGS=\"${MAKEFLAGS}\"" >> .mozconfig

  patch -Np1 -i ${srcdir}/powerpc-disable-malloc-static-sizes.patch
}

build() {
  cd Arctic-Fox 

  #FIXME: build only works the second time around on powerpc
  python2 mach build || python2 mach build
}

package() {
  cd Arctic-Fox
  DESTDIR=${pkgdir} python2 mach install
}

# vim: ts=2 sw=2 et:
