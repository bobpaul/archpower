# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
pkgname=arcticfox
pkgver=40.0
pkgrel=1
pkgdesc='A fork of Palemoon with TenFourFox enhancements'
arch=(powerpc)
license=(MPL2)
url='https://github.com/rmottola/Arctic-Fox'
depends=(gtk2 libxcb libxt alsa-lib libpulse)
makedepends=(zip unzip autoconf2.13 python2 xorgproto python2-psutil python2-setuptools git
             pulseaudio)
provides=('browser')
source=("git+https://github.com/rmottola/Arctic-Fox.git#tag=v${pkgver}"
        mozconfig
        powerpc-disable-malloc-static-sizes.patch)
sha256sums=('SKIP'
            '602e072bf5cc2047d6b0d4714739fce2cb9da2b833a35e534b22be1e62473eb6'
            '91e85670d39c9b58bb0deae45906b86c0fffdc6b440fa1602e3d334cccb6323a')

prepare() {
  cd Arctic-Fox

  cp ${srcdir}/mozconfig .mozconfig
  [ ! -z "${MAKEFLAGS}" ] &&
    echo "mk_add_options MOZ_MAKE_FLAGS=\"${MAKEFLAGS}\"" >> .mozconfig

  patch -Np1 -i ${srcdir}/powerpc-disable-malloc-static-sizes.patch
}

build() {
  cd Arctic-Fox 

  python2 mach build
}

package() {
  cd Arctic-Fox
  DESTDIR=${pkgdir} python2 mach install
}

# vim: ts=2 sw=2 et:
