# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
pkgname=arcticfox
pkgver=27.12.0
pkgrel=1
pkgdesc='A fork of Palemoon'
arch=(x86_64 powerpc)
license=(MPL2)
url='https://github.com/wicknix/Arctic-Fox'
depends=(gtk2 libxcb libxt pulseaudio alsa-lib)
makedepends=(zip unzip autoconf2.13 python2 xorgproto gcc6 python2-psutil python2-setuptools)
provides=('browser')
commit='315800f0dd546fa4830f1567f6b175ed1f5d4c00'
source=("git+https://github.com/wicknix/Arctic-Fox.git#commit=${commit}"
        mozconfig
        powerpc-disable-malloc-static-sizes.patch)
sha256sums=('SKIP'
            '65688f1177d9392c8cbe3a1261dc518693e6d36e55de0532989e13aae9965b05'
            '91e85670d39c9b58bb0deae45906b86c0fffdc6b440fa1602e3d334cccb6323a')

prepare() {
  cd Arctic-Fox

  cp ${srcdir}/mozconfig .mozconfig
  [ ! -z "${MAKEFLAGS}" ] &&
    echo "mk_add_options MOZ_MAKE_FLAGS=\"${MAKEFLAGS}\"" >> .mozconfig

  patch -Np1 -i ${srcdir}/powerpc-disable-malloc-static-sizes.patch
}

build() {
  cd Arctic-Fox 

  python2 mach build
}

package() {
  cd Arctic-Fox
  DESTDIR=${pkgdir} python2 mach install
}

# vim: ts=2 sw=2 et:
