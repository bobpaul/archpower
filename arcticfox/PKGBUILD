# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
pkgname=arcticfox
pkgver=43.1
pkgrel=1
pkgdesc='A fork of Palemoon with TenFourFox enhancements'
arch=(powerpc powerpc64)
license=(MPL2)
url='https://github.com/rmottola/Arctic-Fox'
depends=(gtk2 libxcb libxt alsa-lib libpulse gconf)
makedepends=(zip unzip autoconf2.13 python2 xorgproto python2-psutil python2-setuptools git
             pulseaudio)
provides=('browser')
source=("git+https://github.com/rmottola/Arctic-Fox.git#tag=v${pkgver}"
        mozconfig-powerpc{,64}
        powerpc-disable-malloc-static-sizes.patch)
sha256sums=('SKIP'
            'e68b58821b0506f7467979a6e5d635a10f7b1f7de5e3c428e742a3fa84597eea'
            '0c3b77e0106fe438e0f8f8d91a5fe27e64796b9c1a1632bebc00b2e67bf1df7f'
            '040b262c985df9b7d53c7e739124e604b17975ca3c2bee3a99cb14ba39991e24')

prepare() {
  cd Arctic-Fox

  cp ${srcdir}/mozconfig-${CARCH} .mozconfig
  [ ! -z "${MAKEFLAGS}" ] &&
    echo "mk_add_options MOZ_MAKE_FLAGS=\"${MAKEFLAGS}\"" >> .mozconfig

  patch -Np1 -i ${srcdir}/powerpc-disable-malloc-static-sizes.patch
}

build() {
  cd Arctic-Fox 

  python2 mach build
}

package() {
  cd Arctic-Fox
  DESTDIR=${pkgdir} python2 mach install
}

# vim: ts=2 sw=2 et:
