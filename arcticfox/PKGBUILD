# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
pkgname=arcticfox
pkgver=44.0
pkgrel=1
pkgdesc='A fork of Palemoon with TenFourFox enhancements'
arch=(powerpc powerpc64)
license=(MPL2)
url='https://github.com/rmottola/Arctic-Fox'
depends=(gtk2 libxcb libxt alsa-lib libpulse gconf)
makedepends=(zip unzip autoconf2.13 python2 xorgproto python2-psutil python2-setuptools git
             pulseaudio)
provides=('browser')
options=(!debug)
source=("git+https://github.com/rmottola/Arctic-Fox.git#tag=v${pkgver}"
        mozconfig-powerpc{,64}
        powerpc-disable-malloc-static-sizes.patch)
sha256sums=('5fb86c71fcd80da5412f92139fe0fb5c5bc0c3d07f3ef85ce5dd687914b6951a'
            '269b29cdd41660a9cbb7d613c0bb27b5cc027b47bda28738895f5b53d11367ee'
            '481aeca3241bb4eeec9cf6c427a1bdf749016a787184eba5e057e76cd596fe9b'
            '040b262c985df9b7d53c7e739124e604b17975ca3c2bee3a99cb14ba39991e24')

prepare() {
  cd Arctic-Fox

  # fix hard require yasm
  git revert --mainline 1 --no-commit 1102d049c488e15a07244c8622f2b926f26c7629

  cp ${srcdir}/mozconfig-${CARCH} .mozconfig
  [ ! -z "${MAKEFLAGS}" ] &&
    echo "mk_add_options MOZ_MAKE_FLAGS=\"${MAKEFLAGS}\"" >> .mozconfig

  patch -Np1 -i ${srcdir}/powerpc-disable-malloc-static-sizes.patch
}

build() {
  cd Arctic-Fox 

  python2 mach build
}

package() {
  cd Arctic-Fox
  DESTDIR=${pkgdir} python2 mach install
}

# vim: ts=2 sw=2 et:
