# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: jepaan <jepaan at hotmail dot com>

pkgname=assimp
pkgver=5.2.0
pkgrel=1
pkgdesc="Library to import various well-known 3D model formats in an uniform manner"
arch=(x86_64 powerpc64le powerpc riscv64)
license=('BSD')
depends=('zlib' 'gcc-libs')
makedepends=('cmake' 'ninja')
url='https://assimp.org/'
source=("$pkgname-$pkgver.tar.gz::https://github.com/assimp/assimp/archive/${pkgver}.tar.gz"
	disable-werror.patch
        "$pkgname-$pkgver-byteswap-macros.patch::https://patch-diff.githubusercontent.com/raw/assimp/assimp/pull/4367.patch")
sha512sums=('fe57e528520843747e5299f7f66418f3186a8a581d1b2822f2bdeaa331c6a970cb2ce65b492f7ae60b949d321b342dc9948cebd657727447b35ad8317c058431'
            '3c899b80df3db3e6e336913b0ccd7cf95552c30d378664e3b53c278d72ddf61084f01f642e09be65b39ab83115a4cf0eb9a695d648b7c3151f3466aacc31f811'
            'a3455bea1a4f0f2b5f7dba71b01a58de72419a0449194a9e0577db5f1a8b09b92c36c815f7a64b26293523084a9d1313460bc9d3f6c100d995ab87d89b45c37b')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 -i ${srcdir}/disable-werror.patch
  patch -Np1 -i ${srcdir}/${pkgname}-${pkgver}-byteswap-macros.patch
}

build() {
  cd ${pkgname}-${pkgver}

  cmake \
      -Bbuild \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_BUILD_TYPE=Release \
      -DASSIMP_BUILD_SAMPLES=OFF
  ninja -C build
}

package()  {
  cd ${pkgname}-${pkgver}

  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
