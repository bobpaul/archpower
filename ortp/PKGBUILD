# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=ortp
pkgver=5.1.67
pkgrel=1
pkgdesc='A Real-time Transport Protocol (RTP) library'
arch=(x86_64 powerpc64le powerpc riscv64)
url='https://gitlab.linphone.org/'
license=(GPL3)
depends=(bctoolbox)
makedepends=(cmake doxygen)
source=(https://gitlab.linphone.org/BC/public/$pkgname/-/archive/$pkgver/$pkgname-$pkgver.tar.bz2
        doxygen_disable_warn_as_error.patch )
sha256sums=('9d2f939647dd74658213447ff05ccc492bab60e0d37d16cfca614185208dc19c'
            'bafc82896290eb31a06a397ce6d977354ecff18d742ba503e4bd70520534ac17')

prepare() {
  cd $pkgname-$pkgver
  patch -Np0 -i $srcdir/doxygen_disable_warn_as_error.patch 
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_STATIC=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
