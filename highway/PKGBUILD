# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Daniel Bermond <dbermond@archlinux.org>

pkgname=highway
pkgver=0.15.0
pkgrel=1
pkgdesc='A C++ library for SIMD (Single Instruction, Multiple Data)'
arch=(x86_64 powerpc64le powerpc riscv64)
url='https://github.com/google/highway/'
license=('Apache')
makedepends=('cmake' 'gtest')
source=("https://github.com/google/highway/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        highway-0.15.0-missing-cleanup-includes.patch)
sha256sums=('4bbd4439eae08cf038f1c5cc5d9ebc6a1a50f2c610c13a1483adccacfa24c150'
            '2f9f95b83b2f01279c900856180610b06dc6b4c1b33a83e8a29930a8e2b3af6d')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 -i ${srcdir}/highway-0.15.0-missing-cleanup-includes.patch
}

build() {
    export CXXFLAGS+=' -ffat-lto-objects -DHWY_COMPILE_ALL_ATTAINABLE'
    cmake -B build -S "${pkgname}-${pkgver}" \
        -DCMAKE_BUILD_TYPE:STRING='None' \
        -DCMAKE_INSTALL_PREFIX:PATH='/usr' \
        -DHWY_SYSTEM_GTEST:BOOL='ON' \
        -Wno-dev
    make -C build
}

check() {
    make -C build test
}

package() {
    make -C build DESTDIR="$pkgdir" install
}
