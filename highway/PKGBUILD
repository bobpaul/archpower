# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Daniel Bermond <dbermond@archlinux.org>

pkgname=highway
pkgver=1.0.6
pkgrel=1
pkgdesc='A C++ library for SIMD (Single Instruction, Multiple Data)'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://github.com/google/highway/'
license=('Apache')
depends=('gcc-libs')
makedepends=('cmake' 'git')
makedepends_powerpc64le=('gtest')
makedepends_x86_64=('gtest')
source=("https://github.com/google/highway/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('d89664a045a41d822146e787bceeefbf648cc228ce354f347b18f2b419e57207')

build() {
    case "${CARCH}" in
      powerpc|powerpc64)
        export CXXFLAGS+=' -maltivec -latomic'
        _cmake_options=(
          -DBUILD_TESTING:BOOL='OFF'
          -DHWY_WARNINGS_ARE_ERRORS:BOOL='OFF'
        )
      ;;
      riscv64)
        _cmake_options=(
          -DBUILD_TESTING:BOOL='OFF'
          -DHWY_WARNINGS_ARE_ERRORS:BOOL='OFF'
        )
      ;;
      *)
        _cmake_options=(
          -DWHY_SYSTEM_GTEST:BOOL='ON'
        )
      ;;
    esac

    cmake -B build -S "${pkgname}-${pkgver}" \
        -G 'Unix Makefiles' \
        -DCMAKE_BUILD_TYPE:STRING='None' \
        -DCMAKE_INSTALL_PREFIX:PATH='/usr' \
        -DBUILD_SHARED_LIBS:BOOL='ON' \
        -Wno-dev ${_cmake_options[@]}
    cmake --build build
}

check() {
    ctest --test-dir build --output-on-failure
}

package() {
    DESTDIR="$pkgdir" cmake --install build
}
