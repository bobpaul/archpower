# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Daniel Bermond <dbermond@archlinux.org>

pkgname=highway
pkgver=1.0.7
pkgrel=1
pkgdesc='A C++ library for SIMD (Single Instruction, Multiple Data)'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://github.com/google/highway/'
license=('Apache')
depends=('gcc-libs')
makedepends=('cmake' 'git')
makedepends_powerpc64le=('gtest')
makedepends_x86_64=('gtest')
source=("https://github.com/google/highway/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('5434488108186c170a5e2fca5e3c9b6ef59a1caa4d520b008a9b8be6b8abe6c5')

build() {
    case "${CARCH}" in
      powerpc|powerpc64)
        export CXXFLAGS+=' -maltivec -latomic'
        _cmake_options=(
          -DBUILD_TESTING:BOOL='OFF'
          -DHWY_WARNINGS_ARE_ERRORS:BOOL='OFF'
        )
      ;;
      riscv64)
        _cmake_options=(
          -DBUILD_TESTING:BOOL='OFF'
          -DHWY_WARNINGS_ARE_ERRORS:BOOL='OFF'
        )
      ;;
      *)
        _cmake_options=(
          -DWHY_SYSTEM_GTEST:BOOL='ON'
        )
      ;;
    esac

    cmake -B build -S "${pkgname}-${pkgver}" \
        -G 'Unix Makefiles' \
        -DCMAKE_BUILD_TYPE:STRING='None' \
        -DCMAKE_INSTALL_PREFIX:PATH='/usr' \
        -DBUILD_SHARED_LIBS:BOOL='ON' \
        -Wno-dev ${_cmake_options[@]}
    cmake --build build
}

check() {
    ctest --test-dir build --output-on-failure
}

package() {
    DESTDIR="$pkgdir" cmake --install build
}
