# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Daniel Bermond <dbermond@archlinux.org>

pkgname=highway
pkgver=1.0.2
pkgrel=1
pkgdesc='A C++ library for SIMD (Single Instruction, Multiple Data)'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://github.com/google/highway/'
license=('Apache')
depends=('gcc-libs')
makedepends=('cmake')
makedepends_powerpc64le=('gtest')
makedepends_riscv64=('clang')
makedepends_x86_64=('gtest')
source=("https://github.com/google/highway/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('e8ef71236ac0d97f12d553ec1ffc5b6375d57b5f0b860c7447dd69b6ed1072db')
options=(!distcc)

build() {
    case "${CARCH}" in
      powerpc|powerpc64)
	_cmake_options=(
	  -DBUILD_TESTING:BOOL='OFF'
	)
       ;;
      riscv64)
	export CC=clang;
	export CXX=clang++
	_cmake_options=(
	  -DBUILD_TESTING:BOOL='OFF'
	)
      ;;
      *)
        _cmake_options=(
          -DWHY_SYSTEM_GTEST:BOOL='ON'
        )
    esac

    cmake -B build -S "${pkgname}-${pkgver}" \
        -DCMAKE_BUILD_TYPE:STRING='None' \
        -DCMAKE_INSTALL_PREFIX:PATH='/usr' \
        -DBUILD_SHARED_LIBS:BOOL='ON' \
	-Wno-dev ${_cmake_options}
    make -C build
}

check() {
    make -C build test
}

package() {
    make -C build DESTDIR="$pkgdir" install
}
