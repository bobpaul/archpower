# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jochem Kossen <j.kossen@home.nl>
# Contributor: dorphell <dorphell@archlinux.org>
# Contributor: Thayer Williams <thayer@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=aspell
pkgver=0.60.8
_pkgmajorver=0.60
pkgrel=3
pkgdesc="A spell checker designed to eventually replace Ispell"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="http://aspell.net/"
license=('LGPL')
depends=('gcc-libs' 'ncurses')
optdepends=('perl: to import old dictionaries')
provides=(libaspell.so libpspell.so)
source=(
  "https://github.com/GNUAspell/aspell/archive/rel-$pkgver.tar.gz"
  $pkgname-0.60.8-CVE-2019-25051.patch::https://github.com/gnuaspell/aspell/commit/0718b375425aad8e54e1150313b862e4c6fd324a.patch
)
sha512sums=('e8df8244f3d4f56ac7a1bd30f1f2174cca7c3944c452e0733a63b32e689dd22e493d0e6d1cf4d1edeb809cc3c96108e54b529ba506eba27e9ef65a0853ca959d'
            '529f3f4737d2e19f7571f4c8666b1cd089cc4e9dfdaa52dc468919f01ce9f8f8112d8fe8afda295b3dfb92f5e0c2bbd79bf1ec69f06c163c32eb28f0168ab263')
b2sums=('11df79b79018a7da4920ff33f2c3855d82b88b16f6343d449681f0437ade3d3b585d456e6d02558b76f5357552e006180c24bca3fe8183eae88786968dbe43cd'
        'b181caf27feae7eae908c92496a5f05f7e7f1f1a089c460b757c2f745b97f5339d4c8e13cf851e704cff7952c5dcd5ad0a8496ec1ae45c1e83fb1fa577134c63')

prepare() {
  # fix CVE-2019-25051: https://bugs.archlinux.org/task/71554
  patch -Np1 -d $pkgname-rel-$pkgver -i ../$pkgname-0.60.8-CVE-2019-25051.patch

  cd $pkgname-rel-$pkgver
  PERL_USE_UNSAFE_INC=1 ./autogen
}

build() {
  cd $pkgname-rel-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc

  # prevent excessive overlinking due to libtool
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  depends+=(libncursesw.so)

  cd $pkgname-rel-$pkgver
  make DESTDIR="$pkgdir" install
  ln -s $pkgname-$_pkgmajorver "$pkgdir"/usr/lib/$pkgname
}
