# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Juergen Hoetzel <juergen@archlinux.org>
pkgname=clisp
pkgver=2.50
pkgrel=2
pkgdesc="ANSI Common Lisp interpreter, compiler and debugger"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=('GPL')
url="https://clisp.sourceforge.io/"
depends=('readline' 'libsigsegv' 'ffcall' 'libxcrypt' 'libunistring')
provides=('common-lisp')
makedepends=('git')
options=('!makeflags' '!emptydirs')
_commit=c735dd548ba2365804bcee9c2bdd3c291657e8ae
source=(git+https://gitlab.com/gnu-clisp/clisp#commit=$_commit)
sha512sums=('1707e36069c8c8cce8be6b4fa922607b46c4e91ecd6a3a89cca4a780a7e8f73314bf127160a43d3e7dbdb4314a840ad82e2581ac400aae45a66b37f0720a9c2f')

build() {
  cd $pkgname
  CFLAGS+=" -ffat-lto-objects" \
  ./configure --prefix=/usr --with-readline --with-ffcall --enable-portability src

  cd src
  ./makemake --prefix=/usr --with-readline --with-ffcall --with-dynamic-ffi --enable-portability > Makefile
  make
}

check() {
  cd $pkgname
  make check || :
}

package() {
  cd $pkgname/src
  make DESTDIR="$pkgdir" install
}
