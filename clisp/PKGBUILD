# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Juergen Hoetzel <juergen@archlinux.org>
pkgname=clisp
pkgver=2.50
pkgrel=1
pkgdesc="ANSI Common Lisp interpreter, compiler and debugger"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=('GPL')
url="https://clisp.sourceforge.io/"
depends=('readline' 'libsigsegv' 'ffcall' 'libxcrypt' 'libunistring')
provides=('common-lisp')
makedepends=('git')
options=('!makeflags' '!emptydirs')
_commit=bf72805c4dace982a6d3399ff4e7f7d5e77ab99a
source=(git+https://gitlab.com/gnu-clisp/clisp#commit=$_commit)
md5sums=('SKIP')

build() {
  cd $pkgname
  CFLAGS+=" -ffat-lto-objects" \
  ./configure --prefix=/usr --with-readline --with-ffcall src

  cd src
  ./makemake --prefix=/usr --with-readline --with-ffcall --with-dynamic-ffi > Makefile
  make
}

check() {
  cd $pkgname
  make check || :
}

package() {
  cd $pkgname/src
  make DESTDIR="$pkgdir" install
}
