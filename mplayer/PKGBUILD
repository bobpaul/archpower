# Maintainer: Levente Polyak <anthraxx@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>

pkgbase=mplayer
pkgname=('mplayer' 'mencoder')
pkgver=38542
pkgrel=2
pkgdesc='Media player for Linux'
url='http://www.mplayerhq.hu/'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=('GPL')
makedepends=('xorgproto' 'libxxf86vm' 'libmad' 'libxinerama' 'libmng' 'libxss'
             'smbclient' 'aalib' 'jack' 'libcaca' 'faac' 'faad2' 'lirc' 'libxv'
             'libxvmc' 'enca' 'libdca' 'a52dec' 'libvpx' 'unzip' 'mesa'
             'git' 'mpg123' 'ladspa' 'libcdio' 'zlib'
             'libcdio-paranoia' 'subversion' 'x264' 'rtmpdump'
             'libdvdcss' 'libdvdread' 'libdvdnav' 'ffmpeg' 'giflib'
             'libbs2b' 'fribidi' 'opus' 'libvorbis' 'gsm'
             'libpulse' 'alsa-lib' 'glibc' 'libass' 'libjpeg'
             'libpng' 'libogg' 'libtheora' 'libbluray'
             'xvidcore' 'ncurses'
             'fontconfig' 'freetype2' 'libx11' 'libxext' 'libvdpau' 'libgl'
             'lame' 'desktop-file-utils' 'ttf-font' 'openal' 'speex'
             'libmpeg2')
makedepends_x86_64=('nasm' 'yasm')
options=('!emptydirs' '!lto')
source=(${pkgbase}-${pkgver}::"svn://svn.mplayerhq.hu/mplayer/trunk#revision=${pkgver}"
        mplayer.desktop
        include-samba-4.0.patch
        ffmpeg-7.patch
        add_riscv64_support.patch)
sha512sums=('SKIP'
            'd3c5cbf0035279c6f307e4e225473d7b77f9b56566537a26aa694e68446b9e3240333296da627ad5af83b04cc8f476d1a3f8c05e4cf81cd6e77153feb4ed74bc'
            '9debb8c58b996f6c716c22c86c720bf9dc49b4ee9b76c57519f791667ae1de2cc6f5add878fbf4ac02c6b6fd1865e1bcfa6105e75de4bf7ec596c338ed0cae99'
            '93185451be01fd872c5304f2b4ac2832a91b89397f7d920759ef4681dd43977d50c0207851721d7e8ec00ff3fbef93133aa54e8f6cf3d83190f8ab331e106a16'
            'abca77dfe47e65bc89b3eac3a701c534a05c76b0758db39478593b41f24f7775daafe3414949141d56dca0dda1555d6982519c2aab1090b0d1f4a3ee37bcefe7')

prepare() {
  cd ${pkgbase}-${pkgver}
  patch -p1 < "../include-samba-4.0.patch"
  patch -p0 < "../add_riscv64_support.patch"
  ./version.sh

# Fix build with ffmpeg 7, https://lists.mplayerhq.hu/pipermail/mplayer-dev-eng/2024-April/074138.html
  patch -p0 -i ../ffmpeg-7.patch
}

build() {
  cd ${pkgbase}-${pkgver}
  export CFLAGS="${CFLAGS/-march=x86-64/} -Wno-incompatible-pointer-types -Wno-implicit-function-declaration -Wno-int-conversion"
  export CFLAGS="${CFLAGS/-mtune=generic/}"
  export LDFLAGS="${LDFLAGS/,-O1/}"
  export LDFLAGS="${LDFLAGS/,--sort-common/}"

  case "${CARCH}" in
    riscv64) configure_flags=(--disable-runtime-cpudetection) ;;
    *)       configure_flags=(--enable-runtime-cpudetection) ;;
  esac

  ./configure --prefix=/usr \
    --disable-gui \
    --disable-arts \
    --disable-liblzo \
    --disable-libdv \
    --disable-musepack \
    --disable-esd \
    --disable-mga \
    --disable-ass-internal \
    --disable-cdparanoia \
    --disable-ffmpeg_a \
    --enable-radio \
    --enable-radio-capture \
    --enable-smb \
    --language=all \
    --confdir=/etc/mplayer \
    --extra-cflags="${CFLAGS} ${CPPFLAGS}" \
    --extra-ldflags="${LDFLAGS}" ${configure_flags[@]} || cat config.log
  make
}

package_mplayer() {
  pkgdesc='Media player for Linux'
  backup=('etc/mplayer/codecs.conf'
          'etc/mplayer/input.conf')
  depends=('desktop-file-utils' 'ttf-font' 'enca' 'libxss' 'a52dec' 'libvpx'
           'lirc' 'x264' 'libmng' 'libdca' 'aalib' 'libxinerama' 'smbclient'
           'jack' 'libmad' 'libcaca' 'libxxf86vm' 'faac' 'faad2'
           'libxv' 'libxvmc' 'mpg123' 'libx11' 'libcdio' 'libcdio-paranoia'
           'rtmpdump' 'libdvdread' 'libdvdnav' 'ffmpeg' 'libbs2b' 'alsa-lib'
           'giflib' 'libgif.so' 'glibc' 'libass' 'libass.so' 'zlib' 'libjpeg'
           'libpng' 'libpulse' 'libogg' 'fribidi' 'libtheora' 'libvorbis'
           'libvorbis.so' 'libbluray' 'libbluray.so' 'xvidcore'
           'libxvidcore.so' 'ncurses' 'libncursesw.so' 'fontconfig' 'freetype2'
           'libxext' 'libvdpau' 'libgl' 'openal' 'speex' 'libmpeg2')

  cd ${pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install-mplayer install-mplayer-man

  install -Dm 644 etc/{codecs.conf,input.conf,example.conf} "${pkgdir}/etc/mplayer"
  install -Dm 644 "${srcdir}/mplayer.desktop" -t "${pkgdir}/usr/share/applications"
  install -Dm 644 etc/mplayer256x256.png "${pkgdir}/usr/share/pixmaps/mplayer.png"
}

package_mencoder() {
  pkgdesc='Free command line video decoding, encoding and filtering tool'
  depends=('glibc' 'enca' 'a52dec' 'libvpx' 'x264' 'libx264.so' 'libmng' 'libdca'
           'smbclient' 'libmad' 'faac' 'faad2' 'mpg123' 'libcdio'
           'libcdio-paranoia' 'rtmpdump' 'libdvdread' 'libdvdnav' 'ffmpeg'
           'libbs2b' 'ncurses' 'libncursesw.so' 'zlib' 'alsa-lib' 'fontconfig'
           'freetype2' 'fribidi' 'giflib' 'libgif.so' 'libvorbis'
           'libvorbis.so' 'libpng' 'libjpeg' 'libass' 'libass.so' 'lame'
           'libbluray' 'libbluray.so' 'libtheora' 'xvidcore' 'libxvidcore.so'
           'openal' 'speex' 'libmpeg2')

  make -C ${pkgbase}-${pkgver} DESTDIR="${pkgdir}" install-mencoder install-mencoder-man
  find "${pkgdir}/usr/share/man" -name mplayer.1 -exec rename mplayer.1 mencoder.1 {} +
}

# vim: ts=2 sw=2 et:
