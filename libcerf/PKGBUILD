# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libcerf
epoch=1
pkgver=1.15
pkgrel=1
pkgdesc='Self-contained numeric library that provides an efficient and accurate implementation of complex error functions'
arch=(x86_64 powerpc64le powerpc)
url='https://jugit.fz-juelich.de/mlz/libcerf/'
license=(custom)
depends=(glibc)
makedepends=(cmake)
source=(https://jugit.fz-juelich.de/mlz/libcerf/-/archive/v$pkgver/libcerf-v$pkgver.tar.gz
        pc-install-dir.patch)
sha256sums=('a5d45475e08d431267fd29d6af987a9dd9b6792578ec3feb466d4d21f2844868'
            '7605dfbe028efc37ad63eac35c70260283908c21c51030283576d0439d8b82f7')

prepare() {
  patch -d $pkgname-v$pkgver -p1 < pc-install-dir.patch # Fix pc file install dir
}

build() {
  cmake -B build -S $pkgname-v$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 $pkgname-v$pkgver/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
