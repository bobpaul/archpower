# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>
# Contributor: tobias <tobias@archlinux.org>

pkgname=inkscape
pkgver=1.0.1
_commit=e86c870879310bfb4a9abee3e7fd65aef0408fd7
pkgrel=1
pkgdesc='Professional vector graphics editor'
url='https://inkscape.org/'
license=('GPL' 'LGPL')
arch=(x86_64 powerpc64le powerpc)
makedepends=('cmake' 'boost' 'git')

depends=(
	'aspell'
	'dbus-glib'
	'double-conversion'
	'gc'
	'gdl'
	'graphicsmagick'
	'gsl'
	'gtkmm3'
	'gtkspell3'
	'jemalloc'
	'libcdr'
	'libjpeg-turbo'
	'libvisio'
	'libxslt'
	'poppler-glib'
	'potrace'
	'python'
	'ttf-font'
)

optdepends=(
	'fig2dev: xfig input'
	'gvfs: import clip art'
	'pstoedit: latex formulas'
	'python-lxml: some extensions'
	'python-numpy: some extensions'
	'scour: optimized SVG output, some extensions'
	'texlive-core: latex formulas'
)

#source=("https://media.inkscape.org/dl/resources/file/${pkgname}-${pkgver}.tar.bz2")
source=("git+https://gitlab.com/inkscape/inkscape.git#commit=${_commit}")
sha256sums=('SKIP')

prepare() {
	cd "${srcdir}"
	cd "${pkgname}"

	git cherry-pick eb24388f1730918edd9565d9e5d09340ec0b3b08

	git submodule init
	git submodule update

	sed -e "/-Werror=format/d" -i CMakeScripts/DefineDependsandFlags.cmake
}

build() {
	cd "${srcdir}"
	mkdir -p build
	export PKG_CONFIG_PATH='/usr/lib/imagemagick6/pkgconfig'
	cmake -H${pkgname} -Bbuild \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX:PATH=/usr \
		-DWITH_DBUS=ON \
		-DWITH_JEMALLOC=ON

	cmake --build build
}

package() {
	cd "${srcdir}"
	DESTDIR="${pkgdir}" cmake --build build --target install
}
