# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Giuseppe Borzi <gborzi _AT_ ieee _DOT_ org>

pkgname=openblas
_pkgname=OpenBLAS
pkgver=0.3.21
pkgrel=4
pkgdesc="An optimized BLAS library based on GotoBLAS2 1.13 BSD"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://www.openblas.net/"
license=('BSD')
depends=('gcc-libs')
makedepends=('perl' 'gcc-fortran' 'cblas')
provides=('blas=3.9.0')
conflicts=('blas')
options=(!makeflags)
source=(${_pkgname}-v${pkgver}.tar.gz::https://github.com/xianyi/OpenBLAS/archive/v${pkgver}.tar.gz)
sha512sums=('4625c8e6ccfa9120281fd714d3f6b7c3ba2265470c1be76121d6b25dc3dacb899d26e5d9a417ddc616d23909f1411495aa995ef8d8d6df8511cd5cefbabcb1c5')

build() {
  cd $_pkgname-$pkgver

  case "${CARCH}" in
    powerpc64le) PLATFORM_FLAGS="TARGET=POWER8 DYNAMIC_ARCH=1" ;;
    powerpc64)   PLATFORM_FLAGS="TARGET=PPC970 DYNAMIC_ARCH=1" ;;
    powerpc)     PLATFORM_FLAGS="TARGET=PPCG4" ;;
    riscv64)     PLATFORM_FLAGS="TARGET=RISCV64_GENERIC DYNAMIC_ARCH=1" ;;
    x86_64)      PLATFORM_FLASG="TARGET=CORE2 DYNAMIC_ARCH=1" ;;
  esac

  make NO_STATIC=1 NO_LAPACK=1 NO_LAPACKE=1 NO_CBLAS=1 NO_AFFINITY=1 USE_OPENMP=1 \
       CFLAGS="$CPPFLAGS $CFLAGS" ${PLATFORM_FLAGS} \
       NUM_THREADS=64 MAJOR_VERSION=3 libs
  make NO_STATIC=1 NO_LAPACK=1 NO_LAPACKE=1 NO_CBLAS=1 NO_AFFINITY=1 USE_OPENMP=1 \
       CFLAGS="$CPPFLAGS $CFLAGS" ${PLATFORM_FLAGS} \
       NUM_THREADS=64 MAJOR_VERSION=3 shared

}

check() {
  cd $_pkgname-$pkgver
  # TODO
  #make CFLAGS="$CPPFLAGS $CFLAGS -lcblas" TARGET=CORE2 tests
}

package() {
  cd $_pkgname-$pkgver

  make PREFIX="$pkgdir"/usr NUM_THREADS=64 MAJOR_VERSION=3 install
  rm -f "$pkgdir"/usr/include/cblas.h "$pkgdir"/usr/include/lapack*
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  cd "$pkgdir"/usr/lib/
  ln -s libopenblasp-r$pkgver.so libblas.so
  ln -s libopenblasp-r$pkgver.so libblas.so.3
  sed -i -e "s%$pkgdir%%" "$pkgdir"/usr/lib/cmake/openblas/OpenBLASConfig.cmake
  sed -i -e "s%$pkgdir%%" "$pkgdir"/usr/lib/pkgconfig/openblas.pc
  ln -s openblas.pc "$pkgdir"/usr/lib/pkgconfig/blas.pc

  rmdir "$pkgdir"/usr/bin
}

# vim:set ts=2 sw=2 et:
