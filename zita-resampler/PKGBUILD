# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Philipp Ãœberbacher <murks at lavabit dot com>

pkgbase=zita-resampler
pkgname=(zita-resampler zita-resampler-docs)
pkgver=1.10.1
pkgrel=1
pkgdesc="A C++ library for resampling audio signals"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://kokkinizita.linuxaudio.org/linuxaudio"
license=(GPL3)
makedepends=(gcc-libs glibc libsndfile)
source=($url/downloads/$pkgname-$pkgver.tar.xz
        zita-resampler-1.10.1-makefile.patch)
sha512sums=('ba95286b0744df393194f4f94a8aad62f6c088cf80f6be71482ed55beef6503a9bab6644f2565b69002247611fcf0fb6561fc0072578466a0133311721b43cf5'
            'e592b1634891eb514ca2f3759187a7f78414b27cf2c956959a8edf1a6915d767327ec8ce5e3fb98f1e389296d6e57531e786c58b24e643b7c65aff5f5cf97012')
b2sums=('b8871899b7e6cac9b46df3c45c5bb1d8e9c9edd41bcbc5944098d9c4eee2ff4e4d216f97b3bfa96ac43082b5333de26201d6cba9eaa152efd20e8c5017478391'
        '55acc601fe6683036179001206786d5a899fe8ed2b1c115bfbf819f5bc0f8d5d6196f49168baf08cae6fe5eeb34e0b7e1b11990e6e292fb2df32f411fbd47197')

prepare() {
  cd $pkgname-$pkgver

  patch -Np1 -i ${srcdir}/zita-resampler-1.10.1-makefile.patch
}

build() {
  cd $pkgname-$pkgver

  case "${CARCH}" in
    x86_64) export CXXFLAGS+=' "-DENABLE_SSE2"' ;;
  esac

  # libs
  make -C source
  # create lib link for building apps
  ln -sv lib$pkgname.so.$pkgver source/lib$pkgname.so
  # apps
  make LDFLAGS+=" -L../source" CXXFLAGS+=" -I../source" -C apps
  # cleanup link
  rm -rv source/lib$pkgname.so
}

package_zita-resampler() {
  depends=(gcc-libs glibc)
  optdepends=(
    'libsndfile: for zresample and zretune'
    'zita-resampler-docs: for documentation'
  )
  provides=(libzita-resampler.so)

  for location in $pkgbase-$pkgver/{source,apps}; do
    make DESTDIR="$pkgdir/" PREFIX=/usr SUFFIX="" install -C $location
  done
}

package_zita-resampler-docs() {
  pkgdesc+=" - documentation"

  install -vDm 644 $pkgbase-$pkgver/{AUTHORS,README,docs/*}  -t "$pkgdir/usr/share/doc/$pkgbase/"
}
