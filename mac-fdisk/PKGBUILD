# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
pkgname=mac-fdisk
pkgver=0.1
pkgrel=18
pkgdesc="Partition table editor for Apple partition tables"
arch=(powerpc64 powerpc)
url="https://github.com/glaubitz/mac-fdisk-debian"
license=('GPL')
depends=(glibc)
makedepends=(git)
_commit='bda743065fa2c75a83fec60166bc2e317059ef7a'
source=("${pkgname}::git+https://github.com/glaubitz/mac-fdisk-debian#commit=${_commit}"
	mac-fdisk-dirty-glibc.patch
	mac-fdisk-sbin-to-bin.patch)
sha256sums=('SKIP'
	    'edd663118c6d36f888766a2136d78bc5423eca9e6b77105f39e3c3da7c60b90a'
	    '06e54b18011c7219caa7f11c1ff3e774edea60078c760775559ea4fd0e5aaa83')

prepare() {
	cd "${pkgname}"
	patch -Np1 -i ${srcdir}/mac-fdisk-dirty-glibc.patch
	patch -Np1 -i ${srcdir}/mac-fdisk-sbin-to-bin.patch
}

build() {
	cd "${pkgname}"
	make CFLAGS+=' -fcommon'
}

package() {
	cd "${pkgname}"
	mkdir -p "${pkgdir}/usr/bin"
	make DESTDIR="${pkgdir}/usr" install
}
