# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=genxrdpattern
pkgver=1.1
pkgrel=3
pkgdesc="Uses ObjCryst++ to generate a theoretical XRD pattern"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://github.com/psavery/genXrdPattern"
license=(GPL)
depends=(gcc-libs)
makedepends=(cmake)
source=($pkgname-$pkgver.tar.gz::"https://github.com/psavery/genXrdPattern/archive/$pkgver-linux.tar.gz"
        https://github.com/psavery/genXrdPattern/releases/download/$pkgver-linux/objcryst.tgz
	pointer-nullcheck.patch)
sha256sums=('9bc01dbf637d6520d8e2f5743bb1b46f8319432af5ce2d2a484a36615c1586d3'
            '5523eae9937388260b3e491a1a11a443ab00a0961318ec92eee08f9366a0953f'
	    'b2e983b0923ada70d0fa95e511f11b0e372e7b3bdd30a0da59ab91fa03f02af9')

prepare() {
  sed -e 's|/user/psavery/src/objcryst/Fox/src|.|g' -i objcryst/Fox/src/Fox.dep
  sed -e 's|-march=native||' -i objcryst/ObjCryst/*.mak # Don't use -march=native
  sed -e 's|set(CMAKE_EXE_LINKER_FLAGS \"-static\")|set(CMAKE_EXE_LINKER_FLAGS "$ENV{LDFLAGS}")|' \
    -i genXrdPattern-$pkgver-linux/CMakeLists.txt # Honor system LDFLAGS

  if [ "${CARCH}" != 'x86_64' ]; then
    sed -e 's@-DHAVE_SSE_MATHFUN -DUSE_SSE2@@g' -i objcryst/ObjCryst/rules.mak
  fi

  cd objcryst/newmat
  patch -p0 -i ../../pointer-nullcheck.patch
}

build() {
  cd objcryst/Fox
  make Fox-nogui shared=0 -j1

  cd "$srcdir"
  cmake -B build -S genXrdPattern-$pkgver-linux \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DObjCryst_ROOT="$srcdir"/objcryst
  cmake --build build
}

package() {
  cd build
  install -Dm755 genXrdPattern -t "$pkgdir"/usr/bin
}
