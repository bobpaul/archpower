# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgbase=libwpe
pkgname=(
  libwpe
)
pkgver=1.16.1
pkgrel=1
pkgdesc="General-purpose library for WPE WebKit"
url="https://wpewebkit.org"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(BSD-2-Clause)
depends=(
  gcc-libs
  glibc
  libegl
  libxkbcommon
)
makedepends=(
  clang
  git
  llvm
  mesa
  meson
)
source=("git+https://github.com/WebPlatformForEmbedded/libwpe?signed#tag=$pkgver")
b2sums=('c1bb09bff31a75034028758292c879f1a3a457aa3b6382e30b13904488304cf08087654ded25e395840da64a52507ae109a59a6b004eb07c5294958342c6e7f3')
validpgpkeys=(
  5AA3BC334FD7E3369E7C77B291C559DBE4C9123B  # Adrián Pérez de Castro <aperez@igalia.com>
)

prepare() {
  cd libwpe
}

build() {
  arch-meson libwpe build -D build-docs=false
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_libwpe() {
  provides=(libwpe-1.0.so)

  meson install -C build --destdir "$pkgdir"
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 libwpe/COPYING
}

# vim:set sw=2 sts=-1 et:
