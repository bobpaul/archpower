# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Alexander 'dma147' Mieland <dma147@linux-stats.org>
# Contributor: Simon Morgan <simon@16hz.net>

pkgname=chmlib
pkgver=0.40
pkgrel=7
pkgdesc="Library for dealing with Microsoft ITSS/CHM format files"
arch=(x86_64 powerpc64le powerpc riscv64)
url="http://www.jedrea.com/chmlib/"
license=('LGPL')
depends=('glibc')
source=("http://www.jedrea.com/chmlib/chmlib-0.40.tar.bz2"
        chmlib-0.40-ppc-riscv64.patch)
sha256sums=('3449d64b0cf71578b2c7e3ddc048d4af3661f44a83941ea074a7813f3a59ffa3'
            '82adaf9482afd347ead723cf0432fb14e571422ddb3d91c74119736b2feab688')

prepare() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  patch -Np1 -i ${srcdir}/chmlib-0.40-ppc-riscv64.patch
}

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  ./configure --prefix=/usr \
              --enable-examples=yes \
              --build=${CHOST}
  make
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
}
